000001 IDENTIFICATION DIVISION.
000002 PROGRAM-ID. "RESETTEXT".
000003 ENVIRONMENT DIVISION.
000004 CONFIGURATION SECTION.
000005 REPOSITORY.
000006     CLASS COM AS "*COM"
000007     CLASS COM-EXCEP AS "*COM-EXCEPTION"
000008     .
000009 DATA DIVISION.
000010 WORKING-STORAGE SECTION.
000011 01 CONTROLS-COLLECTION OBJECT REFERENCE COM.
000012 01 ITEM-OBJECT         OBJECT REFERENCE COM.
000013 01 CONTROLS-COUNT      PIC  S9(9) COMP-5.
000014 01 I                   PIC  S9(9) COMP-5.
000015 01 CLEAR-TEXT          PIC  X VALUE SPACE.
000016 01 SCODE-TEMP          PIC  S9(9)  COMP-5.
000017 01 SCODE               PIC  S9(10) BINARY.
000018 LINKAGE SECTION.
000019 01 FORM-OBJECT OBJECT REFERENCE COM.
000020 PROCEDURE DIVISION USING FORM-OBJECT.
000021 DECLARATIVES.
000022 ERR SECTION.
000023     USE AFTER EXCEPTION COM-EXCEP.
000024* エラーを検知したのでSCODEを取得します。
000025         INVOKE EXCEPTION-OBJECT "GET-SCODE" RETURNING SCODE-TEMP.
000026* SCODEは10桁イメージで返されるので転記します。
000027         MOVE SCODE-TEMP TO SCODE.
000028         
000029 END DECLARATIVES.
000030 
000031* 指定されたFormオブジェクトからControlsコレクションを取得します。
000032     MOVE 0 TO SCODE
000033     INVOKE FORM-OBJECT "GET-CONTROLS" RETURNING CONTROLS-COLLECTION.
000034     IF SCODE NOT = 0 THEN
000035         GO TO FAILURE
000036     END-IF
000037
000038* Controlsコレクションからコントロールの数を取得します。
000039     MOVE 0 TO SCODE
000040     INVOKE CONTROLS-COLLECTION "GET-COUNT" RETURNING CONTROLS-COUNT.
000041     IF SCODE NOT = 0 THEN
000042         GO TO FAILURE
000043     END-IF
000044          
000045* Controlsコレクションから順番にひとつずつItem(コントロール)を取得します。
000046     PERFORM VARYING I FROM 1 BY 1 UNTIL I > CONTROLS-COUNT
000047         MOVE 0 TO SCODE
000048         INVOKE CONTROLS-COLLECTION "GET-ITEM" USING I RETURNING ITEM-OBJECT
000049         IF SCODE NOT = 0 THEN
000050             GO TO FAILURE
000051         END-IF
000052* 取得したコントロールの"Text"プロパティに値を設定します。
000053         MOVE 0 TO SCODE
000054         INVOKE ITEM-OBJECT "SET-TEXT" USING CLEAR-TEXT
000055* オブジェクトが"Text"プロパティを実装していない場合はエラーが発生ますが、
000056* ここでは無視します。
000057* Itemオブジェクトを解放します。
000058         SET ITEM-OBJECT TO NULL
000059     END-PERFORM.
000060
000061* Controlsコレクションを解放します。
000062     SET CONTROLS-COLLECTION TO NULL.
000063     
000064     EXIT PROGRAM.
000065     
000066 FAILURE SECTION.
000067* 解放されていないオブジェクトを解放します。
000068     SET ITEM-OBJECT TO NULL.
000069     SET CONTROLS-COLLECTION TO NULL.
000070     EXIT PROGRAM.
000071 
000072 END PROGRAM "RESETTEXT".
000073