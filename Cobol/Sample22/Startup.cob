000100*=================================================================
000200* 「マルチスレッドプログラミング」
000300*     スレッド間でファイル・データを共用してオンラインショッピング
000400*   のアプリケーションを構築します。
000500*
000600*=================================================================
000700*
000800*  ファイル名： Startup.cob
000900*  動作概要  ： オンラインショッピングの開始処理を行います
001000*
001100* Copyright 1999-2015 FUJITSU LIMITED
001200*=================================================================
001300 identification division.
001400 program-id. "HttpExtensionProc".
001500 environment division.
001600 data division.
001700 working-storage section.
001800     copy COBW3.
001900*
002000 01 returnValue                    pic 9(09) comp-5.
002100 01 HTMLFilename                   pic X(40).
002200 01 copyStartPos                   pic 9(05).
002300 01 leftLength                     pic 9(05).
002400 01 オンラインショッピング起動情報 pic 9(1) is external.
002500*----------------------------------------------------------------*
002600* SAFアプリへの移行について                                      *
002700*                                                                *
002800*    SAFアプリに移行する際は、連絡節と手続き部を修正する必要が   *
002900*  あります。インタフェース領域をSAFサブルーチンで使用するため、 *
003000*  SAFサブルーチンとのインタフェースであるCOBW3にそのアドレス    *
003100*  を設定する必要があります。                                    *
003200*                                                                *
003300*  [補足]                                                        *
003400*    SAFアプリの場合は、NSAPI開始から終了までのコメントを外し、  *
003500*  ISAPI開始から終了までをコメント化してください。               *
003600*    SAFアプリのリンク〜実行の詳細については"COBOL Webサブルー   *
003700*  チン使用手引書" を参照してください                            *
003800*                                                                *
003900*----------------------------------------------------------------*
004000*
004100*---------------------   ＩＳＡＰＩ 開始  -----------------------*
004200 linkage section.
004300     copy IsapiCtx.
004400*
004500 procedure division with stdcall linkage using ISAPI-CTX-CNT.
004600*
004700 Auth-Start.
004800*
004900*  ＩＳＡＰＩサブルーチンパラメタ域の初期化
005000     move low-value to COBW3.
005100     move function addr(ISAPI-CTX-CNT) to COBW3-CONTEXT.
005200*---------------------   ＩＳＡＰＩ 終了  -----------------------*
005300*
005400*---------------------   ＮＳＡＰＩ 開始  -----------------------*
005500*linkage section.
005600*01 safpt                         pointer.
005700**
005800* procedure division using safpt.
005900**
006000*Auth-Start.
006100**
006200**  ＮＳＡＰＩサブルーチンパラメタ域の初期化
006300*    move low-value to COBW3.
006400*    move safpt     to COBW3-CONTEXT.
006500*---------------------   ＮＳＡＰＩ 終了  -----------------------*
006600*
006700*=================================================================
006800*  Ｗｅｂサブルーチン作業環境の設定し、Ｗｅｂパラメタを獲得する
006900*=================================================================
007000     call "COBW3_INIT" using COBW3.
007100*=================================================================
007200*  オンラインショッピングの起動
007300*=================================================================
007400     call "オンラインショッピング起動"
007500          returning returnValue
007600*=================================================================
007700*  結果出力用ページの選択
007800*=================================================================
007900     evaluate returnValue
008000       when   zero
008100         move "Startup.html" to HTMLFilename
008200       when   1
008300         move "SysError.html" to HTMLFilename
008400       when   2
008500         move "Opened.html" to HTMLFilename
008600     end-evaluate.
008700*=================================================================
008800* 画面出力処理
008900*   アプリケーションが配置されている物理パスの取得して、出力する
009000*  HTML文書名を完成させる
009100*=================================================================
009200     set COBW3-PHYSICALPATH to true.
009300     call "COBW3_GET_REQUEST_INFO" using COBW3.
009400     if COBW3-STATUS = zero then
009500       move space to COBW3-HTML-FILENAME
009600       string COBW3-REQUEST-INFO(1:COBW3-REQUEST-INFO-LENGTH)
009700              "\"                    delimited by size
009800              HTMLFilename           delimited by space
009900          into COBW3-HTML-FILENAME
010000     end-if.
010100*  HTML文書の出力
010200     call "COBW3_PUT_HTML" using COBW3.
010300*=================================================================
010400*  Ｗｅｂサブルーチン作業領域の解放
010500*=================================================================
010600     call "COBW3_FREE" using COBW3.
010700*
010800 StartOnline-End.
010900*
011000     move 1 to program-status.
011100     exit program.
