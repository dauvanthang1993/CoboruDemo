000100*=================================================================
000200* 「マルチスレッドプログラミング」
000300*     スレッド間でファイル・データを共用してオンラインショッピング
000400*   のアプリケーションを構築します。
000500*
000600*=================================================================
000700*
000800*  ファイル名： OLSPRDGT.cob
000900*  動作概要  ： オンラインショッピングのための、製品データ取得の
001000*               処理をします。
001100*  パラメータ：
001200*     第一引数：製品情報(集団項目)
001300*     復帰値
001400*       成功：０
001500*       失敗：０以外
001600*
001700* Copyright 1999-2015 FUJITSU LIMITED
001800*=================================================================
001900 identification division.
002000  program-id. "製品データ取得".
002100* 製品のデータを製品ファイルから取り出す
002200*   呼出し元：
002300*   パラメータ
002400*     第一引数：
002500*   復帰値
002600*     成功：０
002700*     失敗：０以外
002800 environment division.
002900  input-output section.
003000   file-control.
003100     select 製品情報ファイル assign to sys008
003200      organization is indexed
003300      access mode is random
003400      record key is 製品番号
003500      file status is 製品情報ファイル入出力状態.
003600 data division.
003700  file section.
003800   fd 製品情報ファイル is external.
003900    01 製品情報データ.
004000     02 製品番号              pic x(12).
004100     02 型番              pic x(32).
004200     02 形式              pic x(32).
004300     02 価格              pic 9(09).
004400  working-storage section.
004500   copy user-Log.
004600   copy user-Lock.
004700   01 オンラインショッピング起動情報  pic 9(1) is external.
004800   01 製品情報ファイル入出力状態      pic x(2).
004900 01 メッセージテーブル.
005000   02               pic x(90).
005100   02               pic x(90) VALUE
005200     "無効な製品番号が指定されました。プログラム：製品データ取得".
005300   02               pic x(90).
005400   02               pic x(90) VALUE
005500     "オンラインショッピングが起動されていません。プログラム：製品データ取得".
005600 01 redefines メッセージテーブル.
005700   02 エラーメッセージ pic x(90) occurs 4 times.
005800  linkage section.
005900   copy Product-Info.
006000   01 復帰値              pic 9(9) comp-5.
006100 procedure division using productInfo returning 復帰値.
006200*=================================================================
006300*  作業域を初期化します。
006400*=================================================================
006500     move 0 to 復帰値.
006600     initialize userLog.
006700*=================================================================
006800*  オンラインショッピングの起動状態を確認します。
006900*=================================================================
007000     move "初期化" to lock-key.
007100     move -1 to wait-time.
007200     call "COB_LOCK_DATA" with c linkage
007300                          using by reference lock-key
007400                                by value wait-time
007500                                by reference err-datail
007600                          returning ret-value.
007700*=================================================================
007800*  オンラインショッピングが起動されているなら、在庫情報を取り出す
007900*=================================================================
008000     if オンラインショッピング起動情報 = 1 then
008100       *> productQuantity回数分処理を繰り返す
008200       perform test before
008300           varying productIndex from 1 by 1
008400                   until productIndex > productQuantity
008500*=================================================================
008600*  製品番号をキーに製品情報を取り出す。
008700*=================================================================
008800         move productNumber(productIndex) to 製品番号
008900         read 製品情報ファイル with no lock
009000           invalid key
009100             move 2 to 復帰値      *> 無効キーならエラーとして
009200             set エラー to true    *> ループを脱出
009300             exit perform
009400         end-read
009500         move 型番    to productName(productIndex)
009600         move 形式    to productModel(productIndex)
009700         move 価格    to productPrice(productIndex)
009800       end-perform
009900*=================================================================
010000*  オンラインショッピングが起動されていないなら、エラーとする。
010100*=================================================================
010200     else
010300       move 3 to 復帰値
010400       set エラー to true
010500     end-if.
010600*=================================================================
010700* 呼出し元に復帰する
010800*=================================================================
010900 exit-proc.
011000     call "COB_UNLOCK_DATA" with c linkage
011100                            using by reference lock-key
011200                                  by reference err-datail
011300                            returning ret-value.
011400*=================================================================
011500*  復帰値が０以外ならエラーログを出力します。
011600*=================================================================
011700     if 復帰値 not = 0 then
011800       move エラーメッセージ(復帰値) to 説明
011900       call "COB_REPORT_EVENT" using userLog returning 復帰コード
012000     end-if
012100     exit program.
