000100*=================================================================
000200* 「マルチスレッドプログラミング」
000300*     スレッド間でファイル・データを共用して認証処理を実現します
000400*
000500*=================================================================
000600*
000700*  ファイル名： MTHEND.cob
000800*  動作概要  ： オンラインショッピングを終了するため、顧客情報ファイルを
000900*               クローズします。
001000*
001100* Copyright 1999-2015 FUJITSU LIMITED
001200*=================================================================
001300 identification division.
001400 program-id. "オンラインショッピング停止".
001500 environment division.
001600 input-output section.
001700 file-control.
001800     select 顧客情報ファイル assign to sys006
001900       organization is indexed
002000       access mode  is random
002100       record key   is 顧客番号
002200       file status  is 顧客情報ファイル入出力状態.
002300     select 在庫情報ファイル assign to sys007
002400       organization is indexed
002500       access mode  is random
002600       record key   is 製品番号 of 在庫情報
002700       file status  is 在庫情報ファイル入出力状態.
002800     select 製品情報ファイル assign to sys008
002900       organization is indexed
003000       access mode  is random
003100       record key   is 製品番号 of 製品情報
003200       file status  is 製品情報ファイル入出力状態.
003300     select optional 発注情報ファイル assign to sys009
003400       organization is indexed
003500       access mode  is random
003600       record key   is 発注番号 of 発注情報
003700       file status  is 発注情報ファイル入出力状態.
003800     select optional 発注明細ファイル assign to sys010
003900       organization is indexed
004000       access mode  is dynamic
004100       record key   is 発注番号 of 発注明細 発注製品番号 with duplicates
004200       file status  is 発注明細ファイル入出力状態.
004300 data division.
004400 file section.
004500 fd 顧客情報ファイル is external.
004600 01 顧客情報.
004700   02 顧客番号                pic x(32).
004800   02 暗証番号                pic x(32).
004900 fd 在庫情報ファイル is external.
005000 01 在庫情報.
005100   02 製品番号                pic x(10).
005200   02 在庫数              pic 9(10).
005300 fd 製品情報ファイル is external.
005400 01 製品情報.
005500   02 製品番号                pic x(12).
005600   02 型番                pic x(32).
005700   02 形式                pic x(32).
005800   02 価格                pic 9(09).
005900 fd 発注情報ファイル is external.
006000 01 発注情報.
006100   02 発注番号                pic x(12).
006200   02 発注顧客番号            pic x(32).
006300   02 発注日付                pic x(14).
006400 fd 発注明細ファイル is external.
006500 01 発注明細.
006600   02 発注番号                pic x(12).
006700   02 発注製品番号            pic x(10).
006800   02 発注数              pic 9(09).
006900 working-storage section.
007000   copy user-Log.
007100   copy user-Lock.
007200   01 オンラインショッピング起動情報      pic 9(1) is external.
007300   01 顧客情報ファイル入出力状態          pic x(02).
007400   01 在庫情報ファイル入出力状態          pic x(02).
007500   01 製品情報ファイル入出力状態          pic x(02).
007600   01 発注情報ファイル入出力状態          pic x(02).
007700   01 発注明細ファイル入出力状態          pic x(02).
007800  linkage section.
007900   01 復帰値              pic s9(09) comp-5.
008000 procedure division returning 復帰値.
008100*=================================================================
008200*  作業域を初期化します。
008300*=================================================================
008400     move 0 to 復帰値.
008500     initialize userLog
008600*=================================================================
008700*  オンラインショッピングの起動状態を確認します。
008800*=================================================================
008900     move "初期化" to lock-key.
009000     move -1 to wait-time.
009100     call "COB_LOCK_DATA" with c linkage
009200                          using by reference lock-key
009300                                by value wait-time
009400                                by reference err-datail
009500                          returning ret-value.
009600*=================================================================
009700*  オンラインショッピングが起動済なら、各種ファイルをクローズし、
009800*  オンラインショッピングを停止します。
009900*=================================================================
010000     if オンラインショッピング起動情報 = 1 then
010100       *> 顧客情報ファイルをクローズする
010200       close 顧客情報ファイル
010300       if 顧客情報ファイル入出力状態 not = "00" then
010400         move 1 to 復帰値
010500       end-if
010600*
010700       *> 在庫情報ファイルをクローズする
010800       close 在庫情報ファイル
010900       if 在庫情報ファイル入出力状態 not = "00" then
011000         move 1 to 復帰値
011100       end-if
011200*
011300       *> 製品情報ファイルをクローズする
011400       close 製品情報ファイル
011500       if 製品情報ファイル入出力状態 not = "00" then
011600         move 1 to 復帰値
011700       end-if
011800*
011900       *> 発注情報ファイルをクローズする
012000       close 発注情報ファイル
012100       if 発注情報ファイル入出力状態 not = "00" then
012200         move 1 to 復帰値
012300       end-if
012400*
012500       *> 発注明細ファイルをクローズする
012600       close 発注明細ファイル
012700       if 発注明細ファイル入出力状態 not = "00" then
012800         move 1 to 復帰値
012900       end-if
013000*
013100       if 復帰値 not = 0 then
013200         set エラー to true
013300         move "クローズエラーが発生しました。プログラム
013400-             "：オンラインショッピング停止" to 説明
013500       else
013600         move 0 to オンラインショッピング起動情報
013700       end-if
013800     else
013900*=================================================================
014000*  オンラインショッピングが起動されていないなら、エラーログを
014100*  出力します。
014200*=================================================================
014300       set 警告 to true
014400       move 2 to 復帰値
014500       move "オンラインショッピングが起動されていません。
014600-           "プログラム：オンラインショッピング停止" TO 説明
014700       call "COB_REPORT_EVENT" using userLog returning 復帰コード
014800     end-if.
014900*=================================================================
015000* 呼出し元に復帰します。
015100*=================================================================
015200 exit-proc.
015300     call "COB_UNLOCK_DATA" with c linkage
015400                            using by reference lock-key
015500                                  by reference err-datail
015600                            returning ret-value.
015700     exit program.
