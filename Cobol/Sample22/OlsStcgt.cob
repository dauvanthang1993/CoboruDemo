000100*=================================================================
000200* 「マルチスレッドプログラミング」
000300*     スレッド間でファイル・データを共用してオンラインショッピング
000400*   のアプリケーションを構築します。
000500*
000600*=================================================================
000700*
000800*  ファイル名： OLSSTCGT.cob
000900*  動作概要  ： オンラインショッピングのための、在庫問合わの処理
001000*               をします。
001100*  パラメータ：
001200*     第一引数：在庫情報(集団項目)
001300*     復帰値
001400*       成功：０
001500*       失敗：０以外
001600*
001700* Copyright 1999-2015 FUJITSU LIMITED
001800*=================================================================
001900 identification division.
002000  program-id. "在庫問合わせ".
002100* 商品の在庫を在庫ファイルから取り出す。
002200*   呼出し元：
002300*   パラメータ
002400*     第一引数：商品番号
002500*   復帰値
002600*     成功：０
002700*     失敗：０以外
002800 environment division.
002900 input-output section.
003000 file-control.
003100     select 在庫情報ファイル assign to sys007
003200       organization is indexed
003300       access mode is random
003400       record key is 製品番号
003500       file status is 在庫情報ファイル入出力状態.
003600 data division.
003700 file section.
003800 fd 在庫情報ファイル is external.
003900 01 在庫情報データ.
004000   02 製品番号                pic x(10).
004100   02 在庫数              pic 9(10).
004200 working-storage section.
004300   copy user-Log.
004400   copy user-Lock.
004500 01 オンラインショッピング起動情報    pic 9(1) is external.
004600 01 在庫情報ファイル入出力状態        pic x(2).
004700 01 メッセージテーブル.
004800   02               pic x(90).
004900   02               pic x(90) VALUE
005000     "無効な製品番号が指定されました。プログラム：在庫問合わせ" .
005100   02               pic x(90).
005200   02               pic x(90) VALUE
005300     "オンラインショッピングが起動されていません。プログラム：在庫問合わせ".
005400 01 redefines メッセージテーブル.
005500   02 エラーメッセージ pic x(90) occurs 4 times.
005600 linkage section.
005700   copy Stock-Info.
005800 01 復帰値               pic 9(09) comp-5.
005900 procedure division using stockInfo returning 復帰値.
006000*=================================================================
006100*  作業域を初期化します。
006200*=================================================================
006300     move 0 to 復帰値.
006400     initialize userLog.
006500*=================================================================
006600*  オンラインショッピングの起動状態を確認します。
006700*=================================================================
006800     move "初期化" to lock-key.
006900     move -1 to wait-time.
007000     call "COB_LOCK_DATA" with c linkage
007100                          using by reference lock-key
007200                                by value wait-time
007300                                by reference err-datail
007400                          returning ret-value.
007500*=================================================================
007600*  オンラインショッピング起動済なら、在庫情報を取り出す。
007700*=================================================================
007800     if オンラインショッピング起動情報 = 1 then
007900       *> stockQuantity回数分処理を繰り返す
008000       perform test before
008100           varying stockIndex from 1 by 1
008200                   until stockIndex > stockQuantity or
008300                         エラー
008400*=================================================================
008500*  製品番号をキーに在庫情報を読み込む。レコードロック(fs=99)なら
008600*  成功するまで、読み込みを行う。
008700*=================================================================
008800         move stockProductNumber(stockIndex) to 製品番号
008900         perform test after
009000              until 在庫情報ファイル入出力状態 not = "99"
009100           read 在庫情報ファイル
009200             invalid key           *> 無効キーならエラーとして
009300               move 2 to 復帰値    *> ループを脱出
009400               set エラー to true
009500               exit perform
009600           end-read
009700         end-perform
009800         move 在庫数 to stockProductQuantity(stockIndex)
009900       end-perform
010000*=================================================================
010100*  オンラインショッピング起動されていないなら、エラーとする。
010200*=================================================================
010300     else
010400       set エラー to true
010500       move 4 to 復帰値
010600     end-if.
010700*=================================================================
010800* 呼出し元に復帰する
010900*=================================================================
011000 exit-proc.
011100     call "COB_UNLOCK_DATA" with c linkage
011200                            using by reference lock-key
011300                                  by reference err-datail
011400                            returning ret-value.
011500*=================================================================
011600*  復帰値が０以外ならエラーログを出力します。
011700*=================================================================
011800     if 復帰値 not = 0 then
011900       move エラーメッセージ(復帰値) to 説明
012000       call "COB_REPORT_EVENT" using userLog returning 復帰コード
012100     end-if
012200     exit program.
