000100*=================================================================
000200* 「マルチスレッドプログラミング」
000300*     スレッド間でファイル・データを共用してオンラインショッピング
000400*   のアプリケーションを構築します。
000500*
000600*=================================================================
000700*
000800*  ファイル名： OLSUSRINF.cob
000900*  動作概要  ： オンラインショッピングのための認証処理をする。
001000*  パラメータ：
001100*     第一引数：ユーザＩＤ
001200*     第二引数：パスワード
001300*     復帰値
001400*       成功：０
001500*       失敗：０以外
001600*
001700* Copyright 1999-2015 FUJITSU LIMITED
001800*=================================================================
001900 identification division.
002000 program-id. "顧客情報取得".
002100 environment division.
002200 input-output section.
002300 file-control.
002400     select 顧客情報ファイル assign to sys006
002500       organization is indexed
002600       access mode is random
002700       record key is 顧客番号
002800       file status is 顧客情報ファイル入出力状態.
002900 data division.
003000 file section.
003100 fd 顧客情報ファイル is external.
003200 01 顧客情報.
003300   02 顧客番号                pic x(32).
003400   02 暗証番号                pic x(32).
003500 working-storage section.
003600   copy user-Log.
003700   copy user-Lock.
003800 01 オンラインショッピング起動情報  pic 9(1) is external.
003900 01 顧客情報ファイル入出力状態          pic x(2).
004000 01 メッセージテーブル.
004100   02               pic x(90).
004200   02               pic x(90) VALUE
004300     "顧客番号に誤りがあります。プログラム：顧客情報取得".
004400   02               pic x(90) VALUE
004500     "暗証番号に誤りがあります。プログラム：顧客情報取得".
004600   02               pic x(90) VALUE
004700     "オンラインショッピングが起動されていません。プログラム：顧客情報取得".
004800 01 redefines メッセージテーブル.
004900   02 エラーメッセージ pic x(90) occurs 4 times.
005000 linkage section.
005100   copy User-Info.
005200 01 復帰値                pic 9(09) comp-5.
005300 procedure division using userInfo returning 復帰値.
005400*=================================================================
005500*  作業域を初期化します。
005600*=================================================================
005700     move 0 to 復帰値.
005800     initialize userLog
005900*=================================================================
006000*  オンラインショッピング起動の起動状態を確認します。
006100*=================================================================
006200     move "初期化" to lock-key.
006300     move -1 to wait-time.
006400     call "COB_LOCK_DATA" with c linkage
006500                          using by reference lock-key
006600                                by value wait-time
006700                                by reference err-datail
006800                          returning ret-value.
006900     if オンラインショッピング起動情報 = 1 then
007000*=================================================================
007100*  オンラインショッピング起動の起動されているなら、userIDをキーにレコード
007200*  を読み込み、認証処理を行います。
007300*=================================================================
007400       move userID to 顧客番号
007500       read 顧客情報ファイル with no lock
007600         invalid key
007700           move 2     to 復帰値
007800           set エラー to true
007900         not invalid key
008000           if 暗証番号 not = userPassword
008100             move 3 to 復帰値
008200             set エラー to true
008300           end-if
008400       end-read
008500*=================================================================
008600*  オンラインショッピング起動の起動されていないなら、その旨のコードを設定
008700*  します。
008800*=================================================================
008900     else
009000       move 4     to 復帰値
009100       set エラー to true
009200     end-if.
009300 exit-proc.
009400     call "COB_UNLOCK_DATA" with c linkage
009500                            using by reference lock-key
009600                                  by reference err-datail
009700                            returning ret-value.
009800*=================================================================
009900*  復帰値が０以外ならエラーログを出力します。
010000*=================================================================
010100     if 復帰値 not = 0 then
010200       move エラーメッセージ(復帰値) to 説明
010300       call "COB_REPORT_EVENT" using userLog returning 復帰コード
010400     end-if
010500     exit program.
