000010*=================================================================
000020*「簡易アプリ間通信機能を使用したプログラム」
000030*    簡易アプリ間通信サーバ上の論理宛先を使ってアプリケーション間
000040*  でメッセージのやりとりをします。
000050*
000060*=================================================================
000070*
000080*                               簡易アプリ間通信サーバ
000090*   ┏SAMPLE12━━━━━━━━┓  ┌SERVER1 ───┐  ┌TSUUSHIN─┐
000100*   ┃                        ┃  │              │  │          │
000110*   ┃                        ┃  │┌─────┐│  │          │
000120*   ┃CALL  "COBCI_WRITE"… ───→│ＬＤ１    │┼─→ CALL  "  │
000130*   ┃                        ┃  │├─────┤│  │          │
000140*   ┃CALL  "COBCI_READ" … ←──┼│ＬＤ２    │←─┼ CALL  "  │
000150*   ┃                        ┃  │└─────┘│  │          │
000160*   ┃                        ┃  │              │  │          │
000170*   ┗━━━━━━━━━━━━┛  └───────┘  └─────┘
000180*                                                       LD: 論理宛先
000190*
000200*    上図、太枠部分が本プログラム
000210*
000220*     Copyright 2000-2015 FUJITSU LIMITED
000230*=================================================================
000240 IDENTIFICATION DIVISION.
000250  PROGRAM-ID. TSUUSHIN.
000260*
000270 DATA DIVISION.
000280  WORKING-STORAGE SECTION.
000290      COPY "PRM_REC.CBL".
000300*
000310  01  メッセージ域.
000320      02  通信領域  OCCURS 1 TO 32000 DEPENDING ON メッセージ長.
000330          03              PIC X(1).
000340  01  メッセージ長        PIC 9(5).
000350  01  送信メッセージ１    PIC X(30) VALUE "SENT MESSAGE FROM TSUUSHIN".
000360  01  送信メッセージ２    PIC X(25) VALUE "SENT MESSAGE : PRIORITY=3".
000370*
000380 PROCEDURE DIVISION.
000390*=================================================================
000400**（１）サーバと接続します。
000410*=================================================================
000420      MOVE "SERVER1" TO サーバ名.
000430      CALL "COBCI_OPEN" WITH C LINKAGE
000440           USING BY REFERENCE 状態通知域
000450                 BY REFERENCE サーバ名
000460                 BY REFERENCE サーバ識別子
000470                 BY VALUE     通信予約域.
000480      IF PROGRAM-STATUS = 0 THEN
000490          DISPLAY NC"サーバと接続しました。"
000500          DISPLAY NC"　サーバ名　　：" サーバ名
000510      ELSE
000520          DISPLAY NC"サーバとの接続に失敗しました。"
000530          DISPLAY NC"　サーバ名　　：" サーバ名
000540          DISPLAY NC"　エラーコード：" エラーコード OF 状態通知域
000550          DISPLAY NC"　詳細コード　：" 詳細コード OF 状態通知域
000560          GO TO 処理終了
000570      END-IF.
000580*
000590*=================================================================
000600**（２）メッセージを読み込みます。
000610*=================================================================
000620      MOVE "MYLD1" TO 論理宛先名.
000630      MOVE 32000   TO 受信バッファ長 OF 受信パラメタ.
000640      MOVE 0       TO 受信処理種別 OF 受信パラメタ.
000645      MOVE 0       TO メッセージ長.
000650      CALL "COBCI_READ" WITH C LINKAGE
000660           USING BY REFERENCE 状態通知域
000670                 BY VALUE     サーバ識別子
000680                 BY REFERENCE 論理宛先名
000690                 BY REFERENCE 受信パラメタ
000700                 BY REFERENCE メッセージ域
000710                 BY VALUE     通信予約域.
000720      IF PROGRAM-STATUS = 0 THEN
000730          DISPLAY NC"メッセージを１件読み込みました。"
000740          DISPLAY NC"　論理宛先名　：" 論理宛先名
000750          MOVE 受信メッセージ長 OF 受信パラメタ TO メッセージ長
000760          DISPLAY NC"　受信メッセージ：" メッセージ域
000770      ELSE
000780          DISPLAY NC"メッセージの読み込みに失敗しました。"
000790          DISPLAY NC"　論理宛先名　：" 論理宛先名
000800          DISPLAY NC"　エラーコード：" エラーコード OF 状態通知域
000810          DISPLAY NC"　詳細コード　：" 詳細コード OF 状態通知域
000820      END-IF.
000830*
000840*=================================================================
000850**（３）メッセージを書き込みます。
000860*=================================================================
000870      MOVE 25               TO メッセージ長.
000880      MOVE 送信メッセージ２ TO メッセージ域.
000890      MOVE "MYLD1"          TO 論理宛先名.
000900      MOVE メッセージ長     TO 送信メッセージ長 OF 送信パラメタ.
000910      MOVE 3                TO 送信優先順位 OF 送信パラメタ.
000920      MOVE 0                TO 送信処理種別 OF 送信パラメタ.
000930      CALL "COBCI_WRITE" WITH C LINKAGE
000940           USING BY REFERENCE 状態通知域
000950                 BY VALUE     サーバ識別子
000960                 BY REFERENCE 論理宛先名
000970                 BY REFERENCE 送信パラメタ
000980                 BY REFERENCE メッセージ域
000990                 BY VALUE     通信予約域.
001000      IF PROGRAM-STATUS = 0 THEN
001010          DISPLAY NC"メッセージを１件書き込みました。"
001020          DISPLAY NC"　論理宛先名　：" 論理宛先名
001030      ELSE
001040          DISPLAY NC"メッセージの書き込みに失敗しました。"
001050          DISPLAY NC"　論理宛先名　：" 論理宛先名
001060          DISPLAY NC"　エラーコード：" エラーコード OF 状態通知域
001070          DISPLAY NC"　詳細コード　：" 詳細コード OF 状態通知域
001080      END-IF.
001090*
001100      MOVE 30               TO メッセージ長.
001110      MOVE 送信メッセージ１ TO メッセージ域.
001120      MOVE "MYLD2"          TO 論理宛先名.
001130      MOVE メッセージ長     TO 送信メッセージ長 OF 送信パラメタ.
001140      MOVE 2                TO 送信優先順位 OF 送信パラメタ.
001150      MOVE 0                TO 送信処理種別 OF 送信パラメタ.
001160      CALL "COBCI_WRITE" WITH C LINKAGE
001170           USING BY REFERENCE 状態通知域
001180                 BY VALUE     サーバ識別子
001190                 BY REFERENCE 論理宛先名
001200                 BY REFERENCE 送信パラメタ
001210                 BY REFERENCE メッセージ域
001220                 BY VALUE     通信予約域.
001230      IF PROGRAM-STATUS = 0 THEN
001240          DISPLAY NC"メッセージを１件書き込みました。"
001250          DISPLAY NC"　論理宛先名　：" 論理宛先名
001260      ELSE
001270          DISPLAY NC"メッセージの書き込みに失敗しました。"
001280          DISPLAY NC"　論理宛先名　：" 論理宛先名
001290          DISPLAY NC"　エラーコード：" エラーコード OF 状態通知域
001300          DISPLAY NC"　詳細コード　：" 詳細コード OF 状態通知域
001310      END-IF.
001320*
001330*=================================================================
001340**（４）サーバを切断します。
001350*=================================================================
001360      CALL "COBCI_CLOSE" WITH C LINKAGE
001370           USING BY REFERENCE 状態通知域
001380                 BY VALUE     サーバ識別子
001390                 BY VALUE     通信予約域.
001400      IF PROGRAM-STATUS = 0 THEN
001410          DISPLAY NC"サーバを切断しました。"
001420          DISPLAY NC"　サーバ名　　：" サーバ名
001430      ELSE
001440          DISPLAY NC"サーバの切断に失敗しました。"
001450          DISPLAY NC"　サーバ名　　：" サーバ名
001460          DISPLAY NC"　エラーコード：" エラーコード OF 状態通知域
001470          DISPLAY NC"　詳細コード　：" 詳細コード OF 状態通知域
001480     END-IF.
001490*
001500  処理終了.
001510     EXIT PROGRAM.
001520 END PROGRAM TSUUSHIN.
