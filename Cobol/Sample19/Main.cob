000100*========================================================================
000200*  従業員管理プログラム（永続化）　　　−オブジェクト指向機能サンプル−
000300*
000400*　　　追加，更新，削除等の結果がマスタファイルに保存されますので，前回
000500*　　の処理が終了した時点の状態を初期状態として，再実行することができま
000600*　　す。
000700*
000800*　〇機能
000900*　　１）従業員情報の登録
001000*　　２）従業員情報の削除
001100*  　３）従業員情報の修正
001200*  　４）給与計算及び各従業員毎の給与情報の参照
001300*  　５）職級毎の従業員住所録印刷
001400*
001500*　〇参照クラス
001600*　　１）住所クラス　　　　　（Address-class）
001700*　　２）従業員クラス　　　　（AllMember-class）
001800*　　３）一般従業員クラス　　（Member-class）
001900*　　４）管理者クラス　　　　（Manager-class）
002000*
002100*　〇使用ファイル
002200*　　１）従業員マスタ：SYS006
002300*　　　　　付属の「従業員マスタ」ファイルを指定して下さい。
002400*　　２）住所マスタ　：SYS007
002500*　　　　　付属の「住所マスタ」ファイルを指定して下さい。
002600*
002700*　　※　上記付属ファイルには，予めサンプルの従業員情報が登録されていま
002800*　　　す。
002900*
003000*　〇環境変数
003100*　　　ＰＦ２及びＰＦ３キーを有効とするために，環境変数情報として以下を
003200*　　指定してください。
003300*
003400*  　　  TERMINATOR=!PF1,PF2-PF3,!PF4-PF12
003500*
003600*                              Copyright 1997-2015 FUJITSU LIMITED
003700*========================================================================
003800 PROGRAM-ID.   Main.
003900 ENVIRONMENT  DIVISION.
004000 CONFIGURATION  SECTION.
004100   REPOSITORY.
004200      CLASS  Address-class
004300      CLASS  Member-class
004400      CLASS  AllMember-class
004500      CLASS  Manager-class.
004600*
004700   SPECIAL-NAMES.
004800        CRT  STATUS  IS  画面入力状態.
004900*
005000   INPUT-OUTPUT         SECTION.
005100   FILE-CONTROL.
005200       SELECT  印刷ファイル  ASSIGN  TO  PRINTER.
005300 DATA                   DIVISION.
005400   FILE                 SECTION.
005500   FD  印刷ファイル.
005600   01  出力レコード     PIC X(125).
005700   WORKING-STORAGE      SECTION.
005800*
005900   01  従業員データ.
006000      02  Ｗ−従業員番号              PIC 9(04).
006100      02  Ｗ−氏名                    PIC N(08).
006200      02  Ｗ−郵便番号.
006300         03  Ｗ−郵便番号１           PIC X(03).
006400         03  Ｗ−郵便番号２           PIC X(04).
006500      02  Ｗ−住所                    PIC N(20).
006600      02  Ｗ−入社年月日              PIC X(08).
006700      02  Ｗ−退社年月日              PIC X(08).
006800      02  Ｗ−職級                    PIC 9(01).
006900      02  Ｗ−基本給                  PIC 9(08).
007000      02  Ｗ−特別手当                PIC 9(06).
007100      02  Ｗ−時間外手当              PIC 9(05).
007200      02  Ｗ−給与                    PIC 9(08).
007300*
007400   01  Ｗ−従業員情報.
007500      02  Ｗ−番号情報                PIC 9(04).
007600      02  Ｗ−職級情報                PIC 9(01).
007700*
007800   01  画面−選択番号                 PIC X(01).
007900   01  画面−職級選択                 PIC X(01).
008000   01  画面データ.
008100      02  画面−従業員番号            PIC 9(04).
008200      02  画面−氏名                  PIC N(08).
008300      02  画面−郵便番号.
008400         03  画面−郵便番号１         PIC X(03).
008500         03  画面−郵便番号２         PIC X(04).
008600      02  画面−住所                  PIC N(20).
008700      02  画面−入社年月日            PIC X(08).
008800      02  画面−退社年月日            PIC X(08).
008900      02  画面−職級                  PIC 9(01).
009000      02  画面−基本給                PIC 9(08).
009100      02  画面−特別手当              PIC 9(06).
009200      02  画面−メッセージ            PIC X(65).
009300      02  画面−時間外手当            PIC 9(05).
009400      02  画面−残業時間              PIC 999.9.
009500      02  画面−総支給                PIC 9(08).
009600      02  画面−確認                  PIC X(01).
009700*
009800   01  印刷ヘッダ１.
009900      02                              PIC X(05)  VALUE  SPACE.
010000      02                              PIC N(02)  VALUE  NC"＜　".
010100      02  印刷−見出し                PIC N(04)  VALUE  SPACE.
010200      02                              PIC N(05)  VALUE  NC"住所録　＞".
010300      02                              PIC X(98)  VALUE  SPACE.
010400   01  印刷ヘッダ２.
010500      02                              PIC X(05)  VALUE  SPACE.
010600      02                              PIC N(05)  VALUE  NC"従業員番号".
010700      02                              PIC X(04)  VALUE  SPACE.
010800      02                              PIC N(03)  VALUE  NC"氏　名".
010900      02                              PIC X(14)  VALUE  SPACE.
011000      02                              PIC N(03)  VALUE  NC"住　所".
011100      02                              PIC X(80)  VALUE  SPACE.
011200   01  印刷データ.
011300      02                              PIC X(05)  VALUE  SPACE.
011400      02  印刷−従業員番号            PIC 9(04).
011500      02                              PIC X(10)  VALUE  SPACE.
011600      02  印刷−氏名                  PIC N(08).
011700      02                              PIC X(04)  VALUE  SPACE.
011800      02  印刷−郵便番号.
011900         03  印刷−郵便番号１         PIC X(03).
012000         03                           PIC X(01)  VALUE  "-".
012100         03  印刷−郵便番号２         PIC X(04).
012200      02                              PIC X(02)  VALUE  SPACE.
012300      02  印刷−住所                  PIC N(20).
012400      02                              PIC X(28)  VALUE  SPACE.
012500*
012600   01  画面入力状態.
012700      02  状態キー.
012800         03  状態キー１               PIC X(01).
012900         03  状態キー２               PIC X(01).
013000      02                              PIC X(01).
013100   01  終了キー.
013200        03                            PIC X(01)    VALUE       "1".
013300        03                            PIC X(01)    VALUE       x"02".
013400   01  確定キー.
013500        03                            PIC X(01)    VALUE       "1".
013600        03                            PIC X(01)    VALUE       x"03".
013700*
013800   01  復帰値                         PIC 9(01).
013900*
014000   01  従業員−オブジェクト    OBJECT REFERENCE  AllMember-class.
014100   01  従業員−ファクトリ      OBJECT REFERENCE
014200                                      FACTORY OF AllMember-class.
014300   01  管理者−オブジェクト    OBJECT REFERENCE  Manager-class.
014400   01  一般社員−オブジェクト  OBJECT REFERENCE  Member-class.
014500   01  住所−オブジェクト      OBJECT REFERENCE  Address-class.
014600   01  共通−オブジェクト      OBJECT REFERENCE.
014700*
014800   SCREEN               SECTION.
014900   01  従業員管理画面    BLANK    SCREEN.
015000        03  VALUE    NC"−−＜従業員管理＞−−" LINE  02  COLUMN  29.
015100        03  VALUE    NC"１．従業員情報登録"     LINE  04  COLUMN  31.
015200        03  VALUE    NC"２．従業員情報削除"     LINE  06  COLUMN  31.
015300        03  VALUE    NC"３．従業員情報修正"     LINE  08  COLUMN  31.
015400        03  VALUE    NC"４．給与計算"           LINE  10  COLUMN  31.
015500        03  VALUE    NC"５．住所録"             LINE  12  COLUMN  31.
015600        03  VALUE    NC"Ｘ．終了"               LINE  14  COLUMN  31.
015700        03  VALUE    NC"選択番号＝＝＞"         LINE  16  COLUMN  31.
015800        03  コード   PIC X(01)                  LINE  16  COLUMN  46
015900            USING    画面−選択番号             UNDERLINE.
016000*
016100   01  従業員登録画面    BLANK    SCREEN.
016200        03  VALUE    NC"−−＜従業員登録＞−−" LINE  02  COLUMN  29.
016300        03  VALUE    NC"従業員番号"             LINE  04  COLUMN  11.
016400        03  番号１   PIC 9(04)                  LINE  04  COLUMN  25
016500            USING    画面−従業員番号           UNDERLINE.
016600        03  VALUE    NC"氏名"                   LINE  06  COLUMN  11.
016700        03  Ｗ−氏名１   PIC N(08)              LINE  06  COLUMN  25
016800            USING    画面−氏名                 UNDERLINE.
016900        03  VALUE    NC"住所"                   LINE  08  COLUMN  11.
017000        03  郵便１１ PIC X(03)                  LINE  08  COLUMN  25
017100            USING    画面−郵便番号１           UNDERLINE.
017200        03  VALUE    "-"               LINE  08  COLUMN  28  UNDERLINE.
017300        03  郵便１２ PIC X(04)                  LINE  08  COLUMN  29
017400            USING    画面−郵便番号２           UNDERLINE.
017500        03  住所１   PIC N(20)                  LINE  08  COLUMN  34
017600            USING    画面−住所                 UNDERLINE.
017700        03  VALUE    NC"入社年月日"             LINE  10  COLUMN  11.
017800        03  入社１   PIC X(08)                  LINE  10  COLUMN  25
017900            USING    画面−入社年月日           UNDERLINE.
018000        03  VALUE    NC"職級"                   LINE  12  COLUMN  11.
018100        03  職級１   PIC 9(01)                  LINE  12  COLUMN  25
018200            USING    画面−職級                 UNDERLINE.
018300        03  VALUE    NC"基本給"                 LINE  14  COLUMN  11.
018400        03  基本１   PIC 9(08)                  LINE  14  COLUMN  25
018500            USING    画面−基本給               UNDERLINE.
018600        03  VALUE    NC"特別手当"               LINE  16  COLUMN  11.
018700        03  特別１   PIC 9(06)                  LINE  16  COLUMN  27
018800            USING    画面−特別手当             UNDERLINE.
018900        03  VALUE    NC"時間外手当"             LINE  18  COLUMN  11.
019000        03  時間外１ PIC 9(05)                  LINE  18  COLUMN  28
019100            USING    画面−時間外手当           UNDERLINE.
019200        03  メッセージ１ PIC X(65)              LINE  20  COLUMN  11
019300            USING    画面−メッセージ           UNDERLINE.
019400        03  VALUE    NC"Ｆ２：終了　Ｆ３：登録" LINE  22  COLUMN  43.
019500*
019600   01  従業員選択画面    BLANK    SCREEN.
019700        03  VALUE    NC"−−＜従業員選択＞−−" LINE  02  COLUMN  29.
019800        03  VALUE    NC"従業員番号"             LINE  04  COLUMN  11.
019900        03  番号２   PIC 9(04)                  LINE  04  COLUMN  25
020000            USING    画面−従業員番号           UNDERLINE.
020100        03  メッセージ２ PIC X(65)              LINE  20  COLUMN  11
020200            USING    画面−メッセージ           UNDERLINE.
020300        03  VALUE    NC"Ｆ２：終了　Ｆ３：選択" LINE  22  COLUMN  43.
020400*
020500   01  従業員修正画面    BLANK    SCREEN.
020600        03  VALUE    NC"−−＜従業員修正＞−−" LINE  02  COLUMN  29.
020700        03  VALUE    NC"氏名"                   LINE  04  COLUMN  11.
020800        03  氏名３   PIC N(08)                  LINE  04  COLUMN  25
020900            USING    画面−氏名                 UNDERLINE.
021000        03  VALUE    NC"住所"                   LINE  06  COLUMN  11.
021100        03  郵便３１ PIC X(03)                  LINE  06  COLUMN  25
021200            USING    画面−郵便番号１           UNDERLINE.
021300        03  VALUE    "-"               LINE  06  COLUMN  28  UNDERLINE.
021400        03  郵便３２ PIC X(04)                  LINE  06  COLUMN  29
021500            USING    画面−郵便番号２           UNDERLINE.
021600        03  住所３   PIC N(20)                  LINE  06  COLUMN  34
021700            USING    画面−住所                 UNDERLINE.
021800        03  VALUE    NC"入社年月日"             LINE  08  COLUMN  11.
021900        03  入社３   PIC X(08)                  LINE  08  COLUMN  25
022000            USING    画面−入社年月日           UNDERLINE.
022100        03  VALUE    NC"退社年月日"             LINE  10  COLUMN  11.
022200        03  退社３   PIC X(08)                  LINE  10  COLUMN  25
022300            USING    画面−退社年月日           UNDERLINE.
022400        03  VALUE    NC"職級"                   LINE  12  COLUMN  11.
022500        03  職級３   PIC 9(01)                  LINE  12  COLUMN  25
022600            USING    画面−職級                 UNDERLINE.
022700        03  VALUE    NC"基本給"                 LINE  14  COLUMN  11.
022800        03  基本３   PIC 9(08)                  LINE  14  COLUMN  25
022900            USING    画面−基本給               UNDERLINE.
023000        03  VALUE    NC"特別手当"               LINE  16  COLUMN  11.
023100        03  特別３   PIC 9(06)                  LINE  16  COLUMN  27
023200            USING    画面−特別手当             UNDERLINE.
023300        03  VALUE    NC"時間外手当"             LINE  18  COLUMN  11.
023400        03  時間外３ PIC 9(05)                  LINE  18  COLUMN  28
023500            USING    画面−時間外手当           UNDERLINE.
023600        03  VALUE    NC"残業時間"               LINE  20  COLUMN  11.
023700        03  残業時間３   PIC 999.9              LINE  20  COLUMN  28
023800            USING    画面−残業時間             UNDERLINE.
023900        03  VALUE    NC"Ｆ２：取消　Ｆ３：修正" LINE  22  COLUMN  43.
024000*
024100   01  職級選択画面    BLANK    SCREEN.
024200        03  VALUE    NC"−−＜住所録印刷＞−−" LINE  02  COLUMN  29.
024300        03  VALUE    NC"１．管理者"             LINE  04  COLUMN  31.
024400        03  VALUE    NC"２．一般社員"           LINE  06  COLUMN  31.
024500        03  VALUE    NC"選択番号＝＝＞"         LINE  14  COLUMN  31.
024600        03  コード   PIC X(01)                  LINE  14  COLUMN  46
024700            USING    画面−職級選択             UNDERLINE.
024800*
024900        03  VALUE    NC"Ｆ２：終了　Ｆ３：印刷" LINE  22  COLUMN  43.
025000*
025100   01  給与情報参照選択画面    BLANK    SCREEN.
025200        03  VALUE    NC"−−＜給与計算結果の参照＞−−"
025300                                                LINE  02  COLUMN  5.
025400        03  VALUE  NC"給与計算処理が完了しました。"
025500                                                LINE  04  COLUMN  7.
025600        03  VALUE
025700            NC"各従業員の給与情報を参照したい場合は，"
025800                                                LINE  06  COLUMN  7.
025900        03  VALUE
026000            NC"以下に従業員番号を入力して下さい。"
026100                                                LINE  06  COLUMN  45.
026200        03  VALUE    NC"従業員番号"             LINE  08  COLUMN  7.
026300        03  番号２   PIC 9(04)                  LINE  08  COLUMN  21
026400            USING    画面−従業員番号           UNDERLINE.
026500        03  VALUE    NC"Ｆ２：終了　Ｆ３：選択" LINE  22  COLUMN  43.
026600*
026700   01  給与情報参照画面    BLANK    SCREEN.
026800        03  VALUE    NC"−−＜給与計算結果＞−−" LINE  02  COLUMN  29.
026900        03  VALUE    NC"従業員番号"             LINE  04  COLUMN  11.
027000        03  番号４   PIC 9(04)                  LINE  04  COLUMN  25
027100            USING    画面−従業員番号           UNDERLINE.
027200        03  VALUE    NC"氏名"                   LINE  06  COLUMN  11.
027300        03  Ｗ−氏名４   PIC N(08)              LINE  06  COLUMN  25
027400            USING    画面−氏名                 UNDERLINE.
027500        03  VALUE    NC"職級"                   LINE  08  COLUMN  11.
027600        03  職級４   PIC 9(01)                  LINE  08  COLUMN  25
027700            USING    画面−職級                 UNDERLINE.
027800        03  VALUE    NC"基本給"                 LINE  10  COLUMN  11.
027900        03  基本４   PIC 9(08)                  LINE  10  COLUMN  25
028000            USING    画面−基本給               UNDERLINE.
028100        03  VALUE    NC"特別手当"               LINE  12  COLUMN  11.
028200        03  特別４   PIC 9(06)                  LINE  12  COLUMN  27
028300            USING    画面−特別手当             UNDERLINE.
028400        03  VALUE    NC"時間外手当"             LINE  14  COLUMN  11.
028500        03  時間外４ PIC 9(05)                  LINE  14  COLUMN  28
028600            USING    画面−時間外手当           UNDERLINE.
028700        03  VALUE    NC"×"                     LINE  14  COLUMN  36.
028800        03  VALUE    NC"残業時間"               LINE  14  COLUMN  40.
028900        03  残業時間４ PIC 999.9                LINE  14  COLUMN  49
029000            USING    画面−残業時間             UNDERLINE.
029100        03  VALUE    NC"総支給"                 LINE  16  COLUMN  11.
029200        03  総支給４ PIC 9(08)                  LINE  16  COLUMN  25
029300            USING    画面−総支給               UNDERLINE.
029400        03  VALUE    NC"Ｆ２：終了"             LINE  22  COLUMN  50.
029500*
029600   01  削除確認画面    BLANK    SCREEN.
029700        03  VALUE    NC"−−＜従業員選択＞−−" LINE  02  COLUMN  29.
029800        03  VALUE    NC"従業員番号"             LINE  04  COLUMN  11.
029900        03  番号２   PIC 9(04)                  LINE  04  COLUMN  25
030000            USING    画面−従業員番号           UNDERLINE.
030100        03  VALUE    "削除してよろしいですか？（Y/N）"
030200                                                LINE  06  COLUMN  11.
030300        03  確認     PIC X(1)                   LINE  06  COLUMN  45
030400            USING    画面−確認                 UNDERLINE.
030500*
030600*========================================================================
030700*  メイン処理
030800*========================================================================
030900 PROCEDURE                  DIVISION.
031000*
031100     PERFORM    初期処理.
031200     PERFORM    主処理  UNTIL  画面−選択番号  =  "x"  OR "X".
031300     PERFORM    終了処理.
031400     STOP RUN.
031500*
031600*========================================================================
031700*  初期処理
031800*
031900*　　　マスタファイルをＯＰＥＮする。
032000*========================================================================
032100 初期処理     SECTION.
032200*
032300     INVOKE  AllMember-class   "MasterOpen-method".
032400*
032500 初期処理出口.
032600     EXIT.
032700*
032800*========================================================================
032900*  主処理
033000*========================================================================
033100 主処理     SECTION.
033200*
033300     PERFORM     従業員管理画面処理.
033400*
033500 主処理出口.
033600     EXIT.
033700*
033800*========================================================================
033900*  終了処理
034000*
034100*　　　マスタファイルをＣＬＯＳＥする。
034200*========================================================================
034300 終了処理        SECTION.
034400*
034500     INVOKE  AllMember-class   "MasterClose-method".
034600*
034700 終了処理出口.
034800     EXIT.
034900*
035000*========================================================================
035100*  従業員管理画面処理
035200*
035300*（１）従業員管理画面（初期画面）を表示し，処理の選択を要求する。
035400*========================================================================
035500 従業員管理画面処理         SECTION.
035600*
035700      INITIALIZE  状態キー.
035800      MOVE      SPACE    TO    画面−選択番号.
035900      DISPLAY   従業員管理画面.
036000      ACCEPT    従業員管理画面.
036100      EVALUATE  画面−選択番号
036200      WHEN  "1"
036300          PERFORM  従業員登録画面処理  UNTIL  状態キー  =  終了キー
036400      WHEN  "2"
036500          PERFORM  従業員情報削除処理  UNTIL  状態キー  =  終了キー
036600      WHEN  "3"
036700          PERFORM  従業員情報修正処理  UNTIL  状態キー  =  終了キー
036800      WHEN  "4"
036900          INVOKE   AllMember-class  "SetTop-method"
037000                                       RETURNING  Ｗ−従業員情報
037100          PERFORM  従業員オブジェクト獲得処理
037200          PERFORM  給与計算処理      UNTIL  従業員−オブジェクト  =  NULL
037300          PERFORM  給与情報参照処理  UNTIL  状態キー  =  終了キー
037400      WHEN  "5"
037500          PERFORM  住所録印刷処理
037600      END-EVALUATE.
037700*
037800 従業員管理画面処理出口.
037900      EXIT.
038000*
038100*=======================================================================
038200*  従業員オブジェクト獲得処理
038300*
038400*（１）指定された従業員番号に対応する従業員データのオブジェクトを，「従
038500*　　業員−オブジェクト」に獲得する。
038600*　　　従業員番号に対応する従業員データが存在しない場合，「従業員−オブ
038700*　　ジェクト」には，NULLを設定する。
038800*
038900*========================================================================
039000 従業員オブジェクト獲得処理    SECTION.
039100*
039200          EVALUATE  Ｗ−職級情報
039300          WHEN      1
039400              SET   従業員−ファクトリ    TO   Manager-class
039500          WHEN      2
039600              SET   従業員−ファクトリ    TO   Member-class
039700          WHEN      OTHER
039800              SET   従業員−オブジェクト  TO   NULL
039900              GO  TO  従業員オブジェクト獲得処理出口
040000          END-EVALUATE.
040100*
040200          INVOKE  従業員−ファクトリ  "RetAt-method"
040300                                      USING       Ｗ−番号情報
040400                                      RETURNING   従業員−オブジェクト.
040500*
040600 従業員オブジェクト獲得処理出口.
040700       EXIT.
040800*
040900*========================================================================
041000*  従業員登録画面処理
041100*
041200*（１）新規登録者の情報入力を要求し，入力された情報をオブジェクトに設定
041300*　　する。
041400*　・　ＰＦ３キーにより入力が確定し，ＰＦ４キーにより処理を終了する。
041500*　・　入力された職級が１又は２でなければ，再度入力を要求する。
041600*
041700*【注意】
041800*　　新規登録者の従業員番号として，既存の番号を指定した場合，既存の従業
041900*　員情報は，新規登録情報と置き変わってしまいます。
042000*========================================================================
042100 従業員登録画面処理         SECTION.
042200*
042300      INITIALIZE  画面データ.
042400*
042500 従業員情報入力.
042600      DISPLAY  従業員登録画面.
042700      MOVE  SPACE  TO  画面−メッセージ
042800      ACCEPT   従業員登録画面.
042900*
043000      EVALUATE  状態キー
043100      WHEN      終了キー
043200          GO  TO  従業員登録画面処理出口
043300      WHEN      確定キー
043400          IF  NOT ( 画面−職級  =  1     OR
043500                      画面−職級  =  2 )
043600              MOVE
043700             "「職級」に１（管理者）又は２（一般社員）を設定して下さい。"
043800                                                 TO  画面−メッセージ
043900              GO  TO  従業員情報入力
044000          ELSE
044100              PERFORM  データ設定
044200              MOVE  0  TO  総支給 OF 従業員−オブジェクト
044300              INVOKE   従業員−オブジェクト  "Store-method"
044400          END-IF
044500      END-EVALUATE.
044600*
044700 従業員登録画面処理出口.
044800      EXIT.
044900*
045000*========================================================================
045100*  従業員情報削除処理
045200*
045300*（１）従業員番号を指定し，その従業員番号の従業員情報を削除する。
045400*　・　ＰＦ３キーにより確定し，ＰＦ４キーにより処理を終了する。
045500*
045600*========================================================================
045700 従業員情報削除処理         SECTION.
045800*
045900      INITIALIZE  画面データ.
046000*
046100 従業員情報削除.
046200      INITIALIZE  状態キー.
046300      DISPLAY     従業員選択画面.
046400      ACCEPT      従業員選択画面.
046500*
046600      EVALUATE    状態キー
046700      WHEN        終了キー
046800          GO  TO  従業員情報削除処理出口
046900      WHEN        確定キー
047000          MOVE  SPACE  TO  画面−確認
047100          DISPLAY   削除確認画面
047200          ACCEPT    確認
047300          IF  画面−確認  =  "Y"  OR
047400              画面−確認  =  "y"
047500          THEN
047600              INVOKE  AllMember-class  "RemoveAt-method"
047700                                  USING     BY CONTENT  画面−従業員番号
047800                                  RETURNING             復帰値
047900          ELSE
048000              INITIALIZE   状態キー
048100              MOVE  SPACE  TO  画面−メッセージ
048200              GO  TO  従業員情報削除
048300          END-IF
048400*
048500          IF  復帰値  =  0
048600          THEN
048700              MOVE  "削除完了しました。"         TO  画面−メッセージ
048800          ELSE
048900              MOVE  "削除できませんでした。従業員番号を確認して下さい。"
049000                                                 TO  画面−メッセージ
049100          END-IF
049200          GO  TO  従業員情報削除
049300      END-EVALUATE.
049400*
049500 従業員情報削除処理出口.
049600      EXIT.
049700*
049800*========================================================================
049900*　データ設定処理
050000*
050100*（１）職級毎のオブジェクトを生成し，職級固有の入力情報を設定する。
050200*  ・　入力された職級が１（管理者）ならば，Manager-class のオブジェクト
050300*　　を生成し，特別手当の値をそのオブジェクトに設定する。
050400*　・　入力された職級が２（一般社員）ならば，一般社員クラスのオブジェク
050500*　　トを生成し，時間外手当の値をそのオブジェクトに設定する。
050600*（２）共通の入力情報を（１）で作成したオブジェクトに設定する。
050700*（３）Address-class のオブジェクトを獲得し，そのオブジェクト参照を（１
050800*　　）で作成したオブジェクトに設定した後，入力された住所データを設定す
050900*　　る。
051000*========================================================================
051100 データ設定       SECTION.
051200*
051300      EVALUATE  画面−職級
051400******  　管理者  *****
051500      WHEN  1
051600          INVOKE   Manager-class  "new"  RETURNING  管理者−オブジェクト
051700          SET      従業員−オブジェクト   TO   管理者−オブジェクト
051800          MOVE     画面−特別手当         TO
051900                              特別手当    OF  管理者−オブジェクト
052000******  一般社員  *****
052100      WHEN  2
052200          INVOKE   Member-class  "new"  RETURNING  一般社員−オブジェクト
052300          SET      従業員−オブジェクト   TO   一般社員−オブジェクト
052400          MOVE     画面−時間外手当       TO
052500                              時間外手当  OF   一般社員−オブジェクト
052600          MOVE     画面−残業時間         TO
052700                              残業時間    OF   一般社員−オブジェクト
052800      END-EVALUATE.
052900*
053000      MOVE   画面−従業員番号  TO  従業員番号   OF  従業員−オブジェクト.
053100      MOVE   画面−氏名        TO  氏名         OF  従業員−オブジェクト.
053200      MOVE   画面−入社年月日  TO  入社年月日   OF  従業員−オブジェクト.
053300      MOVE   画面−退社年月日  TO  退社年月日   OF  従業員−オブジェクト.
053400      MOVE   画面−職級        TO  職級         OF  従業員−オブジェクト.
053500      MOVE   画面−基本給      TO  基本給       OF  従業員−オブジェクト.
053600*
053700      INVOKE  Address-class  "new"   RETURNING  住所−オブジェクト.
053800      SET    住所参照  OF  従業員−オブジェクト TO  住所−オブジェクト.
053900      MOVE   画面−郵便番号    TO    郵便番号   OF  住所−オブジェクト.
054000      MOVE   画面−住所        TO    住所       OF  住所−オブジェクト.
054100*
054200 データ設定出口.
054300      EXIT.
054400*
054500*========================================================================
054600*  従業員情報修正処理
054700*
054800*（１）従業員選択画面を表示し，修正する従業員の従業員コードの入力を要求
054900*　　する。
055000*（２）入力されたＰＦキー毎の処理を行う。
055100*　・　ＰＦ２（終了キー）が入力されたならば，処理を終了するために，制御
055200*　　を出口に移行する。
055300*　・　ＰＦ３（確定キー）が入力されたならば，指定された従業員番号の従業
055400*　　員オブジェクトを獲得し，データ取得処理及び従業員情報修正画面処理を
055500*　　行う。
055600*=======================================================================
055700 従業員情報修正処理         SECTION.
055800*
055900      INITIALIZE  画面データ  Ｗ−従業員番号.
056000      DISPLAY  従業員選択画面.
056100      ACCEPT   従業員選択画面.
056200*
056300      EVALUATE  状態キー
056400      WHEN      終了キー
056500          GO  TO  従業員情報修正処理出口
056600      WHEN      確定キー
056700          INVOKE  AllMember-class  "SyokukyuGet-method"
056800                                            USING      画面−従業員番号
056900                                            RETURNING  Ｗ−職級情報
057000          IF  Ｗ−職級情報  NOT = 0
057100          THEN
057200              MOVE  画面−従業員番号   TO  Ｗ−番号情報
057300              PERFORM  従業員オブジェクト獲得処理
057400              PERFORM     データ取得
057500              INITIALIZE  状態キー
057600              PERFORM     従業員情報修正画面処理
057700                          UNTIL  状態キー  =  終了キー  OR  確定キー
057800          END-IF
057900      END-EVALUATE.
058000*
058100 従業員情報修正処理出口.
058200      EXIT.
058300*
058400*========================================================================
058500*  データ取得処理
058600*
058700*（１）取り出した従業員オブジェクトの情報を参照し，処理を切りわける。
058800*  ・　指定された従業員番号のオブジェクトならば，必要な情報を画面データ
058900*　　に設定する。
059000*　・　指定された従業員番号のオブジェクトでなければ，次のオブジェクトを
059100*　　獲得する。
059200*========================================================================
059300 データ取得       SECTION.
059400*
059500        MOVE  氏名        OF  従業員−オブジェクト  TO  画面−氏名.
059600        MOVE  入社年月日  OF  従業員−オブジェクト  TO  画面−入社年月日.
059700        MOVE  退社年月日  OF  従業員−オブジェクト  TO  画面−退社年月日.
059800        MOVE  職級        OF  従業員−オブジェクト  TO  画面−職級.
059900        MOVE  基本給      OF  従業員−オブジェクト  TO  画面−基本給.
060000        IF    画面−職級  =  1
060100        THEN
060200            MOVE   特別手当    OF  従業員−オブジェクト AS  Manager-class
060300                                                    TO   画面−特別手当
060400        ELSE
060500            MOVE   時間外手当  OF  従業員−オブジェクト AS  Member-class
060600                                                    TO   画面−時間外手当
060700            MOVE   残業時間    OF  従業員−オブジェクト AS  Member-class
060800                                                    TO   画面−残業時間
060900        END-IF.
061000        SET    住所−オブジェクト TO  住所参照  OF  従業員−オブジェクト.
061100        MOVE   郵便番号    OF  住所−オブジェクト    TO  画面−郵便番号.
061200        MOVE   住所        OF  住所−オブジェクト    TO  画面−住所.
061300*
061400 データ取得出口.
061500      EXIT.
061600*
061700*========================================================================
061800*  従業員情報修正画面処理
061900*
062000*（１）従業員情報修正画面を表示し，情報の修正を要求する。
062100*（２）修正後入力されたＰＦキー毎の処理を行う。
062200*　・　ＰＦ２（終了キー）が入力されたならば，修正を無効とするために，何
062300*　　もせずに制御を出口に移行する。
062400*　・　ＰＦ３（確定キー）が入力されたならばデータ設定処理を行った後，修
062500*　　正情報の保存処理を行う。
062600*========================================================================
062700 従業員情報修正画面処理         SECTION.
062800      DISPLAY  従業員修正画面.
062900      ACCEPT   従業員修正画面.
063000*
063100      EVALUATE   状態キー
063200      WHEN       終了キー
063300          GO  TO  従業員情報修正画面処理出口
063400      WHEN       確定キー
063500          PERFORM  データ設定
063600          MOVE 0 TO 総支給 OF 従業員−オブジェクト
063700          INVOKE   従業員−オブジェクト  "Store-method"
063800      END-EVALUATE.
063900*
064000 従業員情報修正画面処理出口.
064100      EXIT.
064200*
064300*========================================================================
064400*  給与計算処理
064500*
064600*（１）給与計算を行うメソッドを実行し，その結果をオブジェクトに設定して
064700*　　，マスタファイルに反映する。その後，次の従業員オブジェクトを獲得す
064800*　　る。
064900*========================================================================
065000 給与計算処理         SECTION.
065100*
065200      MOVE  従業員−オブジェクト::"Salary-method"  TO  Ｗ−給与.
065300      MOVE  Ｗ−給与    TO    総支給  OF  従業員−オブジェクト.
065400      INVOKE   従業員−オブジェクト  "Store-method".
065500      INVOKE   AllMember-class  "NextGet-method"
065600                                 RETURNING  Ｗ−従業員情報.
065700      PERFORM  従業員オブジェクト獲得処理.
065800*
065900 給与計算処理出口.
066000      EXIT.
066100*
066200*========================================================================
066300*  給与情報参照処理
066400*
066500*（１）給与情報参照選択画面を表示し，参照する従業員の従業員コードの入力
066600*　　を要求する。
066700*（２）入力されたＰＦキー毎の処理を行う。
066800*　・　ＰＦ２（終了キー）が入力されたならば，処理を終了するために，制御
066900*　　を出口に移行する。
067000*　・　ＰＦ３（確定キー）が入力されたならば，指定された従業員番号の従業
067100*　　員オブジェクトを獲得し，データ取得処理を行い，給与情報を表示する。
067200*========================================================================
067300 給与情報参照処理         SECTION.
067400*
067500      INITIALIZE  画面データ  Ｗ−従業員番号.
067600      DISPLAY  給与情報参照選択画面.
067700      ACCEPT   給与情報参照選択画面.
067800*
067900      EVALUATE  状態キー
068000      WHEN      終了キー
068100          GO  TO  給与情報参照処理出口
068200      WHEN      確定キー
068300          INVOKE  AllMember-class  "SyokukyuGet-method"
068400                                           USING      画面−従業員番号
068500                                           RETURNING  Ｗ−職級情報
068600*
068700          MOVE   画面−従業員番号    TO    Ｗ−番号情報
068800          PERFORM  従業員オブジェクト獲得処理
068900*
069000          IF  従業員−オブジェクト  NOT =  NULL
069100          THEN
069200              PERFORM  データ取得
069300              MOVE   総支給  OF  従業員−オブジェクト  TO  画面−総支給
069400              INITIALIZE  状態キー
069500              PERFORM  UNTIL  状態キー  =  終了キー
069600                  DISPLAY  給与情報参照画面
069700                  ACCEPT   給与情報参照画面
069800              END-PERFORM
069900          END-IF
070000          INITIALIZE  状態キー
070100      END-EVALUATE.
070200*
070300 給与情報参照処理出口.
070400      EXIT.
070500*
070600*========================================================================
070700*  住所録印刷処理
070800*
070900*（１）職級選択画面を表示し，職級の選択要求を行う。
071000*（２）選択されたＰＦキー毎の処理を行う。
071100*　・　ＰＦ２（終了キー）が入力されたならば，出口に制御を移行する。
071200*　・　ＰＦ３（確定キー）が入力されたならば，選択された職級に対する住所
071300*　　録の印刷処理を行う。
071400*========================================================================
071500 住所録印刷処理         SECTION.
071600*
071700      INITIALIZE  画面−職級選択.
071800      DISPLAY  職級選択画面.
071900      ACCEPT   職級選択画面.
072000*
072100      EVALUATE  状態キー
072200      WHEN      終了キー
072300          GO  TO  住所録印刷処理出口
072400      WHEN      確定キー
072500          INVOKE   AllMember-class  "SetTop-method"
072600                                     RETURNING  Ｗ−従業員情報
072700          PERFORM  従業員オブジェクト獲得処理
072800*
072900          IF   従業員−オブジェクト  =  NULL
073000          THEN
073100              GO  TO  住所録印刷処理出口
073200          END-IF
073300*
073400          OPEN  OUTPUT  印刷ファイル
073500          PERFORM  ヘッダー出力処理
073600          PERFORM  印刷処理
073700                   UNTIL  従業員−オブジェクト  =  NULL
073800          CLOSE  印刷ファイル
073900      END-EVALUATE.
074000*
074100 住所録印刷処理出口.
074200      EXIT.
074300*
074400*========================================================================
074500*  ヘッダー出力処理
074600*
074700*（１）選択された職級に対する住所録の，ヘッダー部分を印刷する。
074800*========================================================================
074900 ヘッダー出力処理         SECTION.
075000*
075100      INITIALIZE  印刷ヘッダ１ 印刷ヘッダ２.
075200*
075300      IF  画面−職級選択  =  1
075400      THEN
075500          MOVE  NC"管理者"            TO    印刷−見出し
075600      END-IF.
075700      IF  画面−職級選択  =  2
075800      THEN
075900          MOVE  NC"一般社員"          TO    印刷−見出し
076000      END-IF.
076100      WRITE  出力レコード   FROM   印刷ヘッダ１  AFTER  PAGE.
076200      WRITE  出力レコード   FROM   印刷ヘッダ２  AFTER  2.
076300*
076400 ヘッダー出力処理出口.
076500      EXIT.
076600*
076700*========================================================================
076800*  印刷処理
076900*
077000*（１）マスタファイルを順次読み込んで従業員オブジェクトを生成し，対象と
077100*　　なる職級であれば情報をレコードに設定し，印刷ファイルに書き込む。
077200*========================================================================
077300 印刷処理         SECTION.
077400      INITIALIZE  印刷データ.
077500      MOVE   職級  OF  従業員−オブジェクト  TO  Ｗ−職級.
077600*
077700      IF  (画面−職級選択  =  1  AND  Ｗ−職級  =  1)  OR
077800          (画面−職級選択  =  2  AND  Ｗ−職級  =  2)
077900      THEN
078000          MOVE   従業員番号  OF  従業員−オブジェクト  TO
078100                                                     印刷−従業員番号
078200          MOVE   氏名        OF  従業員−オブジェクト  TO  印刷−氏名
078300*
078400          SET    住所−オブジェクト  TO
078500                                 住所参照  OF  従業員−オブジェクト
078600          MOVE   郵便番号    OF  住所−オブジェクト  TO  Ｗ−郵便番号
078700          MOVE   Ｗ−郵便番号１                      TO  印刷−郵便番号１
078800          MOVE   Ｗ−郵便番号２                      TO  印刷−郵便番号２
078900          MOVE   住所        OF  住所−オブジェクト  TO  印刷−住所
079000*
079100          WRITE  出力レコード    FROM  印刷データ  AFTER  2
079200      END-IF.
079300*
079400      INVOKE   ALlMember-class  "NextGet-method"
079500                                 RETURNING  Ｗ−従業員情報.
079600      PERFORM  従業員オブジェクト獲得処理.
079700 印刷処理出口.
079800      EXIT.
079900*
080000 END PROGRAM Main.
