000100*========================================================================
000200*　住所マスタクラス（AddressMaster-class）
000300*　　　　　　　　　　　　−サンプルプログラム「従業員管理（永続化）」−
000400*
000500*　　　住所情報の保存復元のためのデータ及びメソッドを持つ。
000600*
000700*　〇オブジェクトデータ
000800*　　１）郵便番号
000900*　　２）住所
001000*
001100*　〇ファクトリメソッド
001200*　　１）住所データ取得　　　　（RetAt-method）
001300*　　２）住所データ保存        （Store-method）
001400*　　３）住所データ削除　　　　（RemoveAt-method）
001500*　　４）住所マスタＯＰＥＮ　　（Open-method）
001600*　　５）住所マスタＣＬＯＳＥ　（Close-method）
001700*
001800*                              Copyright 1997-2015 FUJITSU LIMITED
001900*========================================================================
002000 CLASS-ID .   AddressMaster-class.
002100 ENVIRONMENT   DIVISION .
002200 CONFIGURATION   SECTION.
002300 SPECIAL-NAMES.
002400      SYMBOLIC CONSTANT
002500       レコードあり    is    0
002600       レコードなし    is    1
002700       一般レコード長  is   63
002800       管理レコード長  is   59.
002900*
003000   FACTORY.
003100   ENVIRONMENT   DIVISION .
003200   INPUT-OUTPUT         SECTION.
003300   FILE-CONTROL.
003400     SELECT  住所マスタ      ASSIGN             TO  SYS007
003500                             ORGANIZATION       IS  INDEXED
003600                             ACCESS  MODE       IS  DYNAMIC
003700                             RECORD  KEY        IS  住所−キー.
003800   DATA                 DIVISION .
003900   FILE                 SECTION.
004000    FD  住所マスタ.
004100    01  住所−レコード.
004200      02  住所−キー.
004300        03  住所−ＩＤ       PIC   9(004).
004400      02  住所−郵便番号     PIC   X(007).
004500      02  住所−住所         PIC   N(020).
004600   WORKING-STORAGE SECTION.
004700    01  住所ＩＤ             PIC   9(004) PROPERTY.
004800    01  郵便番号             PIC   X(007) PROPERTY.
004900    01  住所                 PIC   N(020) PROPERTY.
005000   PROCEDURE  DIVISION .
005100*
005200*========================================================================
005300*　住所情報復元メソッド
005400*
005500*　　　指定された住所ＩＤの住所情報を住所マスタより読み込み，ファクトリ
005600*　　データに設定する．
005700*
005800*　〇入力：　住所ＩＤ　  PIC 9(4)
005900*　〇出力：　復帰値      PIC 9(1)
006000*========================================================================
006100   METHOD-ID. RetAt-method.
006200   DATA DIVISION.
006300   LINKAGE SECTION.
006400   01 Ｌ−住所ＩＤ          PIC 9(04).
006500   01 Ｌ−復帰値            PIC 9(01).
006600   PROCEDURE DIVISION
006700             USING  Ｌ−住所ＩＤ  RETURNING  Ｌ−復帰値.
006800*
006900       MOVE    Ｌ−住所ＩＤ        TO  住所−ＩＤ.
007000       READ   住所マスタ
007100         INVALID KEY
007200           MOVE    1               TO  Ｌ−復帰値
007300         NOT INVALID KEY
007400           MOVE   住所−郵便番号   TO  郵便番号
007500           MOVE   住所−住所       TO  住所
007600           MOVE  0                 TO  Ｌ−復帰値
007700       END-READ.
007800*
007900   END METHOD RetAt-method.
008000*
008100*=======================================================================
008200*　住所情報保存メソッド
008300*
008400*　　　当メソッドが呼び出された時点でファクトリデータに格納されている住
008500*　　所情報を，住所マスタに書き込む．
008600*　　　なお，住所ＩＤが０のときは，何もしない．
008700*========================================================================
008800   METHOD-ID. Store-method.
008900   ENVIRONMENT  DIVISION .
009000   DATA DIVISION.
009100   WORKING-STORAGE SECTION.
009200    01  Ｗ−ＲＥＡＤ結果    PIC 9(01).
009300   LINKAGE SECTION.
009400   PROCEDURE DIVISION.
009500*
009600       IF  住所ＩＤ  =  0
009700       THEN
009800           EXIT METHOD
009900       ELSE
010000           MOVE  住所ＩＤ       TO  住所−ＩＤ
010100       END-IF.
010200*
010300       READ  住所マスタ
010400         INVALID KEY
010500             MOVE  レコードなし   TO  Ｗ−ＲＥＡＤ結果
010600         NOT INVALID KEY
010700             MOVE  レコードあり   TO  Ｗ−ＲＥＡＤ結果
010800       END-READ.
010900*
011000       MOVE   郵便番号   TO  住所−郵便番号
011100       MOVE   住所       TO  住所−住所
011200*
011300       IF  Ｗ−ＲＥＡＤ結果 =  レコードなし
011400       THEN
011500           WRITE    住所−レコード
011600       ELSE
011700           REWRITE 住所−レコード
011800       END-IF.
011900*
012000   END METHOD Store-method.
012100*
012200*========================================================================
012300*　住所情報削除メソッド
012400*
012500*　　　指定された住所ＩＤの住所情報を，住所マスタより削除する。
012600*　　　削除が成功した場合，復帰値として０を，指定された住所ＩＤのレコー
012700*　　ドが存在しない等の理由から，削除が不成功に終わった場合，復帰値とし
012800*    て１を返却する。
012900*
013000*　〇入力：　住所ＩＤ　   PIC 9(4)
013100*　〇出力：　復帰値       PIC 9(1)
013200*========================================================================
013300   METHOD-ID. RemoveAt-method.
013400   DATA DIVISION.
013500   LINKAGE SECTION.
013600   01 Ｌ−住所ＩＤ          PIC 9(04).
013700   01 Ｌ−復帰値            PIC 9(1).
013800   PROCEDURE DIVISION
013900             USING  Ｌ−住所ＩＤ   RETURNING  Ｌ−復帰値.
014000*
014100       MOVE    Ｌ−住所ＩＤ  TO  住所−ＩＤ.
014200       DELETE  住所マスタ
014300         INVALID KEY
014400           MOVE  1   TO  Ｌ−復帰値
014500         NOT INVALID KEY
014600           MOVE  0   TO  Ｌ−復帰値
014700       END-DELETE.
014800*
014900   END METHOD RemoveAt-method.
015000*
015100*========================================================================
015200*　住所マスタファイルＯＰＥＮメソッド
015300*
015400*　　　住所マスタファイルをＩ−ＯモードでＯＰＥＮする。
015500*
015600*========================================================================
015700   METHOD-ID. Open-method.
015800   DATA DIVISION.
015900   LINKAGE SECTION.
016000   PROCEDURE DIVISION.
016100*
016200       OPEN I-O   住所マスタ.
016300*
016400   END METHOD Open-method.
016500*
016600*========================================================================
016700*　住所マスタファイルＣＬＯＳＥメソッド
016800*
016900*　　　住所マスタファイルをＣＬＯＳＥする。
017000*
017100*========================================================================
017200   METHOD-ID. Close-method.
017300   DATA DIVISION.
017400   LINKAGE SECTION.
017500   PROCEDURE DIVISION.
017600*
017700       Close 住所マスタ.
017800*
017900   END METHOD Close-method.
018000*
018100   END FACTORY.
018200  END CLASS  AddressMaster-class.
