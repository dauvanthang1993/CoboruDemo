000100*========================================================================
000200*　従業員マスタクラス（MemberMaster-class）
000300*　　　　　　　　　　　　　−サンプルプログラム「従業員管理（永続化）」−
000400*
000500*　　　従業員情報の保存復元のためのデータ及びメソッドを持つ。
000600*
000700*　〇ファクトリデータ
000800*　　１）従業員番号　−　PIC  9(4)
000900*　　２）氏名　　　　−　PIC  N(8)
001000*　　３）入社年月日　−　PIC  X(8)
001100*　　４）退社年月日　−　PIC  X(8)
001200*　　５）職級　　　　−　PIC  9(1)
001300*　　６）基本給　　　−　PIC  9(8)
001400*　　７）総支給　　　−　PIC  9(8)
001500*　　８）時間外手当　−　PIC  9(5)
001600*　　９）残業時間  　−　PIC  9(5)
001700*　１０）特別手当　　−　PIC  9(6)
001800*
001900*　〇ファクトリメソッド
002000*　　１）従業員情報復元  　　　　（RetAt-method）
002100*　　２）次従業員情報復元　　  　（RetNext-method）
002200*　　３）従業員情報保存          （Store-method）
002300*　　４）従業員情報削除          （RemoveAt-method）
002400*　　５）従業員マスタＯＰＥＮ　　（Open-method）
002500*　　６）従業員マスタＣＬＯＳＥ　（Close-method）
002600*　　７）先頭位置づけ　　　　　　（SetTop-method）
002700*
002800*                              Copyright 1997-2015 FUJITSU LIMITED
002900*========================================================================
003000 CLASS-ID.   MemberMaster-class.
003100 ENVIRONMENT    DIVISION.
003200 CONFIGURATION    SECTION.
003300 SPECIAL-NAMES.
003400      SYMBOLIC CONSTANT
003500       レコードあり    IS    0
003600       レコードなし    IS    1
003700       一般レコード長  IS   64
003800       管理レコード長  IS   59.
003900*
004000   FACTORY.
004100   ENVIRONMENT    DIVISION.
004200   INPUT-OUTPUT         SECTION.
004300   FILE-CONTROL.
004400     SELECT  従業員マスタ    ASSIGN             TO  SYS006
004500                             ORGANIZATION       IS  INDEXED
004600                             ACCESS  MODE       IS  DYNAMIC
004700                             RECORD  KEY        IS  従業員−キー.
004800   DATA                 DIVISION.
004900   FILE                 SECTION.
005000    FD  従業員マスタ
005100           RECORD IS VARYING IN SIZE FROM 59 TO 64 CHARACTERS
005200           DEPENDING ON  レコード長.
005300    01  従業員−レコード.
005400      02  固定部.
005500        03  従業員−キー.
005600          04  従業員−従業員番号        PIC 9(04).
005700        03  従業員−氏名                PIC N(08).
005800        03  従業員−入社年月日          PIC X(08).
005900        03  従業員−退社年月日          PIC X(08).
006000        03  従業員−職級                PIC 9(01).
006100        03  従業員−基本給              PIC 9(08).
006200        03  従業員−総支給              PIC 9(08).
006300      02  可変部.
006400        03  従業員−時間外手当          PIC 9(05).
006500        03  従業員−残業時間            PIC 9(05)V9(1).
006600      02  REDEFINES 可変部.
006700        03  従業員−特別手当            PIC 9(06).
006800   WORKING-STORAGE SECTION.
006900    01  共通情報.
007000      02  従業員番号          PIC   9(004)    VALUE 0     PROPERTY.
007100      02  氏名                PIC   N(008)    VALUE SPACE PROPERTY.
007200      02  入社年月日          PIC   X(008)    VALUE SPACE PROPERTY.
007300      02  退社年月日          PIC   X(008)    VALUE SPACE PROPERTY.
007400      02  職級                PIC   9(001)    VALUE 0     PROPERTY.
007500      02  基本給              PIC   9(008)    VALUE 0     PROPERTY.
007600      02  総支給              PIC   9(008)    VALUE 0     PROPERTY.
007700    01  時間外手当          PIC   9(5)       VALUE 0   PROPERTY.
007800    01  残業時間            PIC   9(5)V9(1)  VALUE 0   PROPERTY.
007900    01  特別手当            PIC   9(5)       VALUE 0   PROPERTY.
008000    01  レコード長          PIC   9(8) COMP-5   VALUE 0.
008100   PROCEDURE  DIVISION.
008200*
008300*========================================================================
008400*　従業員情報復元メソッド
008500*
008600*　　　指定された従業員番号の従業員情報を従業員マスタより読み込み，ファ
008700*　　クトリデータに設定する．
008800*
008900*　〇入力：　従業員番号   PIC 9(4)
009000*　〇出力：　復帰値       PIC 9(1)
009100*========================================================================
009200   METHOD-ID. RetAt-method.
009300   DATA DIVISION.
009400   LINKAGE SECTION.
009500   01 Ｌ−従業員番号        PIC 9(04).
009600   01 Ｌ−復帰値            PIC 9(1).
009700   PROCEDURE DIVISION
009800             USING  Ｌ−従業員番号   RETURNING  Ｌ−復帰値.
009900*
010000       MOVE    Ｌ−従業員番号  TO  従業員−従業員番号.
010100       READ   従業員マスタ
010200         INVALID KEY
010300           MOVE    1               TO  Ｌ−復帰値
010400         NOT INVALID KEY
010500           MOVE   固定部   TO   共通情報
010600           IF  従業員−職級  =  1
010700           THEN
010800               MOVE  従業員−特別手当       TO   特別手当
010900           ELSE
011000               MOVE  従業員−時間外手当     TO   時間外手当
011100               MOVE  従業員−残業時間       TO   残業時間
011200           END-IF
011300           MOVE  0         TO  Ｌ−復帰値
011400       END-READ.
011500*
011600   END METHOD RetAt-method.
011700*
011800*========================================================================
011900*　次従業員情報復元メソッド
012000*
012100*　　　現在参照している従業員マスタレコードの，次のレコードを読み込み，
012200*　　ファクトリデータに設定する．
012300*
012400*　〇出力：　従業員情報
012500*　　　　　　　　従業員番号    PIC 9(4)
012600*　　　　　　　　職級          PIC 9(1)
012700*========================================================================
012800   METHOD-ID. RetNext-method.
012900   DATA DIVISION.
013000   LINKAGE SECTION.
013100   01  Ｌ−従業員情報.
013200     02  Ｌ−従業員番号       PIC 9(04).
013300     02  Ｌ−職級             PIC 9(01).
013400   PROCEDURE DIVISION
013500                RETURNING  Ｌ−従業員情報.
013600*
013700       READ   従業員マスタ  NEXT
013800         AT END
013900           MOVE  0                    TO   Ｌ−従業員番号
014000           MOVE  0                    TO   Ｌ−職級
014100         NOT AT END
014200           MOVE  従業員−従業員番号   TO   Ｌ−従業員番号
014300           MOVE  従業員−職級         TO   Ｌ−職級
014400       END-READ.
014500*
014600   END METHOD RetNext-method.
014700*
014800*========================================================================
014900*　従業員情報保存メソッド
015000*
015100*　　　当メソッドが呼び出された時点でファクトリデータに格納されている従
015200*　　業員情報を，従業員マスタに書き込む．
015300*========================================================================
015400   METHOD-ID. Store-method.
015500   ENVIRONMENT DIVISION.
015600   DATA DIVISION.
015700   WORKING-STORAGE SECTION.
015800    01  Ｗ−ＲＥＡＤ結果    PIC 9(1).
015900   PROCEDURE DIVISION.
016000*
016100             MOVE  従業員番号           TO  従業員−従業員番号.
016200             READ  従業員マスタ
016300               INVALID KEY
016400                   MOVE  レコードなし   TO  Ｗ−ＲＥＡＤ結果
016500               NOT INVALID KEY
016600                   MOVE  レコードあり   TO  Ｗ−ＲＥＡＤ結果
016700             END-READ.
016800*
016900             MOVE  共通情報             TO  固定部.
017000             IF  職級  =  1
017100             THEN
017200                 MOVE  特別手当         TO  従業員−特別手当
017300                 MOVE  管理レコード長   TO  レコード長
017400             ELSE
017500                 MOVE  時間外手当       TO  従業員−時間外手当
017600                 MOVE  残業時間         TO  従業員−残業時間
017700                 MOVE  一般レコード長   TO  レコード長
017800             END-IF.
017900*
018000             IF  Ｗ−ＲＥＡＤ結果 =  レコードなし
018100             THEN
018200                 WRITE    従業員−レコード
018300             ELSE
018400                 REWRITE  従業員−レコード
018500             END-IF.
018600*
018700   END METHOD Store-method.
018800*
018900*========================================================================
019000*　従業員情報削除メソッド
019100*
019200*　　　指定された従業員番号の従業員情報を，従業員マスタより削除する。
019300*　　　削除が成功した場合，復帰値として０を，指定された従業員番号のレコ
019400*　　ードが存在しない等の理由から，削除が不成功に終わった場合，復帰値と
019500*　　して１を返却する。
019600*
019700*　〇入力：　従業員番号   PIC 9(4)
019800*　〇出力：　復帰値       PIC 9(1)
019900*========================================================================
020000   METHOD-ID. RemoveAt-method.
020100   DATA DIVISION.
020200   LINKAGE SECTION.
020300   01 Ｌ−従業員番号        PIC 9(04).
020400   01 Ｌ−復帰値            PIC 9(1).
020500   PROCEDURE DIVISION
020600             USING  Ｌ−従業員番号   RETURNING  Ｌ−復帰値.
020700*
020800       MOVE    Ｌ−従業員番号  TO  従業員−従業員番号.
020900       DELETE  従業員マスタ
021000         INVALID KEY
021100           MOVE  1   TO  Ｌ−復帰値
021200         NOT INVALID KEY
021300           MOVE  0   TO  Ｌ−復帰値
021400       END-DELETE.
021500*
021600   END METHOD RemoveAt-method.
021700*
021800*========================================================================
021900*　従業員マスタファイルＯＰＥＮメソッド
022000*
022100*　　　従業員マスタファイルをＩ−ＯモードでＯＰＥＮする。
022200*
022300*【注意】
022400*　　Ｉ−ＯモードでファイルをＯＰＥＮする場合，ファイル中に，最低一件の
022500*　レコードが存在している必要があります。
022600*　　付属の従業員マスタファイル「従業員マスタ」は，従業員番号０のレコー
022700*　ドを一件初期レコードとして保持していますので，そのままＩ−Ｏモードで
022800*　ＯＰＥＮできます。なおこのレコードを，当サンプルプログラムにより，参
022900*　照，更新，削除することはできません。
023000*========================================================================
023100   METHOD-ID. Open-method.
023200   DATA DIVISION.
023300   LINKAGE SECTION.
023400   PROCEDURE DIVISION.
023500*
023600       OPEN I-O  従業員マスタ.
023700       INITIALIZE 従業員−レコード.
023800*
023900   END METHOD Open-method.
024000*
024100*========================================================================
024200*　従業員マスタファイルＣＬＯＳＥメソッド
024300*
024400*　　　従業員マスタファイルをＣＬＯＳＥする。
024500*========================================================================
024600   METHOD-ID. Close-method.
024700   DATA DIVISION.
024800   LINKAGE SECTION.
024900   PROCEDURE DIVISION.
025000*
025100       CLOSE 従業員マスタ.
025200*
025300   END METHOD Close-method.
025400*
025500*========================================================================
025600*　先頭位置づけメソッド
025700*
025800*　　　従業員マスタファイルの先頭のレコードに位置づけ，そのレコードが保
025900*　　持するデータを，ファクトリデータに設定する。
026000*
026100*　〇出力：　従業員情報
026200*　　　　　　　　従業員番号    PIC 9(4)
026300*　　　　　　　　職級          PIC 9(1)
026400*========================================================================
026500   METHOD-ID. SetTop-method.
026600   DATA DIVISION.
026700   CONSTANT SECTION.
026800   LINKAGE SECTION.
026900    01  Ｌ−従業員情報.
027000      02  Ｌ−従業員番号       PIC 9(04).
027100      02  Ｌ−職級             PIC 9(01).
027200   PROCEDURE DIVISION
027300                RETURNING  Ｌ−従業員情報.
027400*
027500       MOVE  0   TO  従業員−従業員番号.
027600       START 従業員マスタ  KEY IS  >=  従業員−キー
027700         INVALID  KEY
027800           MOVE  0                    TO   Ｌ−従業員番号
027900           MOVE  0                    TO   Ｌ−職級
028000           EXIT METHOD
028100       END-START.
028200       READ  従業員マスタ NEXT.
028300       MOVE  従業員−従業員番号   TO   Ｌ−従業員番号.
028400       MOVE  従業員−職級         TO   Ｌ−職級.
028500*
028600   END METHOD SetTop-method.
028700*
028800   END FACTORY.
028900  END CLASS  MemberMaster-class.
