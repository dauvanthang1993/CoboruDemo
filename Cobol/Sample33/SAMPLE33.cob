000100*=================================================================
000200*「エンコード方式を使用するプログラム」
000300*
000400*    Unicodeデータ(サロゲートペア)をファイルから入力し、画面表示
000500*  および印刷します。
000600*
000700*     Copyright 2013-2015 FUJITSU LIMITED
000800*=================================================================
000900 IDENTIFICATION              DIVISION.
001000  PROGRAM-ID.                SAMPLE33.
001100*
001200 ENVIRONMENT                 DIVISION.
001300  CONFIGURATION              SECTION.
001400   SPECIAL-NAMES.
001500     ALPHABET
001600     U8   FOR ALPHANUMERIC UTF8
001700     U16L FOR NATIONAL     UTF16LE
001800     U32L FOR NATIONAL     UTF32LE
001900     SYMBOLIC CONSTANT
002000     オン         IS B"1"
002100     オフ         IS B"0"
002200*---------------------------------------------------------------*
002300* 印字の属性を定義します。                                      *
002400*---------------------------------------------------------------*
002500     PRINTING MODE  印字モード１ IS FOR MOCS
002600                    IN   SIZE  12.0 POINT
002700                    AT   PITCH 5.00 CPI
002800                    WITH FONT  GOTHIC
002900                    BY   FORM  F0102
003000     PRINTING MODE  印字モード２ IS FOR SOCS
003100                    IN   SIZE  8.0 POINT
003200                    AT   PITCH 10.00 CPI
003300                    WITH FONT  GOTHIC
003400                    BY   FORM  F0102
003500     POSITIONING UNIT PU IS 10.0 CPI.
003600 INPUT-OUTPUT               SECTION.
003700 FILE-CONTROL.
003800     SELECT 行順ファイル
003900         ASSIGN       TO  INFILE
004000         ORGANIZATION IS  LINE SEQUENTIAL.
004100     SELECT 印刷ファイル
004200         ASSIGN       TO  PRINTER.
004300*
004400 DATA                        DIVISION.
004500 FILE                       SECTION.
004600 FD  行順ファイル IS GLOBAL.
004700 01  行順レコード.
004800   02 キーデータ            PIC N(10).
004900   02                       PIC N(01).
005000   02 表示データ            PIC N(12).
005100 FD  印刷ファイル IS GLOBAL.
005200 01  印刷レコード           PIC X(100).
005300 WORKING-STORAGE             SECTION.
005400 01  出力選択               IS GLOBAL PIC X(01).
005500 01  KEY_OF_UT8             IS GLOBAL PIC X(20).
005600 01  現レコードカウンタ     IS GLOBAL PIC 9(02) VALUE 0.
005700 01  終了フラグ             IS GLOBAL PIC 1(1) BIT.
005800 PROCEDURE                   DIVISION.
005900*---------------------------------------------------------------*
006000* 印刷ファイルをオープンします。                                *
006100*---------------------------------------------------------------*
006200     OPEN OUTPUT 印刷ファイル.
006300*---------------------------------------------------------------*
006400* 作業域を初期化する                                            *
006500*---------------------------------------------------------------*
006600     MOVE オフ  TO  終了フラグ.
006700*---------------------------------------------------------------*
006800* 終了が選択されるか、エラーの起きるまで処理を繰り返します。    *
006900*---------------------------------------------------------------*
007000     DISPLAY " ".
007100     DISPLAY "<< サロゲートペアの文字の表示・印刷を行います。>>".
007200     PERFORM TEST BEFORE
007300            UNTIL  終了フラグ = オン
007400       DISPLAY " "
007500       DISPLAY "1.さかな編"
007600       DISPLAY "2.いと編"
007700       DISPLAY "X.終了"
007800       DISPLAY " "
007900       DISPLAY "以上から番号で選んでください。>> " WITH NO ADVANCING
008000       ACCEPT   出力選択
008100*
008200       EVALUATE 出力選択
008300         WHEN "1"
008400            CALL "選択"
008500         WHEN "2"
008600            CALL "選択"
008700         WHEN "X"
008800           MOVE オン TO 終了フラグ
008900         WHEN OTHER
009000           DISPLAY "指定された番号はありません。"
009100           MOVE オン TO 終了フラグ
009200       END-EVALUATE
009300       IF 終了フラグ NOT = オン THEN
009400          CALL "出力"
009500       END-IF
009600     END-PERFORM.
009700*
009800 終了処理.
009900*---------------------------------------------------------------*
010000* 使用したファイルをクローズし、処理を終えます。                *
010100*---------------------------------------------------------------*
010200     CLOSE  印刷ファイル.
010300     STOP RUN.
010400/
010500*===============================================================*
010600* 選択処理を行います。                                          *
010700*===============================================================*
010800 IDENTIFICATION              DIVISION.
010900  PROGRAM-ID.                選択.
011000 ENVIRONMENT                 DIVISION.
011100 DATA                        DIVISION.
011200 PROCEDURE                   DIVISION.
011300     DISPLAY " ".
011400     EVALUATE 出力選択
011500       WHEN "1"
011600         DISPLAY "●さかな編"
011700         DISPLAY "こち  はも  ほっけ      とびうお"
011800         DISPLAY "あら  かん  めいただき  まぐろ  終了"
011900       WHEN "2"
012000         DISPLAY "●いと編"
012100         DISPLAY "ひきづな  かたびら  きぬ  のみ"
012200         DISPLAY "しょく    あき      しま  終了"
012300     END-EVALUATE.
012400     DISPLAY " ".
012500     DISPLAY "以上から選んでください。>> " WITH NO ADVANCING.
012600     ACCEPT KEY_OF_UT8 FROM CONSOLE.
012700*
012800        *> UTF-8での比較
012900     IF KEY_OF_UT8 = "終了" THEN
013000       MOVE オン TO 終了フラグ
013100     END-IF.
013200 END PROGRAM 選択.
013300/
013400*===============================================================*
013500* 出力処理を行います。                                          *
013600*===============================================================*
013700 IDENTIFICATION              DIVISION.
013800  PROGRAM-ID.                出力.
013900 ENVIRONMENT                 DIVISION.
014000 DATA                        DIVISION.
014100  WORKING-STORAGE            SECTION.
014200  01 KEY_OF_UTF16            PIC N(10).
014300  01 文字数１６              PIC 9(2).
014400  01 バイト長１６            PIC 9(2).
014500  01 文字数３２              PIC 9(2).
014600  01 バイト長３２            PIC 9(2).
014700  01 表示データ２            PIC N(12) ENCODING U32L.
014800*
014900 01  印刷データ CHARACTER TYPE IS 印字モード１ OR
015000                                    印字モード２.
015100   02 レコードカウンタ      PIC 9(4).
015200   02                       PIC X(2).
015300   02 表示データ            PIC N(12).
015400   02                       PIC X(2).
015500   02 キーデータ            PIC X(20)
015600                          PRINTING POSITION IS 33 BY PU.
015700   02                       PIC X(2).
015800   02 文字数ＵＴＦ１６      PIC 9(2)
015900                          PRINTING POSITION IS 55 BY PU.   
016000   02                       PIC X(2).
016100   02 バイト長ＵＴＦ１６    PIC 9(2).
016200   02                       PIC X(2).
016300   02 文字数ＵＴＦ３２      PIC 9(2).
016400   02                       PIC X(2).
016500   02 バイト長ＵＴＦ３２    PIC 9(2).
016600*
016700 PROCEDURE                   DIVISION.
016800*---------------------------------------------------------------*
016900* コンソールから受け取ったＵＴＦ−８のデータをＵＴＦ１６に変換  *
017000* し、入力ファイル（ＵＴＦ１６）のデータと比較可能なコード・字  *
017100* 類にします。                                                  *
017200*---------------------------------------------------------------*
017300       MOVE FUNCTION NATIONAL-OF(KEY_OF_UT8) TO KEY_OF_UTF16.
017400*---------------------------------------------------------------*
017500* レコードの中から、表示・印刷するデータを取り出します。        *
017600*---------------------------------------------------------------*
017700                                 *> UTF-16での比較
017800       OPEN INPUT  行順ファイル.
017900       PERFORM TEST BEFORE
018000               UNTIL KEY_OF_UTF16 =  キーデータ OF 行順レコード
018100         READ 行順ファイル
018200           AT END
018300             DISPLAY "指定された文字列はありません。"
018400             CLOSE 行順ファイル
018500             EXIT PROGRAM
018600         END-READ
018700       END-PERFORM.
018800*---------------------------------------------------------------*
018900* コンソール出力をします。                                      *
019000*---------------------------------------------------------------*
019100       DISPLAY "該当する文字列をＵＴＦ１６／ＵＴＦ３２のデータに格納します。".
019200*---------------------------------------------------------------*
019300* エンコード指定ＵＴＦ−１６のデータに転記した場合の文字数と　　*
019400* バイト長 を出力する。　　　　　　　　　　　　　　　　　　　　　*
019500*---------------------------------------------------------------*
019600       DISPLAY "【ＵＴＦ−１６】".
019700       DISPLAY "文字列：" 表示データ OF 行順レコード.
019800       COMPUTE 文字数１６ = FUNCTION STORED-CHAR-LENGTH(表示データ OF 行順レコード).
019900       DISPLAY "文字数：" 文字数１６ "文字".
020000       MOVE FUNCTION LENG(表示データ OF 行順レコード(1:文字数１６)) TO バイト長１６.
020100       DISPLAY "バイト長：" バイト長１６ "バイト".
020200*---------------------------------------------------------------*
020300* エンコード指定ＵＴＦ−３２のデータに転記した場合の文字数と　　*
020400* バイト長 を出力する。　　　　　　　　　　　　　　　　　　　　　*
020500*---------------------------------------------------------------*
020600       MOVE CONV 表示データ OF 行順レコード TO 表示データ２.
020700       DISPLAY "【ＵＴＦ−３２】".
020800       DISPLAY "文字列：" 表示データ２.
020900       COMPUTE 文字数３２ = FUNCTION STORED-CHAR-LENGTH(表示データ２).
021000       DISPLAY "文字数：" 文字数３２  "文字".
021100       MOVE FUNCTION LENG(表示データ２(1:文字数３２)) TO バイト長３２.
021200       DISPLAY "バイト長：" バイト長３２ "バイト".
021300*---------------------------------------------------------------*
021400* 印刷用のレコードを出力します。                                *
021500*---------------------------------------------------------------*
021600       COMPUTE 現レコードカウンタ = 現レコードカウンタ + 1.
021700       INITIALIZE 印刷データ WITH FILLER.
021800       MOVE 現レコードカウンタ TO レコードカウンタ OF 印刷データ.
021900       MOVE KEY_OF_UT8 TO キーデータ OF 印刷データ.
022000       MOVE 表示データ OF 行順レコード TO 表示データ OF 印刷データ.
022100       MOVE 文字数１６   TO 文字数ＵＴＦ１６   OF 印刷データ.
022200       MOVE バイト長１６ TO バイト長ＵＴＦ１６ OF 印刷データ.
022300       MOVE 文字数３２   TO 文字数ＵＴＦ３２   OF 印刷データ.
022400       MOVE バイト長３２ TO  バイト長ＵＴＦ３２ OF 印刷データ.
022500       WRITE   印刷レコード FROM 印刷データ AFTER ADVANCING 2 LINES.
022600*---------------------------------------------------------------*
022700* 入力用のファイルを閉じます。                                  *
022800*---------------------------------------------------------------*
022900       CLOSE 行順ファイル.
023000 END PROGRAM 出力.
023100 END PROGRAM SAMPLE33.
023200
