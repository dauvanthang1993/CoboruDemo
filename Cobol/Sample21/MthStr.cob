000100*=================================================================
000200* 「マルチスレッドプログラミング」
000300*     スレッド間でファイル・データを共用して認証処理を実現します
000400*
000500*=================================================================
000600*
000700*  ファイル名： MTHSTR.cob
000800*  動作概要  ： 認証サービスを開始するため、顧客情報ファイルを
000900*               オープンします。
001000*
001100* Copyright 1999-2015 FUJITSU LIMITED
001200*=================================================================
001300 identification division.
001400  program-id. "認証サービス起動".
001500 environment division.
001600 configuration section.
001700 special-names.
001800      symbolic constant
001900        no-limit is -1.
002000 input-output  section.
002100 file-control.
002200     select 顧客情報ファイル assign to sys006
002300       organization is indexed
002400       access mode  is random
002500       record key   is 顧客番号
002600       file status  is 顧客情報ファイル入出力状態.
002700 data division.
002800 file section.
002900 fd 顧客情報ファイル is external.
003000 01 顧客情報.
003100   02 顧客番号                          pic x(32).
003200   02 暗証番号                          pic x(32).
003300 working-storage section.
003400   copy user-Lock.
003500   01 認証サービス起動情報      pic 9(1) is external.
003600   01 顧客情報ファイル入出力状態          pic x(02).
003700  linkage section.
003800   01 復帰値                              pic s9(09) comp-5.
003900 procedure division returning 復帰値.
004000*=================================================================
004100*  作業域を初期化します。
004200*=================================================================
004300     move 0 to 復帰値.
004400*=================================================================
004500*  認証サービスの起動状態を確認します。
004600*=================================================================
004700     move "初期化"  to lock-key.
004800     move NO-LIMIT  to wait-time.
004900     call "COB_LOCK_DATA" with c linkage
005000                          using by reference lock-key
005100                                by value wait-time
005200                                by reference err-datail
005300                          returning ret-value.
005400*=================================================================
005500*  認証サービスが起動されていないなら、顧客情報ファイルをオープン
005600*  し、認証サービスを起動します。
005700*=================================================================
005800     if 認証サービス起動情報 not = 1 then
005900       open input 顧客情報ファイル
006000       if 顧客情報ファイル入出力状態 not = "00" then
006100         move 1 to 復帰値
006200       else
006300         move 1 to 認証サービス起動情報
006400       end-if
006500     else
006600*=================================================================
006700*  認証サービスが起動済なら、エラーログを出力します。
006800*=================================================================
006900       move 2 to 復帰値
007000     end-if.
007100*=================================================================
007200* 呼出し元に復帰します。
007300*=================================================================
007400 exit-proc.
007500     call "COB_UNLOCK_DATA" with c linkage
007600                            using by reference lock-key
007700                                  by reference err-datail
007800                            returning ret-value
007900     exit program.
