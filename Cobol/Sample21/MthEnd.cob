000100*=================================================================
000200* 「マルチスレッドプログラミング」
000300*     スレッド間でファイル・データを共用して認証処理を実現します
000400*
000500*=================================================================
000600*
000700*  ファイル名： MTHEND.cob
000800*  動作概要  ： 認証サービスを終了するため、顧客情報ファイルを
000900*               クローズします。
001000*
001100* Copyright 1999-2015 FUJITSU LIMITED
001200*=================================================================
001300 identification division.
001400 program-id. "認証サービス停止".
001500 environment division.
001600 input-output section.
001700 file-control.
001800     select 顧客情報ファイル assign to sys006
001900      organization is indexed
002000      access mode  is random
002100      record key   is 顧客番号
002200      file status  is 顧客情報ファイル入出力状態.
002300 data division.
002400 file section.
002500 fd 顧客情報ファイル is external.
002600 01 顧客情報.
002700   02 顧客番号                pic x(32).
002800   02 暗証番号                pic x(32).
002900 working-storage section.
003000   copy user-Lock.
003100   01 認証サービス起動情報    pic 9(1) is external.
003200   01 顧客情報ファイル入出力状態      pic x(02).
003300  linkage section.
003400   01 復帰値              pic s9(09) comp-5.
003500 procedure division returning 復帰値.
003600*=================================================================
003700*  作業域を初期化します。
003800*=================================================================
003900     move 0 to 復帰値.
004000*=================================================================
004100*  認証サービスの起動状態を確認します。
004200*=================================================================
004300     move "初期化" to lock-key.
004400     move -1 to wait-time.
004500     call "COB_LOCK_DATA" with c linkage
004600                          using by reference lock-key
004700                                by value wait-time
004800                                by reference err-datail
004900                          returning ret-value.
005000*=================================================================
005100*  認証サービスが起動されているなら、顧客情報ファイルをクローズ
005200*  し、認証サービスを停止します。
005300*=================================================================
005400     if 認証サービス起動情報 = 1 then
005500       close 顧客情報ファイル
005600       if 顧客情報ファイル入出力状態 not = "00" then
005700         move 1 to 復帰値
005800       else
005900         move 0 to 認証サービス起動情報
006000       end-if
006100     else
006200*=================================================================
006300*  認証サービスが起動されていないなら、エラーログを出力します。
006400*=================================================================
006500       move 2 to 復帰値
006600     end-if.
006700*=================================================================
006800* 呼出し元に復帰します。
006900*=================================================================
007000 exit-proc.
007100     call "COB_UNLOCK_DATA" with c linkage
007200                            using by reference lock-key
007300                                  by reference err-datail
007400                            returning ret-value.
007500     exit program.
