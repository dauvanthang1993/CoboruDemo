000010*=================================================================
000020* 「セション管理機能を使用したWebアプリケーション」
000030*     セション管理機能を使用して、特定のクライアントから複数回の
000040*   リクエストにまたがった業務を行うアプリケーションを構築します。
000050*
000060*=================================================================
000070*
000080*  ファイル名： SESSOBJ.COB
000090*  動作概要  ： セションで使用するデータ及び操作するメソッド
000100*
000110* Copyright 2000-2015 FUJITSU LIMITED
000120*=================================================================
000130 IDENTIFICATION DIVISION.
000140  CLASS-ID セションオブジェクト INHERITS COBW3-SESSION-ADAPTER.
000150 ENVIRONMENT DIVISION.
000160  CONFIGURATION SECTION.
000170   REPOSITORY.
000180     CLASS COBW3-SESSION-ADAPTER.
000190 IDENTIFICATION DIVISION.
000200  FACTORY.
000210 ENVIRONMENT DIVISION.
000220 DATA DIVISION.
000230 WORKING-STORAGE SECTION.
000240 PROCEDURE DIVISION.
000250 END FACTORY.
000260*
000270 ID DIVISION.
000280  OBJECT.
000290 ENVIRONMENT DIVISION.
000300  INPUT-OUTPUT SECTION.
000310   *> ファイル定義
000320   FILE-CONTROL.
000330     SELECT 預金ファイル ASSIGN TO SYS007
000340      ORGANIZATION IS INDEXED
000350      ACCESS MODE  IS RANDOM
000360      RECORD KEY   IS ユーザＩＤ
000370      LOCK MODE    IS AUTOMATIC
000380      FILE STATUS  IS 預金ファイル入出力状態.
000390 DATA DIVISION.
000400  FILE SECTION.
000410   FD 預金ファイル.
000420    01 預金データ.
000430     02 ユーザＩＤ                PIC X(32).
000440     02 残高                      PIC 9(16).
000450  WORKING-STORAGE SECTION.
000460  01 預金ファイル入出力状態       PIC X(02).
000470  COPY SESSDATA.
000480 PROCEDURE DIVISION.
000490*
000500*=================================================================
000510*  SWEEP-SESSIONメソッド
000520*  セションタイムアウト時に呼び出されます。
000530*=================================================================
000540 IDENTIFICATION DIVISION.
000550  METHOD-ID. SWEEP-SESSION OVERRIDE.
000560 DATA DIVISION.
000570 PROCEDURE DIVISION.
000580     CLOSE 預金ファイル.
000590     EXIT METHOD.
000600 END METHOD SWEEP-SESSION.
000610*
000620*=================================================================
000630*  初期化処理メソッド
000640*=================================================================
000650 IDENTIFICATION DIVISION.
000660  METHOD-ID. 初期化処理.
000670 DATA DIVISION.
000680  LINKAGE SECTION.
000690   01 USERID   PIC X(32).
000700   01 BALANCE  PIC 9(16).
000710   01 復帰値   PIC 9(09) COMP-5.
000720 PROCEDURE DIVISION USING USERID BALANCE RETURNING 復帰値.
000730     MOVE 0 TO 復帰値.
000740     MOVE USERID TO S-USERID.
000750     *> 預金ファイルをオープンする
000760     OPEN I-O 預金ファイル.
000770     *> オープンに成功したか調べる
000780     IF 預金ファイル入出力状態 NOT = "00"
000790       MOVE 1 TO 復帰値
000800       EXIT METHOD
000810     END-IF.
000820*
000830      MOVE USERID TO ユーザＩＤ.
000840      READ 預金ファイル INVALID KEY
000850        MOVE 2 TO 復帰値
000860        CLOSE 預金ファイル
000870        EXIT METHOD
000880      END-READ.
000890      *> レコードがロックされている場合
000900      IF 預金ファイル入出力状態  = "99"
000910        MOVE 3 TO 復帰値
000920        CLOSE 預金ファイル
000930        EXIT METHOD
000940      END-IF.
000950
000960      MOVE 残高 TO S-OLDBALANCE BALANCE.
000970      EXIT METHOD.
000980 END METHOD 初期化処理.
000990*
001000*=================================================================
001010*  終了処理メソッド
001020*=================================================================
001030 IDENTIFICATION DIVISION.
001040  METHOD-ID. 終了処理.
001050 DATA DIVISION.
001060 PROCEDURE DIVISION.
001070     CLOSE 預金ファイル.
001080     EXIT METHOD.
001090 END METHOD 終了処理.
001100*
001110*=================================================================
001120*  セションデータ取得
001130*=================================================================
001140 IDENTIFICATION DIVISION.
001150  METHOD-ID. セションデータ取得.
001160 DATA DIVISION.
001170  LINKAGE SECTION.
001180   COPY GETDATA.
001190 PROCEDURE DIVISION USING GETDATA.
001200     MOVE S-USERID TO G-USERID.
001210     MOVE S-OLDBALANCE TO G-OLDBALANCE.
001220     MOVE S-TRADE TO G-TRADE.
001230     MOVE S-MONEY TO G-MONEY.
001240     MOVE S-NEWBALANCE TO G-NEWBALANCE.
001250     EXIT METHOD.
001260 END METHOD セションデータ取得.
001270*
001280*=================================================================
001290*  預入メソッド
001300*=================================================================
001310 IDENTIFICATION DIVISION.
001320  METHOD-ID. 預入.
001330 DATA DIVISION.
001340  LINKAGE SECTION.
001350 01 I-MONEY                       PIC 9(16).
001360 01 O-BALANCE                     PIC 9(16).
001370 01 復帰値                        PIC 9(09) COMP-5.
001380 PROCEDURE DIVISION USING I-MONEY O-BALANCE RETURNING 復帰値.
001390     MOVE 0 TO 復帰値.
001400     *> 桁あふれするか調べる
001410     IF I-MONEY + S-OLDBALANCE >= 10000000000000000 THEN
001420       MOVE 1 TO 復帰値
001430       EXIT METHOD
001440     END-IF.
001450     *> セションデータ登録
001460     SET S-DEPOSIT TO TRUE.
001470     MOVE I-MONEY TO S-MONEY.
001480     COMPUTE S-NEWBALANCE = S-OLDBALANCE + S-MONEY.
001490     MOVE S-NEWBALANCE TO O-BALANCE.
001500     EXIT METHOD.
001510 END METHOD 預入.
001520*
001530*=================================================================
001540*  払戻メソッド
001550*=================================================================
001560 IDENTIFICATION DIVISION.
001570  METHOD-ID. 払戻.
001580 DATA DIVISION.
001590  LINKAGE SECTION.
001600 01 I-MONEY                       PIC 9(16).
001610 01 O-BALANCE                     PIC 9(16).
001620 01 復帰値                        PIC 9(09) COMP-5.
001630 PROCEDURE DIVISION USING I-MONEY O-BALANCE RETURNING 復帰値.
001640     MOVE 0 TO 復帰値.
001650     *> マイナスになるか調べる
001660     IF S-OLDBALANCE - I-MONEY < 0 THEN
001670       MOVE 1 TO 復帰値
001680       EXIT METHOD
001690     END-IF.
001700     *> セションデータ登録
001710     SET S-REPAYMENT TO TRUE.
001720     MOVE I-MONEY TO S-MONEY.
001730     COMPUTE S-NEWBALANCE = S-OLDBALANCE - S-MONEY.
001740     MOVE S-NEWBALANCE TO O-BALANCE.
001750     EXIT METHOD.
001760 END METHOD 払戻.
001770*
001780*=================================================================
001790*  更新メソッド
001800*=================================================================
001810 IDENTIFICATION DIVISION.
001820  METHOD-ID. 更新.
001830 DATA DIVISION.
001840  LINKAGE SECTION.
001850   01 復帰値   PIC 9(09) COMP-5.
001860 PROCEDURE DIVISION RETURNING 復帰値.
001870     MOVE 0 TO 復帰値.
001880     MOVE S-USERID TO ユーザＩＤ.
001890     MOVE S-NEWBALANCE TO 残高.
001900     REWRITE 預金データ.
001910     *> 登録に成功したか調べる
001920     IF 預金ファイル入出力状態 NOT = "00"
001930       MOVE 1 TO 復帰値
001940     END-IF.
001950     EXIT METHOD.
001960 END METHOD 更新.
001970*
001980 END OBJECT.
001990 END CLASS セションオブジェクト.
