000010*=================================================================
000020* 「セション管理機能を使用したWebアプリケーション」
000030*     セション管理機能を使用して、特定のクライアントから複数回の
000040*   リクエストにまたがった業務を行うアプリケーションを構築します。
000050*
000060*=================================================================
000070*
000080*  ファイル名： USRINF.COB
000090*  動作概要  ： 認証サービスのための認証処理をする。
000100*  パラメータ：
000110*     第一引数：ユーザＩＤ
000120*     第二引数：パスワード
000130*     復帰値
000140*       成功：０
000150*       失敗：０以外
000160*
000170* Copyright 2000-2015 FUJITSU LIMITED
000180*=================================================================
000190 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. "顧客情報取得".
000210 ENVIRONMENT DIVISION.
000220 INPUT-OUTPUT SECTION.
000230 FILE-CONTROL.
000240     SELECT 顧客情報ファイル ASSIGN TO SYS006
000250       ORGANIZATION IS INDEXED
000260       ACCESS MODE IS RANDOM
000270       RECORD KEY IS 顧客番号
000280       FILE STATUS IS 顧客情報ファイル入出力状態.
000290 DATA DIVISION.
000300 FILE SECTION.
000310 FD 顧客情報ファイル.
000320 01 顧客情報.
000330   02 顧客番号                PIC X(32).
000340   02 暗証番号                PIC X(32).
000350 WORKING-STORAGE SECTION.
000360 01 顧客情報ファイル入出力状態          PIC X(2).
000370 LINKAGE SECTION.
000380   COPY USER-INFO.
000390 01 復帰値                PIC 9(09) COMP-5.
000400 PROCEDURE DIVISION USING USERINFO RETURNING 復帰値.
000410*=================================================================
000420*  作業域を初期化します。
000430*=================================================================
000440     MOVE 0 TO 復帰値.
000450*=================================================================
000460*  顧客情報ファイルをオープンします。
000470*=================================================================
000480     OPEN INPUT 顧客情報ファイル.
000490     IF 顧客情報ファイル入出力状態 NOT = "00" THEN
000500       MOVE 1 TO 復帰値
000510       EXIT PROGRAM
000520     END-IF.
000530*=================================================================
000540*  USERIDをキーにレコードを読み込み、認証処理を行います。
000550*=================================================================
000560     MOVE USERID TO 顧客番号.
000570     READ 顧客情報ファイル WITH NO LOCK
000580       INVALID KEY
000590         MOVE 2     TO 復帰値
000600       NOT INVALID KEY
000610         IF 暗証番号 NOT = USERPASSWORD
000620           MOVE 3 TO 復帰値
000630         END-IF
000640     END-READ.
000650*=================================================================
000660*  顧客情報ファイルをクローズします。
000670*=================================================================
000680     CLOSE 顧客情報ファイル.
000690     IF 顧客情報ファイル入出力状態 NOT = "00" THEN
000700       MOVE 1 TO 復帰値
000710     END-IF.
000720     EXIT PROGRAM.
