000100*========================================================================
000200*　仕様管理クラス
000300*                                   −サンプルプログラム「会議室予約」−
000400*
000500*　　各種仕様を管理するためのメソッドを持つ。
000600*
000700*　〇継承クラス
000800*　　１）FJBASE
000900*
001000*　〇参照クラス
001100*　　１）List
001200*　　２）仕様クラス
001300*　　３）リストイテレータ
001400*　　４）エラークラス
001500*
001600*　〇オブジェクトデータ
001700*　　１）仕様個数　　　　　　　　−　PIC 9(8) BINARY
001800*　　２）仕様リストオブジェクト　−　OBJECT REFERENCE List
001900*　　３）仕様クラスオブジェクト  −  OBJECT REFERENCE FACTORY OF 仕様クラス
002000*
002100*　〇オブジェクトメソッド
002200*　　１）初期化　（INIT）
002300*　　２）仕様クラスの設定
002400*　　３）仕様情報の参照
002500*　　４）仕様情報の更新
002600*  　５）仕様情報の削除
002700*　　６）仕様情報の追加
002800*　　７）仕様情報の検索
002900*　　８）仕様個数の参照
003000*　　９）仕様リストの参照
003100*
003200*            Copyright 1999-2015 FUJITSU LIMITED
003300*========================================================================
003400 IDENTIFICATION  DIVISION.
003500 CLASS-ID.   仕様管理クラス  INHERITS FJBASE.
003600 ENVIRONMENT     DIVISION.
003700 CONFIGURATION   SECTION.
003800 SPECIAL-NAMES.
003900     SYMBOLIC CONSTANT
004000     COPY R_CONST.
004100     .
004200 REPOSITORY.
004300     CLASS FJBASE
004400     CLASS List
004500     CLASS 仕様クラス
004600     CLASS リストイテレータ
004700     CLASS エラークラス
004800     .
004900*
005000 FACTORY.
005100 DATA            DIVISION.
005200 WORKING-STORAGE SECTION.
005300 PROCEDURE       DIVISION.
005400 END FACTORY.
005500*
005600 OBJECT.
005700 DATA DIVISION.
005800 BASED-STORAGE   SECTION.
005900  COPY SPECINFO.
006000 WORKING-STORAGE SECTION.
006100  01 仕様個数                         PIC 9(8) BINARY.
006200  01 仕様リストオブジェクト           OBJECT REFERENCE List.
006300  01 仕様クラスオブジェクト           OBJECT REFERENCE FACTORY OF 仕様クラス.
006400 PROCEDURE       DIVISION.
006500*========================================================================
006600*　初期化メソッド（INIT）
006700*
006800*　　仕様個数に0を、仕様リストオブジェクトにNULLを設定する。
006900*
007000*　〇入力：なし
007100*　〇出力：なし
007200*========================================================================
007300 METHOD-ID.  INIT  OVERRIDE.
007400 DATA            DIVISION.
007500 WORKING-STORAGE SECTION.
007600 LINKAGE         SECTION.
007700 PROCEDURE       DIVISION.
007800     MOVE  0                      TO  仕様個数
007900     SET   仕様リストオブジェクト TO  NULL
008000 END METHOD INIT.
008100*========================================================================
008200*　仕様クラスの設定メソッド
008300*
008400*　　仕様クラスオブジェクトを設定する。
008500*
008600*　〇入力：仕様クラスオブジェクト (OBJECT REFERENCE FACTORY OF 仕様クラス)
008700*　〇出力：なし
008800*========================================================================
008900 METHOD-ID.  仕様クラスの設定.
009000 DATA            DIVISION.
009100 WORKING-STORAGE SECTION.
009200 LINKAGE         SECTION.
009300  01  Ｌ−仕様クラスオブジェクト      OBJECT REFERENCE FACTORY OF 仕様クラス.
009400 PROCEDURE       DIVISION
009500                    USING             Ｌ−仕様クラスオブジェクト.
009600     SET  仕様クラスオブジェクト  TO  Ｌ−仕様クラスオブジェクト.
009700 END METHOD 仕様クラスの設定.
009800*========================================================================
009900*　仕様情報の参照メソッド
010000*
010100*　　仕様名をキーに、仕様オブジェクトを取得する。該当する情報が見つからな
010200*　い場合は、エラーコードを設定する。
010300*
010400*　〇入力：仕様データ (TYPE 仕様リスト)
010500*　〇出力：処理コード (PIC  S9(8) BINARY)
010600*========================================================================
010700 METHOD-ID.  仕様情報の参照.
010800 DATA            DIVISION.
010900 WORKING-STORAGE SECTION.
011000  01 仕様オブジェクト                 OBJECT REFERENCE 仕様クラス.
011100 LINKAGE         SECTION.
011200  01 Ｌ−仕様データ                   TYPE 仕様リスト.
011300  01 Ｌ−処理コード                   PIC  S9(8) BINARY.
011400 PROCEDURE       DIVISION
011500                    USING             Ｌ−仕様データ
011600                    RETURNING         Ｌ−処理コード.
011700 DECLARATIVES.
011800 ERR SECTION.
011900     USE AFTER EXCEPTION エラークラス
012000     INVOKE EXCEPTION-OBJECT          "仕様検索エラー".
012100 END DECLARATIVES.  
012200*
012300*　仕様名をキーに仕様オブジェクトを検索する。
012400*
012500     INVOKE  SELF                     "仕様情報の検索"
012600                                      USING BY CONTENT 仕様名 
012700                                      RETURNING        仕様オブジェクト
012800*
012900*　該当する仕様オブジェクトが見つからなかった場合、エラーを設定する。
013000*　該当する仕様オブジェクトが見つかった場合、仕様データを取得する。
013100*
013200     IF 仕様オブジェクト = NULL THEN
013300       MOVE  ERROR-ON             TO  Ｌ−処理コード
013400       RAISE エラークラス :: "NEW"
013500     ELSE
013600       MOVE  ERROR-OFF            TO  Ｌ−処理コード
013700       MOVE  仕様オブジェクト :: "仕様参照"  
013800                                  TO  Ｌ−仕様データ
013900     END-IF
014000 END METHOD 仕様情報の参照.
014100*========================================================================
014200*　仕様情報の更新メソッド
014300*
014400*　　仕様名をキーに、仕様オブジェクトを取得・更新する。該当する情報が見つ
014500*　からない場合は、エラーコードを設定する。
014600*
014700*　〇入力：仕様データ (TYPE 仕様リスト)
014800*　〇出力：処理コード (PIC  S9(8) BINARY)
014900*========================================================================
015000 METHOD-ID.  仕様情報の更新.
015100 DATA            DIVISION.
015200 WORKING-STORAGE SECTION.
015300  01 仕様オブジェクト                 OBJECT REFERENCE 仕様クラス.
015400 LINKAGE         SECTION.
015500  01 Ｌ−仕様データ                   TYPE 仕様リスト.
015600  01 Ｌ−処理コード                   PIC S9(8) BINARY.
015700 PROCEDURE       DIVISION
015800                    USING             Ｌ−仕様データ
015900                    RETURNING         Ｌ−処理コード.
016000 DECLARATIVES.
016100 ERR SECTION.
016200     USE AFTER EXCEPTION エラークラス
016300     INVOKE EXCEPTION-OBJECT          "仕様検索エラー".
016400 END DECLARATIVES.  
016500*
016600* 仕様名をキーに仕様オブジェクトを検索する。
016700*
016800     INVOKE  SELF                     "仕様情報の検索"
016900                                      USING BY CONTENT 仕様名
017000                                      RETURNING        仕様オブジェクト
017100*
017200*　該当する仕様オブジェクトが見つからなかった場合、エラーを設定する。
017300*　該当する仕様オブジェクトが見つかった場合、仕様データを設定する。
017400*
017500     IF 仕様オブジェクト = NULL   THEN
017600       MOVE   ERROR-ON            TO  Ｌ−処理コード
017700       RAISE  エラークラス :: "NEW"
017800     ELSE
017900       MOVE   ERROR-OFF           TO  Ｌ−処理コード
018000       INVOKE 仕様オブジェクト        "仕様設定"
018100                                      USING            Ｌ−仕様データ
018200     END-IF
018300 END METHOD 仕様情報の更新.
018400*========================================================================
018500*　仕様情報の削除メソッド
018600*
018700*　　仕様名をキーに、仕様オブジェクトを削除する。該当する情報が見つからな
018800*　い場合は、エラーコードを設定する。
018900*　　また、仕様個数を−１する。
019000*
019100*　〇入力：仕様データ (TYPE 仕様リスト)
019200*　〇出力：処理コード (PIC  S9(8) BINARY)
019300*========================================================================
019400 METHOD-ID.  仕様情報の削除.
019500 DATA            DIVISION.
019600 WORKING-STORAGE SECTION.
019700  01 共通オブジェクト                 OBJECT REFERENCE.
019800  01 仕様オブジェクト                 OBJECT REFERENCE 仕様クラス.
019900  01 インデックス番号                 PIC 9(8) BINARY.
020000 LINKAGE         SECTION.
020100  01 Ｌ−仕様データ                   TYPE 仕様リスト.
020200  01 Ｌ−処理コード                   PIC S9(8) BINARY.
020300 PROCEDURE       DIVISION
020400                    USING             Ｌ−仕様データ
020500                    RETURNING         Ｌ−処理コード.
020600 DECLARATIVES.
020700 ERR SECTION.
020800     USE AFTER EXCEPTION エラークラス
020900     INVOKE EXCEPTION-OBJECT          "仕様検索エラー".
021000 END DECLARATIVES.  
021100*
021200*　仕様名をキーに仕様オブジェクトを検索する。
021300*
021400     INVOKE SELF                      "仕様情報の検索"
021500                                      USING BY CONTENT 仕様名
021600                                      RETURNING        仕様オブジェクト
021700*
021800*　該当する仕様オブジェクトが見つからなかった場合、エラーを設定する。
021900*　該当する仕様オブジェクトが見つかった場合、削除処理を行う。
022000*
022100     IF  仕様オブジェクト = NULL THEN
022200       MOVE   ERROR-ON            TO  Ｌ−処理コード
022300       RAISE エラークラス :: "NEW"
022400     ELSE
022500       MOVE   ERROR-OFF           TO  Ｌ−処理コード
022600       SET     共通オブジェクト   TO  仕様オブジェクト
022700       INVOKE  仕様リストオブジェクト "ElementNo-Get"
022800                                      USING            共通オブジェクト
022900                                      RETURNING        インデックス番号
023000       INVOKE  仕様リストオブジェクト "Remove-At"
023100                                      USING            インデックス番号
023200       COMPUTE 仕様個数 = 仕様個数 - 1
023300     END-IF
023400 END METHOD 仕様情報の削除.
023500*========================================================================
023600*　仕様情報の追加メソッド
023700*
023800*　　仕様リストオブジェクトがNULLならば、リストオブジェクトを生成する。
023900*　　仕様データに対して仕様オブジェクトを生成し、仕様リストオブジェクトに
024000*　登録する。
024100*　　また、仕様個数を＋１する。
024200*
024300*　〇入力：仕様データ (TYPE 仕様リスト)
024400*　〇出力：なし
024500*========================================================================
024600 METHOD-ID.  仕様情報の追加.
024700 DATA            DIVISION.
024800 WORKING-STORAGE SECTION.
024900  01 仕様オブジェクト                 OBJECT REFERENCE 仕様クラス.
025000 LINKAGE         SECTION.
025100  01 Ｌ−仕様データ                   TYPE 仕様リスト.
025200 PROCEDURE       DIVISION
025300                    USING             Ｌ−仕様データ.
025400     IF   仕様リストオブジェクト = NULL THEN
025500       SET 仕様リストオブジェクト TO  List :: "NEW"
025600     END-IF
025700*
025800     INVOKE 仕様クラスオブジェクト    "NEW"   RETURNING   仕様オブジェクト
025900     INVOKE 仕様オブジェクト          "仕様設定"
026000                                       USING BY CONTENT   Ｌ−仕様データ
026100     INVOKE 仕様リストオブジェクト    "Element-PutLast"
026200                                       USING BY CONTENT   仕様オブジェクト
026300     COMPUTE 仕様個数 = 仕様個数 + 1
026400 END METHOD 仕様情報の追加.
026500*========================================================================
026600*　仕様情報の検索メソッド
026700*
026800*　　仕様名をキーに、仕様オブジェクトを検索する。
026900*
027000*  ○入力：仕様名 (PIC X(4))
027100*　〇出力：仕様オブジェクト (OBJECT REFERENCE 仕様クラス)
027200*========================================================================
027300 METHOD-ID.  仕様情報の検索.
027400 DATA            DIVISION.
027500 WORKING-STORAGE SECTION.
027600  01  仕様オブジェクト                OBJECT REFERENCE 仕様クラス.
027700  01  仕様データ                      TYPE 仕様リスト.
027800  01  イテレータオブジェクト          OBJECT REFERENCE リストイテレータ.
027900  01  共通オブジェクト                OBJECT REFERENCE.
028000 LINKAGE         SECTION.
028100  01  Ｌ−仕様名                      PIC X(4).
028200  01  Ｌ−仕様オブジェクト            OBJECT REFERENCE 仕様クラス.
028300 PROCEDURE       DIVISION
028400                    USING             Ｌ−仕様名
028500                    RETURNING         Ｌ−仕様オブジェクト.
028600     INVOKE  リストイテレータ         "イテレータ生成"
028700                                      USING BY CONTENT 仕様リストオブジェクト
028800                                      RETURNING        イテレータオブジェクト
028900     INVOKE  イテレータオブジェクト   "先頭要素設定"
029000     PERFORM TEST BEFORE 
029100      UNTIL イテレータオブジェクト :: "要素確認" = ZOFF
029200       INVOKE イテレータオブジェクト  "現在の要素取得"
029300                                      RETURNING        共通オブジェクト
029400       SET    仕様オブジェクト    TO  共通オブジェクト AS 仕様クラス
029500       INVOKE 仕様オブジェクト        "仕様参照"
029600                                      RETURNING        仕様データ
029700       IF  仕様名 OF 仕様データ  = Ｌ−仕様名 THEN
029800         SET  Ｌ−仕様オブジェクト    TO  仕様オブジェクト
029900         SET  イテレータオブジェクト  TO  NULL
030000         EXIT METHOD
030100       END-IF
030200       INVOKE イテレータオブジェクト "次要素設定"
030300     END-PERFORM
030400*
030500*　一致する仕様名がなかった場合、返却する仕様オブジェクトにNULLを設定する。
030600*　また、イテレータオブジェクトを削除する。
030700*
030800     SET  Ｌ−仕様オブジェクト       TO  NULL
030900     SET  イテレータオブジェクト     TO  NULL
031000 END METHOD 仕様情報の検索.
031100*========================================================================
031200*　仕様個数の参照メソッド
031300*
031400*　　仕様個数を取得する。
031500*
031600*  ○入力：なし
031700*　〇出力：仕様個数（PIC 9(8) BINARY)
031800*========================================================================
031900 METHOD-ID.  仕様個数の参照.
032000 DATA            DIVISION.
032100 LINKAGE         SECTION.
032200  01  Ｌ−仕様個数                    PIC 9(8) BINARY.
032300 PROCEDURE       DIVISION
032400                    RETURNING         Ｌ−仕様個数.
032500     MOVE  仕様個数               TO  Ｌ−仕様個数.
032600 END METHOD 仕様個数の参照.
032700*========================================================================
032800*　仕様リストの参照メソッド
032900*
033000*　　仕様リストオブジェクトを取得する。
033100*
033200*  ○入力：なし
033300*　〇出力：仕様リストオブジェクト（OBJECT RERERENCE List)
033400*========================================================================
033500 METHOD-ID.  仕様リストの参照.
033600 DATA            DIVISION.
033700 LINKAGE         SECTION.
033800  01  Ｌ−仕様リストオブジェクト      OBJECT REFERENCE List.
033900 PROCEDURE       DIVISION
034000                    RETURNING         Ｌ−仕様リストオブジェクト.
034100     SET   Ｌ−仕様リストオブジェクト TO  仕様リストオブジェクト
034200 END METHOD 仕様リストの参照.
034300 END OBJECT.
034400 END CLASS  仕様管理クラス.
