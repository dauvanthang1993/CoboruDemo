000100*========================================================================
000200*　リストイテレータ
000300*                                   −サンプルプログラム「会議室予約」−
000400*
000500*　　リストオブジェクトの内部構造を意識せずに、要素へ順アクセスする。
000600*
000700*　〇継承クラス
000800*　　１）FJBASE
000900*
001000*　〇参照クラス
001100*　　１）List
001200*
001300*　〇オブジェクトデータ
001400*　　１）現在の要素    　　　　　−　PIC 9(8) BINARY  (PROPERTY)
001500*　　２）リストオブジェクト　　　−  OBJECT REFERENCE List  (PROPERTY)
001600*
001700*　〇ファクトリメソッド
001800*　　１）イテレータ生成
001900*
002000*　〇オブジェクトメソッド
002100*　　１）先頭要素設定
002200*　　２）次要素設定
002300*　　３）要素確認
002400*　　４）現在の要素取得
002500*
002600*            Copyright 1999-2015 FUJITSU LIMITED
002700*========================================================================
002800 IDENTIFICATION  DIVISION.
002900 CLASS-ID.   リストイテレータ INHERITS FJBASE.
003000 ENVIRONMENT     DIVISION.
003100 CONFIGURATION   SECTION.
003200 SPECIAL-NAMES.
003300     SYMBOLIC CONSTANT
003400     COPY R_CONST.
003500     .
003600 REPOSITORY.
003700     CLASS FJBASE
003800     CLASS List.
003900 FACTORY.
004000 PROCEDURE       DIVISION.
004100**========================================================================
004200*　イテレータ生成メソッド
004300*
004400*　　入力パラメタであるリストオブジェクトに対して、イテレータオブジェクト
004500*　を生成し、返却する。
004600*
004700*　〇入力：リストオブジェクト (OBJECT REFERENCE List)
004800*　〇出力：イテレータオブジェクト (OBJECT REFERENCE SELF)
004900*========================================================================
005000 METHOD-ID.  イテレータ生成.
005100 DATA            DIVISION.
005200 LINKAGE         SECTION.
005300  01  Ｌ−リストオブジェクト          OBJECT REFERENCE List.
005400  01  Ｌ−イテレータオブジェクト      OBJECT REFERENCE SELF.
005500 PROCEDURE       DIVISION
005600                    USING             Ｌ−リストオブジェクト
005700                    RETURNING         Ｌ−イテレータオブジェクト.
005800     INVOKE SELF    "NEW"             RETURNING        Ｌ−イテレータオブジェクト.
005900     SET  リストオブジェクト  OF  Ｌ−イテレータオブジェクト
006000                                  TO  Ｌ−リストオブジェクト
006100     MOVE   0                     TO  現在の要素  OF Ｌ−イテレータオブジェクト
006200 END METHOD イテレータ生成.
006300 END FACTORY.
006400*
006500 OBJECT.
006600 DATA DIVISION.
006700 WORKING-STORAGE SECTION.
006800  01  現在の要素                      PIC 9(8) BINARY       PROPERTY.
006900  01  リストオブジェクト              OBJECT REFERENCE List PROPERTY.
007000 PROCEDURE       DIVISION.
007100*========================================================================
007200*　先頭要素設定メソッド
007300*
007400*  リストオブジェクトの先頭要素を設定する。
007500*
007600*　〇入力：なし
007700*　〇出力：なし
007800*========================================================================
007900 METHOD-ID.  先頭要素設定.
008000 DATA            DIVISION.
008100 LINKAGE         SECTION.
008200 PROCEDURE       DIVISION.
008300     MOVE  1                      TO  現在の要素.
008400 END METHOD 先頭要素設定.
008500*========================================================================
008600*　次要素設定メソッド
008700*
008800*  リストオブジェクト内の次の要素を設定する。
008900*
009000*　〇入力：なし
009100*　〇出力：なし
009200*========================================================================
009300 METHOD-ID.  次要素設定.
009400 DATA            DIVISION.
009500 WORKING-STORAGE SECTION.
009600 LINKAGE         SECTION.
009700 PROCEDURE       DIVISION.
009800     COMPUTE 現在の要素  = 現在の要素  + 1
009900 END METHOD 次要素設定.
010000*========================================================================
010100*　要素確認メソッド
010200*
010300*  リストオブジェクト内の要素を指しているかどうかを確認する。
010400*　要素がリストオブジェクト内にあればZONを、なければZOFFを返却する。
010500*
010600*　〇入力：なし
010700*　〇出力：要素確認フラグ (PIC 1(1) BIT)
010800*========================================================================
010900 METHOD-ID.  要素確認.
011000 DATA            DIVISION.
011100 WORKING-STORAGE SECTION.
011200  01  全要素数                        PIC 9(8) BINARY.
011300 LINKAGE         SECTION.
011400  01  Ｌ−要素確認フラグ              PIC 1(1) BIT.
011500 PROCEDURE       DIVISION
011600                    RETURNING         Ｌ−要素確認フラグ.
011700     INVOKE リストオブジェクト        "CollectionSize-Get"
011800                                      RETURNING        全要素数
011900     IF 全要素数 >= 現在の要素 THEN
012000       MOVE  ZON                  TO  Ｌ−要素確認フラグ
012100     ELSE
012200       MOVE  ZOFF                 TO  Ｌ−要素確認フラグ
012300     END-IF
012400 END METHOD 要素確認.
012500*========================================================================
012600*　現在の要素取得メソッド
012700*
012800*  リストオブジェクト内の現在の要素を返却する。
012900*
013000*　〇入力：なし
013100*　〇出力：共通オブジェクト (OBJECT REFERENCE)
013200*========================================================================
013300 METHOD-ID.  現在の要素取得.
013400 DATA            DIVISION.
013500 WORKING-STORAGE SECTION.
013600 LINKAGE         SECTION.
013700  01  Ｌ−共通オブジェクト            OBJECT REFERENCE.
013800 PROCEDURE       DIVISION
013900                    RETURNING         Ｌ−共通オブジェクト.
014000     INVOKE リストオブジェクト        "Element-Get"
014100                                      USING            現在の要素
014200                                      RETURNING        Ｌ−共通オブジェクト
014300 END METHOD 現在の要素取得.
014400 END OBJECT.
014500 END CLASS  リストイテレータ.
