000100*========================================================================
000200*　予約情報クラス
000300*                                   −サンプルプログラム「会議室予約」−
000400*
000500*　　予約情報について固有なデータ及びメソッドを持つ。
000600*
000700*　〇継承クラス
000800*　　１）FJBASE
000900*
001000*　〇参照クラス
001100*　　１）List
001200*　　２）リストイテレータ
001300*　　３）エラークラス
001400*
001500*  ○ファクトリデータ
001600*　　１）予約リストオブジェクト　−　OBJECT REFERENCE List
001700*　　２）予約番号最大値　　　　　−　PIC S9(4) COMP-5
001800*　　３）予約データ個数　　　　　−　PIC 9(8)  BINARY
001900*
002000*　〇オブジェクトデータ
002100*　　１）予約データ　　　　　　　−　TYPE 予約リスト
002200*
002300*　〇ファクトリメソッド
002400*　　１）予約データの生成
002500*　　２）予約データの削除
002600*　　３）予約データの検索
002700*　　４）予約データ個数の参照
002800*　　５）予約リストオブジェクトの参照
002900*
003000*　〇オブジェクトメソッド
003100*　　１）予約データの設定
003200*　　２）予約データの参照
003300*
003400*            Copyright 1999-2015 FUJITSU LIMITED
003500*========================================================================
003600 IDENTIFICATION  DIVISION.
003700 CLASS-ID.   予約情報クラス  INHERITS FJBASE.
003800 ENVIRONMENT     DIVISION.
003900 CONFIGURATION   SECTION.
004000  SPECIAL-NAMES.
004100     SYMBOLIC CONSTANT
004200     COPY R_CONST.
004300     .
004400 REPOSITORY.
004500     CLASS FJBASE
004600     CLASS List
004700     CLASS リストイテレータ
004800     CLASS エラークラス
004900     .
005000 FACTORY.
005100 DATA            DIVISION.
005200 BASED-STORAGE   SECTION.
005300  COPY RSVINFO.
005400 WORKING-STORAGE SECTION.
005500  01 予約リストオブジェクト           OBJECT REFERENCE List.
005600  01 予約番号最大値                   PIC S9(4) COMP-5.
005700  01 予約データ個数                   PIC  9(8) BINARY.
005800 PROCEDURE       DIVISION.
005900*========================================================================
006000*　予約データの生成メソッド
006100*
006200*　　予約リストオブジェクトがNULLならば、予約リストオブジェクトを生成する。
006300*　　パラメタで渡された予約データを持つ予約オブジェクトを生成し、予約リス
006400*　トに登録する。
006500*　　予約データ個数を＋１する。
006600*
006700*　〇入力：予約データ (TYPE 予約リスト)
006800*　〇出力：予約オブジェクト（OBJECT REFERENCE SELF)
006900*========================================================================
007000 METHOD-ID.  予約データの生成.
007100 DATA            DIVISION.
007200 WORKING-STORAGE SECTION.
007300 LINKAGE         SECTION.
007400  01 Ｌ−予約データ                   TYPE 予約リスト. 
007500  01 Ｌ−予約オブジェクト             OBJECT REFERENCE SELF.
007600 PROCEDURE       DIVISION
007700                    USING             Ｌ−予約データ
007800                    RETURNING         Ｌ−予約オブジェクト.
007900*
008000*　予約リストオブジェクトがNULLの時、予約リストオブジェクトを生成し、予約
008100*　データ個数および予約番号最大値に 0を設定する。
008200*
008300     IF 予約リストオブジェクト = NULL THEN
008400       MOVE  0                    TO  予約番号最大値
008500       MOVE  0                    TO  予約データ個数
008600       SET 予約リストオブジェクト TO  List :: "NEW"
008700     END-IF
008800*
008900*　予約番号が設定されている場合（ＥＸＣＥＬシートから復元した時）、現在保
009000*　持している予約番号最大値と比較し、大きい方を予約番号最大値として置き換
009100*  える。予約番号が設定されていない場合（新規予約の場合）、予約番号最大値
009200*  に１を足した数を予約番号とする。
009300*
009400     IF 予約番号 > 0 THEN
009500       COMPUTE 予約番号最大値 = FUNCTION MAX (予約番号最大値 予約番号)
009600     ELSE
009700       COMPUTE 予約番号最大値 = 予約番号最大値 + 1
009800       MOVE    予約番号最大値     TO  予約番号
009900     END-IF
010000*
010100*　予約オブジェクトを生成し、予約データ個数を＋１する。
010200*
010300     COMPUTE 予約データ個数   = 予約データ個数 + 1
010400     SET  Ｌ−予約オブジェクト    TO  SELF :: "NEW"
010500*
010600*　予約データを設定する。
010700*
010800     INVOKE  Ｌ−予約オブジェクト     "予約データの設定" 
010900	                                    USING BY CONTENT Ｌ−予約データ
011000     INVOKE  予約リストオブジェクト   "Element-PutLast"
011100                                      USING BY CONTENT Ｌ−予約オブジェクト
011200 END METHOD 予約データの生成.
011300*========================================================================
011400*　予約データの削除メソッド
011500*
011600*　　指定された予約データを削除する。この時、予約データ個数を−１する。
011700*
011800*　〇入力：予約オブジェクト (OBJECT REFERENCE 予約情報クラス)
011900*　〇出力：なし
012000*========================================================================
012100 METHOD-ID.  予約データの削除.
012200 DATA            DIVISION.
012300 WORKING-STORAGE SECTION.
012400  01  共通オブジェクト                OBJECT REFERENCE.
012500  01  インデックス番号                PIC 9(8) BINARY.
012600  01  復帰値                          PIC 9(4) BINARY.
012700 LINKAGE         SECTION.
012800  01  Ｌ−予約オブジェクト            OBJECT REFERENCE 予約情報クラス.
012900 PROCEDURE       DIVISION
013000                    USING             Ｌ−予約オブジェクト.
013100 DECLARATIVES.
013200 ERR SECTION.
013300     USE AFTER EXCEPTION エラークラス
013400     INVOKE EXCEPTION-OBJECT          "予約オブジェクト削除エラー".
013500 END DECLARATIVES. 
013600*
013700*　指定された予約オブジェクトの位置（インデックス番号）を取得
013800*
013900     SET    共通オブジェクト          TO  Ｌ−予約オブジェクト
014000     INVOKE 予約リストオブジェクト    "ElementNo-Get"
014100                                      USING            共通オブジェクト
014200                                      RETURNING        インデックス番号
014300*
014400*　指定された予約オブジェクトが見つからなかったら、エラーを出力する。
014500*　指定された予約オブジェクトが見つかったら、予約リストから予約オブジェク
014600*　トを削除する。削除に失敗したらエラーを出力する。
014700*
014800     IF インデックス番号 NOT = 0 THEN
014900       INVOKE 予約リストオブジェクト  "Remove-At"
015000                                      USING            インデックス番号
015100                                      RETURNING        復帰値
015200       IF 復帰値 NOT = 0 THEN
015300         COMPUTE 予約データ個数 = 予約データ個数 - 1
015400       ELSE
015500         RAISE  エラークラス :: "NEW"
015600       END-IF
015700     ELSE
015800       RAISE    エラークラス :: "NEW"
015900     END-IF
016000 END METHOD 予約データの削除.
016100*========================================================================
016200*　予約データの検索メソッド
016300*
016400*　　予約者名をキーに、予約データを検索する。
016500*    注）デフォルトでは、検索した結果を５個までしか格納できない。格納でき
016600*　る数を増やしたい場合は、R_CONST.CBLに定義されているRSV-MAXの値を修正す
016700*　すること。
016800*
016900*　〇入力：予約者名 (PIC X(20))
017000*　〇出力：予約情報 (集団項目)
017100*========================================================================
017200 METHOD-ID.  予約データの検索.
017300 DATA            DIVISION.
017400 WORKING-STORAGE SECTION.
017500  01  イテレータオブジェクト          OBJECT REFERENCE リストイテレータ.
017600  01  共通オブジェクト                OBJECT REFERENCE.
017700  01  予約オブジェクト                OBJECT REFERENCE 予約情報クラス.
017800  01  予約データ                      TYPE 予約リスト.
017900 LINKAGE         SECTION.
018000  01  Ｌ−予約者名                    PIC X(20).
018100  01  Ｌ−予約情報.
018200    02 Ｌ−検索個数                   PIC 9(8) BINARY.
018300    02 Ｌ−予約データ                 TYPE 予約リスト OCCURS RSV-MAX.
018400 PROCEDURE       DIVISION
018500                    USING             Ｌ−予約者名
018600                    RETURNING         Ｌ−予約情報.
018700 DECLARATIVES.
018800 ERR SECTION.
018900     USE AFTER EXCEPTION エラークラス
019000     INVOKE EXCEPTION-OBJECT          "予約情報検索エラー".
019100 END DECLARATIVES. 
019200*
019300*　検索個数の初期化
019400*
019500     MOVE    0                    TO  Ｌ−検索個数
019600*
019700*　予約リストに登録された予約オブジェクトを順に取り出し、予約者名が一致す
019800*　る予約オブジェクトを検索する。
019900*
020000     INVOKE  リストイテレータ         "イテレータ生成"
020100                                      USING BY CONTENT 予約リストオブジェクト
020200                                      RETURNING        イテレータオブジェクト
020300     INVOKE  イテレータオブジェクト   "先頭要素設定"
020400     PERFORM TEST BEFORE 
020500      UNTIL イテレータオブジェクト :: "要素確認" = ZOFF
020600       INVOKE  イテレータオブジェクト "現在の要素取得"
020700                                      RETURNING        共通オブジェクト
020800       SET    予約オブジェクト    TO  共通オブジェクト AS 予約情報クラス
020900       INVOKE 予約オブジェクト        "予約データの参照"
021000                                      RETURNING        予約データ
021100       IF  予約者名 OF 予約データ =   Ｌ−予約者名 THEN
021200         COMPUTE  Ｌ−検索個数    =   Ｌ−検索個数 + 1
021300         MOVE  予約データ         TO  Ｌ−予約データ(Ｌ−検索個数)
021400         IF  Ｌ−検索個数 >= RSV-MAX  THEN
021500           SET  イテレータオブジェクト  TO  NULL
021600           EXIT METHOD
021700         END-IF
021800       END-IF
021900       INVOKE イテレータオブジェクト  "次要素設定"
022000     END-PERFORM
022100     SET  イテレータオブジェクト  TO  NULL
022200*
022300*　一致する情報が１つもなかった場合には、エラーメッセージを出力する。
022400*
022500     IF Ｌ−検索個数 <= 0 THEN
022600       RAISE  エラークラス  :: "NEW"
022700     END-IF
022800 END METHOD 予約データの検索.
022900*========================================================================
023000*　予約データ個数の参照メソッド
023100*
023200*　　予約データ個数を取得する。
023300*
023400*  ○入力：なし
023500*　〇出力：予約データ個数 (PIC 9(8) BINARY)
023600*========================================================================
023700 METHOD-ID.  予約データ個数の参照.
023800 DATA            DIVISION.
023900 WORKING-STORAGE SECTION.
024000 LINKAGE         SECTION.
024100  01  Ｌ−予約データ個数              PIC 9(8) BINARY.
024200 PROCEDURE       DIVISION
024300                    RETURNING         Ｌ−予約データ個数.
024400     MOVE  予約データ個数         TO  Ｌ−予約データ個数
024500 END METHOD 予約データ個数の参照.
024600*========================================================================
024700*　予約リストオブジェクトの参照メソッド
024800*
024900*　　予約リストオブジェクトを取得する。
025000*
025100*  ○入力：なし
025200*　〇出力：予約リストオブジェクト (OBJECT REFRENCE List)
025300*========================================================================
025400 METHOD-ID.  予約リストオブジェクトの参照.
025500 DATA            DIVISION.
025600 WORKING-STORAGE SECTION.
025700 LINKAGE         SECTION.
025800  01  Ｌ−予約リストオブジェクト      OBJECT REFERENCE List.
025900 PROCEDURE       DIVISION        	
026000                    RETURNING         Ｌ−予約リストオブジェクト.
026100     SET   Ｌ−予約リストオブジェクト TO  予約リストオブジェクト
026200 END METHOD 予約リストオブジェクトの参照.
026300 END FACTORY.
026400*
026500 OBJECT.
026600 DATA DIVISION.
026700 BASED-STORAGE   SECTION.
026800  COPY RSVINFO.
026900 WORKING-STORAGE SECTION.
027000  01  予約データ                  TYPE 予約リスト.
027100 PROCEDURE       DIVISION.
027200*========================================================================
027300*　予約データの設定メソッド
027400*
027500*　　予約データを設定する。
027600*
027700*　〇入力：予約データ (TYPE 予約リスト)
027800*　〇出力：なし
027900*========================================================================
028000 METHOD-ID.  予約データの設定.
028100 DATA            DIVISION.
028200 LINKAGE         SECTION.
028300  01 Ｌ−予約データ                   TYPE  予約リスト.
028400 PROCEDURE       DIVISION
028500                    USING             Ｌ−予約データ.
028600     MOVE  Ｌ−予約データ         TO  予約データ
028700 END METHOD 予約データの設定.
028800*========================================================================
028900*　予約データの参照メソッド
029000*
029100*　　予約データを取得する。
029200*
029300*　〇入力：なし
029400*　〇出力：予約データ (TYPE 予約リスト)
029500*========================================================================
029600 METHOD-ID.  予約データの参照.
029700 DATA            DIVISION.
029800 LINKAGE         SECTION.
029900  01 Ｌ−予約データ                   TYPE 予約リスト.
030000 PROCEDURE       DIVISION
030100                    RETURNING         Ｌ−予約データ.
030200     MOVE   予約データ            TO  Ｌ−予約データ
030300 END METHOD 予約データの参照.
030400 END OBJECT.
030500 END CLASS  予約情報クラス.
