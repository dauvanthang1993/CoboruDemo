000100*========================================================================
000200*　予約管理クラス
000300*                                   −サンプルプログラム「会議室予約」−
000400*
000500*　　　予約を管理するためのメソッドを持つ。
000600*
000700*　〇継承クラス
000800*　　１）シングルトン
000900*
001000*　〇参照クラス
001100*　　１）List
001200*　　２）予約情報クラス
001300*　　３）予約状態−日付クラス
001400*　　４）予約状態−会議室クラス
001500*　　５）リストイテレータ
001600*　　６）エラークラス
001700*
001800*　〇オブジェクトメソッド
001900*　　１）予約状況の一覧表示
002000*  　２）予約情報の検索
002100*　　３）予約
002200*　　４）予約取消
002300*　　５）予約状態オブジェクトの削除
002400*
002500*
002600*            Copyright 1999-2015 FUJITSU LIMITED
002700*========================================================================
002800 IDENTIFICATION  DIVISION.
002900 CLASS-ID.   予約管理クラス  INHERITS シングルトン.
003000 ENVIRONMENT     DIVISION.
003100 CONFIGURATION   SECTION.
003200 SPECIAL-NAMES.
003300     CONSOLE IS CONS
003400     SYMBOLIC CONSTANT
003500     COPY R_CONST.
003600     .
003700 REPOSITORY.
003800     CLASS List
003900     CLASS シングルトン
004000     CLASS 予約情報クラス
004100     CLASS 予約状態−日付クラス
004200     CLASS 予約状態−会議室クラス
004300     CLASS リストイテレータ
004400     CLASS エラークラス
004500     .
004600*
004700 FACTORY.
004800 DATA            DIVISION.
004900 PROCEDURE       DIVISION.
005000 END FACTORY.
005100*
005200 OBJECT.
005300 DATA DIVISION.
005400 BASED-STORAGE   SECTION.
005500  COPY ROOMINFO.
005600  COPY RSVINFO.
005700 WORKING-STORAGE SECTION.
005800 PROCEDURE       DIVISION.
005900*========================================================================
006000*　予約状況の一覧表示メソッド
006100*
006200*　　指定された予約日の予約状況の一覧を表示する。
006300*
006400*　〇入力：予約日 (PIC 9(8))
006500*　○出力：なし
006600*========================================================================
006700 METHOD-ID.  予約状況の一覧表示.
006800 DATA            DIVISION.
006900 WORKING-STORAGE SECTION.
007000  01 予約日オブジェクト               OBJECT REFERENCE 予約状態−日付クラス.
007100  01 時間枠カウンタ                   PIC 9(8) BINARY.
007200  01 会議室データ                     TYPE 会議室仕様.
007300  01 予約データ                       TYPE 予約リスト.
007400  01 予約状況                         PIC  9.
007500  01 会議室オブジェクト               OBJECT REFERENCE 予約状態−会議室クラス.
007600  01 イテレータオブジェクト           OBJECT REFERENCE リストイテレータ.
007700  01 会議室リスト                     OBJECT REFERENCE List.
007800  01 共通オブジェクト                 OBJECT REFERENCE.
007900 LINKAGE         SECTION.
008000  01 Ｌ−予約日                       PIC X(8).
008100 PROCEDURE       DIVISION
008200                    USING            Ｌ−予約日.
008300*
008400*　予約日オブジェクトの取得
008500*
008600     INVOKE  予約状態−日付クラス     "予約日の参照"
008700                                      USING BY CONTENT Ｌ−予約日
008800                                      RETURNING        予約日オブジェクト
008900*
009000*　予約日オブジェクトが持つ、会議室リストオブジェクトの取得
009100*
009200     INVOKE  予約日オブジェクト       "会議室リストオブジェクトの参照"
009300                                      RETURNING        会議室リスト
009400*
009500*　会議室リストに登録された会議室オブジェクトを順に取り出し、時間枠単位
009600*　毎の予約状況を表示する
009700*
009800     INVOKE  リストイテレータ         "イテレータ生成"
009900                                      USING BY CONTENT 会議室リスト
010000                                      RETURNING        イテレータオブジェクト
010100     INVOKE  イテレータオブジェクト   "先頭要素設定"
010200     DISPLAY "----------------------------------------------------------------------"
010300     DISPLAY  "日付   : " Ｌ−予約日
010400     PERFORM TEST BEFORE
010500      UNTIL イテレータオブジェクト :: "要素確認" = ZOFF
010600       INVOKE イテレータオブジェクト  "現在の要素取得"
010700                                      RETURNING        共通オブジェクト
010800       SET  会議室オブジェクト    TO  共通オブジェクト AS 予約状態−会議室クラス
010900*
011000*　時間枠毎の予約状況の取得
011100*
011200       MOVE    LOW-VALUE          TO  予約データ
011300       PERFORM  TEST  BEFORE  VARYING 時間枠カウンタ FROM 1 BY 1
011400                                      UNTIL 時間枠カウンタ > 2
011500         IF 時間枠カウンタ = 1 THEN
011600           MOVE  "AM"             TO  時間枠   OF 予約データ
011700         ELSE
011800           MOVE  "PM"             TO  時間枠   OF 予約データ
011900         END-IF
012000         INVOKE  会議室オブジェクト   "時間枠の予約状況参照"
012100                                      USING            予約データ
012200                                                       会議室データ
012300                                      RETURNING        予約状況
012400         INVOKE  会議室オブジェクト   "会議室情報の参照"
012500                                      RETURNING        会議室データ
012600         IF 時間枠カウンタ = 1 THEN
012700           DISPLAY  "会議室 : "       会議室名 OF 会議室データ
012800         END-IF
012900         EVALUATE  予約状況
013000         WHEN  予約
013100           DISPLAY  "  時間枠 = "     時間枠  OF 予約データ "：予約"
013200         WHEN  空き
013300           DISPLAY  "  時間枠 = "     時間枠  OF 予約データ "：空き"
013400         WHEN  終了
013500           EXIT METHOD
013600         END-EVALUATE
013700       END-PERFORM
013800       INVOKE  イテレータオブジェクト "次要素設定"
013900     END-PERFORM
014000     SET  イテレータオブジェクト  TO  NULL
014100 END METHOD 予約状況の一覧表示.
014200*========================================================================
014300*  予約情報の検索メソッド
014400*
014500*　　予約者名をキーに、予約オブジェクトを検索する。
014600*
014700*　〇入力：予約者名 (PIC X(20))
014800*　〇出力：予約情報 (集団項目) 
014900*========================================================================
015000 METHOD-ID.  予約情報の検索.
015100 DATA            DIVISION.
015200 WORKING-STORAGE SECTION.
015300 LINKAGE         SECTION.
015400  01 Ｌ−予約者名                     PIC X(20).
015500  01 Ｌ−予約情報.
015600    02 Ｌ−検索個数                   PIC 9(8) BINARY.
015700    02 Ｌ−予約データ                 TYPE 予約リスト OCCURS RSV-MAX.
015800 PROCEDURE       DIVISION
015900                    USING             Ｌ−予約者名
016000                    RETURNING         Ｌ−予約情報.
016100*
016200*　該当する予約オブジェクトの予約データと検索個数を取得する。
016300*
016400     INVOKE  予約情報クラス           "予約データの検索"
016500                                      USING BY CONTENT Ｌ−予約者名
016600                                      RETURNING        Ｌ−予約情報
016700 END METHOD 予約情報の検索.
016800*========================================================================
016900*　予約メソッド
017000*
017100*　　予約日、会議室名、時間枠からその枠の予約状況を取得し、「予約」状態な
017200*　らば、予約情報の表示とキャンセル処理に移る。「空き」状態ならば、予約処
017300*　理に移る。
017400*    入力パラメタである予約データは、予約処理後の予約データに書き換えられ
017500*　る。予約処理において、なんらかのエラーが発生した場合には、エラーコード
017600*　を返却し、予約データの中身は保証されない。
017700*
017800*　〇入力：予約データ (TYPE 予約リスト)
017900*　〇出力：エラーコード (PIC S9(8) BINARY)
018000*========================================================================
018100 METHOD-ID.  予約.
018200 DATA            DIVISION.
018300 WORKING-STORAGE SECTION.
018400  01  予約日オブジェクト              OBJECT REFERENCE 予約状態−日付クラス.
018500  01  予約データ                      TYPE 予約リスト.
018600  01  予約状況                        PIC  9.
018700  01 キャンセル指示子                 PIC  X.
018800 LINKAGE         SECTION.
018900  01 Ｌ−予約データ                   TYPE 予約リスト.
019000  01 Ｌ−会議室データ                 TYPE 会議室仕様.
019100  01 Ｌ−処理コード                   PIC  S9(8) BINARY.
019200 PROCEDURE       DIVISION
019300                    USING             Ｌ−予約データ
019400                                      Ｌ−会議室データ
019500                    RETURNING         Ｌ−処理コード.
019600*
019700*　エラーコードの初期化
019800*
019900     MOVE    0                    TO  エラーコード OF エラークラス
020000     MOVE    0                    TO  Ｌ−処理コード
020100*
020200*  予約日オブジェクトの取得
020300*
020400     INVOKE  予約状態−日付クラス     "予約日の参照"
020500                                      USING BY CONTENT 予約日 OF Ｌ−予約データ
020600                                      RETURNING        予約日オブジェクト
020700*
020800*  指定した会議室時間枠の予約状況取得
020900* 
021000     INVOKE  予約日オブジェクト       "会議室の予約状況参照"
021100                                      USING            Ｌ−予約データ
021200                                                       Ｌ−会議室データ
021300                                      RETURNING        予約状況
021400*
021500*　エラークラスのコードを見て、エラーが発生していれば、エラーコードを設定
021600*  して復帰する。
021700*
021800     IF  エラーコード OF エラークラス < 0 THEN
021900       MOVE  エラーコード OF エラークラス TO  Ｌ−処理コード
022000       EXIT METHOD
022100     END-IF
022200*
022300*  予約状況が「予約」の時は、取得した予約データに書き換えれているため、そ
022400*  のまま呼出し元に復帰する。「空き」の時は、予約データを新規に入力する。
022500*
022600     EVALUATE 予約状況
022700     WHEN      予約
022800       MOVE    予約−既存         TO  Ｌ−処理コード
022900     WHEN      空き
023000       IF  予約番号  OF Ｌ−予約データ = 0 THEN
023100          MOVE    予約−新規      TO  Ｌ−処理コード
023200          DISPLAY "----------------------------------------------------------------------"
023300          DISPLAY " <<予約情報の入力>>"
023400          DISPLAY "　予約者名（10文字まで：例.富士通太郎）：　" WITH NO ADVANCING
023500          ACCEPT   予約者名 OF Ｌ−予約データ FROM CONS
023600          DISPLAY "　内線　　（数字9桁まで：例.1234-5678）：　" WITH NO ADVANCING
023700          ACCEPT   内線     OF Ｌ−予約データ FROM CONS
023800          DISPLAY "　所属　　（10文字まで：例.勤労課）　　：　" WITH NO ADVANCING
023900          ACCEPT   所属     OF Ｌ−予約データ FROM CONS
024000          MOVE     0              TO  予約番号  OF Ｌ−予約データ
024100          INVOKE 予約日オブジェクト   "会議室の予約状況設定"
024200                                      USING            Ｌ−予約データ
024300       ELSE
024400          MOVE    予約−復元      TO  Ｌ−処理コード
024500          INVOKE  予約日オブジェクト  "会議室の予約状況設定"
024600                                      USING            Ｌ−予約データ
024700       END-IF
024800     END-EVALUATE
024900 END METHOD 予約.
025000*========================================================================
025100*　予約取消メソッド
025200*
025300*　　予約キャンセル処理を行う。
025400*
025500*　〇入力：予約データ (TYPE 予約リスト)
025600*　〇出力：なし
025700*========================================================================
025800 METHOD-ID.  予約取消.
025900 DATA            DIVISION.
026000 WORKING-STORAGE SECTION.
026100  01 キャンセル番号                   PIC S9(4) COMP-5.
026200  01 予約日オブジェクト               OBJECT REFERENCE 予約状態−日付クラス.
026300 LINKAGE         SECTION.
026400  01 Ｌ−予約データ                   TYPE 予約リスト.
026500 PROCEDURE       DIVISION
026600                    USING             Ｌ−予約データ.
026700 DECLARATIVES.
026800 ERR SECTION.
026900     USE AFTER EXCEPTION エラークラス
027000     INVOKE EXCEPTION-OBJECT          "予約キャンセルエラー".
027100 END DECLARATIVES.  
027200*
027300*　予約日オブジェクトの取得
027400*
027500     INVOKE  予約状態−日付クラス     "予約日の参照"
027600                                      USING BY CONTENT 予約日 OF Ｌ−予約データ
027700                                      RETURNING        予約日オブジェクト
027800*
027900* 予約番号の取得
028000*
028100     DISPLAY "----------------------------------------------------------------------"
028200     DISPLAY "キャンセルには予約時の予約番号が必要です。予約番号を入力してください。"
028300     DISPLAY "　予約番号（数字４桁：例.0001）:　" WITH NO ADVANCING
028400     ACCEPT  キャンセル番号  FROM  CONS
028500*
028600     IF キャンセル番号 = 予約番号 OF  Ｌ−予約データ THEN
028700       MOVE  -1                   TO  予約番号  OF Ｌ−予約データ
028800       INVOKE 予約日オブジェクト      "会議室の予約状況設定"
028900                                      USING BY CONTENT Ｌ−予約データ
029000     ELSE
029100       RAISE  エラークラス  :: "NEW"
029200     END-IF
029300 END METHOD 予約取消.
029400*========================================================================
029500*　予約状態オブジェクトの削除メソッド
029600*
029700*　　予約状態オブジェクトの全削除を行う。
029800*
029900*　〇入力：なし
030000*　〇出力：なし
030100*========================================================================
030200 METHOD-ID.  予約状態オブジェクトの削除.
030300 DATA            DIVISION.
030400 WORKING-STORAGE SECTION.
030500 PROCEDURE       DIVISION.
030600     INVOKE  予約状態−日付クラス     "予約日の全削除"
030700 END METHOD 予約状態オブジェクトの削除.
030800 END OBJECT.
030900 END CLASS  予約管理クラス.
