000100*========================================================================
000200*　会議室情報管理クラス
000300*                                   −サンプルプログラム「会議室予約」−
000400*
000500*　　会議室情報を管理するためのメソッドを持つ。
000600*
000700*　〇継承クラス
000800*　　１）シングルトン
000900*　　２）仕様管理クラス
001000*
001100*　〇参照クラス
001200*　　１）List
001300*　　２）会議室情報クラス
001400*　　３）リストイテレータ
001500*　　４）エラークラス
001600*
001700*　〇オブジェクトメソッド
001800*　　１）初期化　（INIT）
001900*　　２）会議室情報の一覧表示
002000*
002100*            Copyright 1999-2015 FUJITSU LIMITED
002200*========================================================================
002300 IDENTIFICATION  DIVISION.
002400 CLASS-ID.   会議室情報管理クラス  INHERITS シングルトン 仕様管理クラス.
002500 ENVIRONMENT     DIVISION.
002600 CONFIGURATION   SECTION.
002700 SPECIAL-NAMES.
002800     SYMBOLIC CONSTANT
002900     COPY R_CONST.
003000     .
003100 REPOSITORY.
003200     CLASS List
003300     CLASS シングルトン
003400     CLASS 仕様管理クラス
003500     CLASS 会議室情報クラス
003600     CLASS エラークラス
003700     CLASS リストイテレータ
003800     .
003900*
004000 FACTORY.
004100 DATA            DIVISION.
004200 WORKING-STORAGE SECTION.
004300 PROCEDURE       DIVISION.
004400 END FACTORY.
004500*
004600 OBJECT.
004700 DATA DIVISION.
004800 BASED-STORAGE   SECTION.
004900  COPY ROOMINFO.
005000 WORKING-STORAGE SECTION.
005100 PROCEDURE       DIVISION.
005200*========================================================================
005300*　初期化（INIT）
005400*
005500*　　仕様クラスの設定メソッドを呼び出して会議室情報クラスを設定する。
005600*　　また、会議室情報個数および会議室情報リストオブジェクトの初期化を行う
005700*　為に、INITメソッドを呼び出す。 
005800*
005900*　〇入力：なし
006000*　〇出力：なし
006100*========================================================================
006200 METHOD-ID.  INIT  OVERRIDE.
006300 DATA            DIVISION.
006400 WORKING-STORAGE SECTION.
006500 PROCEDURE       DIVISION.
006600     INVOKE  SELF                     "仕様クラスの設定"
006700                                      USING BY CONTENT 会議室情報クラス
006800     INVOKE  仕様管理クラス OF SUPER  "INIT"
006900 END METHOD INIT.
007000*========================================================================
007100*　会議室情報の一覧表示
007200*
007300*　　会議室情報の一覧を表示する。
007400*
007500*　〇入力：なし
007600*　〇出力：なし
007700*========================================================================
007800 METHOD-ID.  会議室情報の一覧表示.
007900 DATA            DIVISION.
008000 WORKING-STORAGE SECTION.
008100  01 会議室データ                     TYPE 会議室仕様.
008200  01 イテレータオブジェクト           OBJECT REFERENCE リストイテレータ.
008300  01 共通オブジェクト                 OBJECT REFERENCE.
008400  01 会議室情報リストオブジェクト     OBJECT REFERENCE List.
008500  01 会議室データ−表示用.
008600    02                                PIC X(1).
008700    02  Ｗ−会議室名                  PIC X(4).
008800    02                                PIC X(7).
008900    02  Ｗ−定員                      PIC X(3).
009000    02                                PIC X(4).
009100    02  Ｗ−内線                      PIC X(9).
009200    02                                PIC X(2).
009300    02  Ｗ−種類                      PIC N(10).
009400 LINKAGE         SECTION.
009500 PROCEDURE       DIVISION.
009600 DECLARATIVES.
009700 ERR SECTION.
009800     USE AFTER EXCEPTION エラークラス
009900     INVOKE EXCEPTION-OBJECT          "会議室情報検索エラー".
010000 END DECLARATIVES.  
010100*
010200*  会議室情報リストオブジェクトの取得
010300*
010400     SET  会議室情報リストオブジェクト  
010500                                  TO  仕様管理クラス OF SUPER :: "仕様リストの参照"
010600*
010700*  会議室情報リストオブジェクトに対してイテレータオブジェクトを生成し、会
010800*　議室情報リストに登録された順に会議室情報オブジェクトを取得する。
010900*
011000 
011100     INVOKE  リストイテレータ         "イテレータ生成"
011200                                      USING BY CONTENT 会議室情報リストオブジェクト
011300                                      RETURNING        イテレータオブジェクト
011400     INVOKE  イテレータオブジェクト   "先頭要素設定"
011500*
011600*　会議室情報オブジェクトが１つもなければ、エラーメッセージを出力する
011700*
011800     IF  イテレータオブジェクト   ::  "要素確認" = ZOFF THEN
011900       RAISE エラークラス :: "NEW" 
012000       SET イテレータオブジェクト TO  NULL
012100       EXIT METHOD
012200     END-IF
012300*
012400*　会議室情報オブジェクトがあれば、会議室情報を表示する
012500*
012600     DISPLAY "=============================================="
012700     DISPLAY "-会議室名---定員---内線-------種類------------"
012800     PERFORM TEST BEFORE 
012900      UNTIL イテレータオブジェクト :: "要素確認" = ZOFF
013000       INVOKE イテレータオブジェクト  "現在の要素取得"
013100                                      RETURNING        共通オブジェクト
013200       MOVE  共通オブジェクト AS 会議室情報クラス :: "仕様参照"  
013300                                  TO  会議室データ
013400       MOVE  SPACE                TO  会議室データ−表示用
013500       MOVE  会議室名             TO  Ｗ−会議室名
013600       MOVE  定員                 TO  Ｗ−定員
013700       MOVE  内線                 TO  Ｗ−内線
013800       EVALUATE   種類 
013900       WHEN  "N"
014000         MOVE  NC"一般会議室"     TO  Ｗ−種類
014100       WHEN  "T"
014200         MOVE  NC"テレビ会議室"   TO  Ｗ−種類
014300       END-EVALUATE
014400       DISPLAY 会議室データ−表示用
014500       INVOKE イテレータオブジェクト  "次要素設定"
014600     END-PERFORM
014700     DISPLAY "=============================================="
014800*
014900*　イテレータオブジェクトの削除
015000*
015100     SET イテレータオブジェクト   TO  NULL
015200 END METHOD 会議室情報の一覧表示.
015300 END OBJECT.
015400 END CLASS  会議室情報管理クラス.
