000100*========================================================================
000200*   予約状態−会議室クラス
000300*                                   −サンプルプログラム「会議室予約」−
000400*
000500*　　　会議室単位の予約状態について固有なデータ及びメソッドを持つ。
000600*
000700*　〇継承クラス
000800*　　１）FJBASE
000900*
001000*　〇参照クラス
001100*　　１）List
001200*　　２）予約状態−時間枠クラス
001300*　　３）会議室情報管理クラス
001400*　　４）会議室情報クラス
001500*　　５）リストイテレータ
001600*　　６）エラークラス
001700*
001800*　〇オブジェクトデータ
001900*　　１）ＡＭ時間枠オブジェクト   −  OBJECT REFERENCE 予約状態−時間枠クラス.
002000*　　２）ＰＭ時間枠オブジェクト   −  OBJECT REFERENCE 予約状態−時間枠クラス.
002100*　　３）会議室情報オブジェクト   −  OBJECT REFERENCE 会議室情報クラス.
002200*
002300*　〇ファクトリメソッド
002400*　　１）会議室オブジェクトの生成
002500*　　２）会議室の検索
002600*　　３）会議室の全削除
002700*
002800*　〇オブジェクトメソッド
002900*　　１）会議室の初期化
003000*　　２）会議室の削除
003100*　　３）会議室情報の参照
003200*　　４）時間枠の予約状況参照
003300*　　５）時間枠の予約状況設定
003400*
003500*            Copyright 1999-2015 FUJITSU LIMITED
003600*========================================================================
003700 IDENTIFICATION  DIVISION.
003800 CLASS-ID.   予約状態−会議室クラス  INHERITS FJBASE.
003900 ENVIRONMENT     DIVISION.
004000 CONFIGURATION   SECTION.
004100 SPECIAL-NAMES.
004200     SYMBOLIC CONSTANT
004300     COPY R_CONST.
004400     .
004500 REPOSITORY.
004600     CLASS FJBASE
004700     CLASS List
004800     CLASS 予約状態−時間枠クラス
004900     CLASS 会議室情報管理クラス
005000     CLASS 会議室情報クラス
005100     CLASS エラークラス
005200     CLASS リストイテレータ
005300     .
005400 FACTORY.
005500 DATA            DIVISION.
005600 BASED-STORAGE   SECTION.
005700     COPY  ROOMINFO.
005800 PROCEDURE       DIVISION.
005900*========================================================================
006000*　会議室オブジェクトの生成メソッド
006100*
006200*　　まず、会議室リストオブジェクトを生成し、次に会議室情報個数分の会議室
006300*　オブジェクトを生成し、会議室リストオブジェクトに登録する。
006400*
006500*　〇入力：なし
006600*　〇出力：会議室リストオブジェクト（OBJECT REFERENCE List)
006700*========================================================================
006800 METHOD-ID.  会議室オブジェクトの生成.
006900 DATA            DIVISION.
007000 WORKING-STORAGE SECTION.
007100  01 会議室管理オブジェクト           OBJECT REFERENCE 会議室情報管理クラス.
007200  01 会議室情報リストオブジェクト     OBJECT REFERENCE List.
007300  01 会議室情報オブジェクト           OBJECT REFERENCE 会議室情報クラス.
007400  01 会議室オブジェクト               OBJECT REFERENCE SELF.
007500  01 共通オブジェクト                 OBJECT REFERENCE.
007600  01 イテレータオブジェクト           OBJECT REFERENCE リストイテレータ.
007700 LINKAGE         SECTION.
007800  01 Ｌ−会議室リストオブジェクト     OBJECT REFERENCE List.
007900 PROCEDURE       DIVISION
008000                    RETURNING         Ｌ−会議室リストオブジェクト.
008100*
008200*　会議室リストオブジェクトを生成
008300*
008400     SET Ｌ−会議室リストオブジェクト TO  List :: "NEW"
008500*
008600*　会議室情報リストオブジェトを取得する
008700*
008800     INVOKE  会議室情報管理クラス ::  "NEW"  "仕様リストの参照"
008900                                      RETURNING        会議室情報リストオブジェクト
009000*
009100*　会議室情報リストに登録された会議室情報オブジェクトを順に取り出し、会議
009200*　室オブジェクトに登録する。
009300*
009400     INVOKE  リストイテレータ         "イテレータ生成"
009500                                      USING BY CONTENT 会議室情報リストオブジェクト
009600                                      RETURNING        イテレータオブジェクト
009700     INVOKE  イテレータオブジェクト   "先頭要素設定"
009800     PERFORM TEST BEFORE 
009900      UNTIL イテレータオブジェクト :: "要素確認" = ZOFF
010000       INVOKE  イテレータオブジェクト "現在の要素取得"
010100                                      RETURNING        共通オブジェクト
010200       SET 会議室情報オブジェクト TO  共通オブジェクト AS 会議室情報クラス
010300*
010400*　会議室オブジェクトの生成と初期化
010500*
010600       INVOKE  SELF  "NEW"            RETURNING        会議室オブジェクト
010700       INVOKE  会議室オブジェクト     "会議室の初期化"
010800                                      USING BY CONTENT 会議室情報オブジェクト
010900       INVOKE Ｌ−会議室リストオブジェクト  "Element-PutLast"
011000                                      USING BY CONTENT 会議室オブジェクト
011100       INVOKE イテレータオブジェクト  "次要素設定"
011200     END-PERFORM
011300     SET  イテレータオブジェクト  TO  NULL
011400 END METHOD 会議室オブジェクトの生成.
011500*========================================================================
011600*　会議室の検索メソッド
011700*                                              
011800*　　会議室名をキーに、会議室オブジェクトを検索する。
011900*
012000*　〇入力：会議室名（PIC X(4))
012100*　　　　　会議室リストオブジェクト (OBJECT REFERENCE List)
012200*　〇出力：会議室オブジェクト（OBJECT REFERENCE 予約状態−会議室クラス)
012300*========================================================================
012400 METHOD-ID.  会議室の検索.
012500 DATA            DIVISION.
012600 WORKING-STORAGE SECTION.
012700  01 会議室オブジェクト           OBJECT REFERENCE 予約状態−会議室クラス.
012800  01 会議室データ                 TYPE 会議室仕様.
012900  01 イテレータオブジェクト       OBJECT REFERENCE リストイテレータ.
013000  01 共通オブジェクト             OBJECT REFERENCE.
013100 LINKAGE         SECTION.
013200  01 Ｌ−会議室名                 PIC X(4).
013300  01 Ｌ−会議室リストオブジェクト OBJECT REFERENCE List.
013400  01 Ｌ−会議室オブジェクト       OBJECT REFERENCE 予約状態−会議室クラス.
013500 PROCEDURE       DIVISION
013600                    USING         Ｌ−会議室名
013700                                  Ｌ−会議室リストオブジェクト
013800                    RETURNING     Ｌ−会議室オブジェクト.
013900*
014000*　会議室リストに登録された会議室情報オブジェクトを順に取り出し、指定され
014100*　た会議室名と一致する会議室オブジェクトを検索する。
014200*
014300     INVOKE  リストイテレータ         "イテレータ生成"
014400                                      USING BY CONTENT Ｌ−会議室リストオブジェクト
014500                                      RETURNING        イテレータオブジェクト
014600     INVOKE  イテレータオブジェクト   "先頭要素設定"
014700     PERFORM TEST BEFORE
014800      UNTIL イテレータオブジェクト :: "要素確認" = ZOFF
014900       INVOKE  イテレータオブジェクト "現在の要素取得"
015000                                      RETURNING        共通オブジェクト
015100       SET     会議室オブジェクト     TO  共通オブジェクト AS 予約状態−会議室クラス
015200*
015300*　会議室オブジェクトが持つ会議室情報を取得し、会議室名を検索対象と比較する。
015400*
015500       INVOKE  会議室オブジェクト     "会議室情報の参照"
015600                                      RETURNING        会議室データ
015700       IF 会議室名 OF 会議室データ = Ｌ−会議室名  THEN
015800         SET  Ｌ−会議室オブジェクト  TO  会議室オブジェクト
015900         SET  イテレータオブジェクト  TO  NULL
016000         EXIT METHOD
016100       END-IF
016200       INVOKE イテレータオブジェクト  "次要素設定"
016300     END-PERFORM
016400*
016500*　該当する会議室オブジェクトが見つからなかった場合、返却する会議室オブジ
016600*　ェクトにはNULLを設定する。
016700*
016800     SET  イテレータオブジェクト      TO  NULL
016900     SET  Ｌ−会議室オブジェクト      TO  NULL
017000 END METHOD 会議室の検索.
017100*========================================================================
017200*　会議室の全削除メソッド
017300*                                              
017400*　　会議室リストオブジェクトに登録された会議室オブジェクトを全て削除する。
017500*
017600*  ○入力：会議室リストオブジェクト (OBJECT REFERENCE List)
017700*　〇出力：なし
017800*========================================================================
017900 METHOD-ID.  会議室の全削除.
018000 DATA            DIVISION.
018100 WORKING-STORAGE SECTION.
018200  01 会議室オブジェクト               OBJECT REFERENCE 予約状態−会議室クラス.
018300  01 イテレータオブジェクト           OBJECT REFERENCE リストイテレータ.
018400  01 共通オブジェクト                 OBJECT REFERENCE.
018500 LINKAGE         SECTION.
018600  01 Ｌ−会議室リストオブジェクト     OBJECT REFERENCE List.
018700 PROCEDURE       DIVISION
018800                    USING             Ｌ−会議室リストオブジェクト.
018900*
019000*　会議室リストに登録された会議室オブジェクトを順に削除する。
019100*
019200     INVOKE  リストイテレータ         "イテレータ生成"
019300                                      USING BY CONTENT Ｌ−会議室リストオブジェクト
019400                                      RETURNING        イテレータオブジェクト
019500     INVOKE  イテレータオブジェクト   "先頭要素設定"
019600     PERFORM TEST BEFORE 
019700      UNTIL イテレータオブジェクト :: "要素確認" = ZOFF
019800       INVOKE  イテレータオブジェクト "現在の要素取得"
019900                                      RETURNING        共通オブジェクト
020000       SET   会議室オブジェクト   TO  共通オブジェクト AS 予約状態−会議室クラス
020100*
020200*　会議室オブジェクトの削除
020300*
020400       INVOKE  会議室オブジェクト     "会議室の削除"
020500       INVOKE  イテレータオブジェクト "次要素設定"
020600     END-PERFORM
020700*
020800*　イテレータオブジェクトの削除
020900*
021000     SET  イテレータオブジェクト  TO  NULL
021100 END METHOD 会議室の全削除.
021200 END FACTORY.
021300*
021400 OBJECT.
021500 DATA DIVISION.
021600 BASED-STORAGE   SECTION.
021700  COPY RSVINFO.
021800  COPY ROOMINFO.
021900 WORKING-STORAGE SECTION.
022000  01 時間枠.
022100    02 ＡＭ時間枠オブジェクト         OBJECT REFERENCE 予約状態−時間枠クラス.
022200    02 ＰＭ時間枠オブジェクト         OBJECT REFERENCE 予約状態−時間枠クラス.
022300  01 会議室情報オブジェクト           OBJECT REFERENCE 会議室情報クラス.
022400 PROCEDURE       DIVISION.
022500*========================================================================
022600*　会議室の初期化メソッド
022700*
022800*　　会議室オブジェクトの初期情報を設定する。
022900*　　会議室オブジェクトが持つ、AM用／PM用の時間枠オブジェクトを生成する。
023000*　また、パラメタとして渡された会議室情報オブジェクトを設定する。
023100*
023200*  ○入力：会議室情報オブジェクト　(OBJECT REFRENCE 会議室情報クラス)
023300*　〇出力：なし
023400*========================================================================
023500 METHOD-ID.  会議室の初期化.
023600 DATA            DIVISION.
023700 WORKING-STORAGE SECTION.
023800  01 時間枠                           PIC X(4).
023900 LINKAGE         SECTION.
024000  01 Ｌ−会議室情報オブジェクト       OBJECT REFERENCE 会議室情報クラス.
024100 PROCEDURE       DIVISION
024200                    USING             Ｌ−会議室情報オブジェクト.
024300     MOVE    "AM"                 TO  時間枠
024400     INVOKE  予約状態−時間枠クラス   "時間枠オブジェクトの生成"
024500                                      USING BY CONTENT 時間枠
024600                                      RETURNING        ＡＭ時間枠オブジェクト
024700     MOVE    "PM"                 TO  時間枠
024800     INVOKE  予約状態−時間枠クラス  "時間枠オブジェクトの生成"
024900                                      USING BY CONTENT 時間枠
025000                                      RETURNING        ＰＭ時間枠オブジェクト
025100     SET  会議室情報オブジェクト  TO  Ｌ−会議室情報オブジェクト
025200 END METHOD 会議室の初期化.
025300*========================================================================
025400*　会議室の削除メソッド
025500*
025600*　　会議室オブジェクトが持つ、AM用／PM用の時間枠オブジェクトを削除してか
025700*　ら、会議室情報オブジェクトを削除する。
025800*
025900*  ○入力：なし
026000*　〇出力：なし
026100*========================================================================
026200 METHOD-ID.  会議室の削除.
026300 DATA            DIVISION.
026400 WORKING-STORAGE SECTION.
026500 LINKAGE         SECTION.
026600 PROCEDURE       DIVISION.
026700     INVOKE ＡＭ時間枠オブジェクト    "時間枠の削除"
026800     INVOKE ＰＭ時間枠オブジェクト    "時間枠の削除"
026900     SET   ＡＭ時間枠オブジェクト TO  NULL
027000     SET   ＰＭ時間枠オブジェクト TO  NULL
027100     SET   会議室情報オブジェクト TO  NULL
027200 END METHOD 会議室の削除.
027300*========================================================================
027400*　会議室情報の参照メソッド
027500*
027600*　　会議室オブジェクトが持つ会議室情報オブジェクトから会議室データを取得
027700*　する。
027800*
027900*　〇入力：なし
028000*　〇出力：会議室データ (TYPE 会議室仕様)
028100*========================================================================
028200 METHOD-ID.  会議室情報の参照.
028300 DATA            DIVISION.
028400 LINKAGE         SECTION.
028500  01 Ｌ−会議室データ                 TYPE 会議室仕様.
028600 PROCEDURE       DIVISION
028700                    RETURNING         Ｌ−会議室データ.
028800     INVOKE 会議室情報オブジェクト    "仕様参照"
028900                                      RETURNING        Ｌ−会議室データ.
029000 END METHOD 会議室情報の参照.
029100*========================================================================
029200*　時間枠の予約状況参照メソッド
029300*                                                  
029400*　　指定した時間枠の予約状況を取得する。時間枠の予約状態が「予約」ならば、
029500*　予約データと会議室データも取得する。
029600*
029700*　〇入力：予約データ (TYPE 予約リスト)
029800*　〇出力：予約状況   (PIC 9)
029900*========================================================================
030000 METHOD-ID.  時間枠の予約状況参照.
030100 DATA            DIVISION.
030200 WORKING-STORAGE SECTION.
030300  01  時間枠オブジェクト              OBJECT REFERENCE 予約状態−時間枠クラス.
030400 LINKAGE         SECTION.
030500  01  Ｌ−予約データ                  TYPE 予約リスト.
030600  01  Ｌ−会議室データ                TYPE 会議室仕様.
030700  01  Ｌ−予約状況                    PIC 9.
030800 PROCEDURE       DIVISION
030900                    USING             Ｌ−予約データ
031000                                      Ｌ−会議室データ
031100                    RETURNING         Ｌ−予約状況.
031200 DECLARATIVES.
031300 ERR SECTION.
031400     USE AFTER EXCEPTION エラークラス
031500     INVOKE EXCEPTION-OBJECT          "時間枠オブジェクト検索エラー".
031600 END DECLARATIVES.  
031700*
031800*  時間枠オブジェクトの取得
031900*
032000     EVALUATE 時間枠
032100     WHEN  "AM"
032200       SET  時間枠オブジェクト    TO  ＡＭ時間枠オブジェクト
032300     WHEN  "PM"
032400       SET  時間枠オブジェクト    TO  ＰＭ時間枠オブジェクト
032500     WHEN  OTHER
032600       RAISE   エラークラス  :: "NEW"
032700       EXIT METHOD
032800     END-EVALUATE
032900*
033000*  予約状況の取得
033100*
033200     INVOKE  時間枠オブジェクト       "時間枠の予約状況参照"
033300                                      RETURNING        Ｌ−予約状況
033400*
033500*  予約状況が「予約」の場合、予約データと会議室仕様データを取得する。
033600*
033700     IF Ｌ−予約状況 = 予約 THEN
033800       INVOKE  時間枠オブジェクト     "時間枠の予約データ参照"
033900                                      RETURNING        Ｌ−予約データ
034000       INVOKE  SELF                   "会議室情報の参照"
034100                                      RETURNING        Ｌ−会議室データ
034200     END-IF
034300 END METHOD 時間枠の予約状況参照.
034400*========================================================================
034500*　時間枠の予約状況設定メソッド
034600*                                                  
034700*　　指定した時間枠に予約データを設定する。
034800*
034900*　〇入力：予約データ (TYPE 予約リスト)
035000*　〇出力：なし
035100*========================================================================
035200 METHOD-ID.  時間枠の予約状況設定.
035300 DATA            DIVISION.
035400 WORKING-STORAGE SECTION.
035500  01 時間枠オブジェクト               OBJECT REFERENCE 予約状態−時間枠クラス.
035600 LINKAGE         SECTION.
035700  01 Ｌ−予約データ                   TYPE 予約リスト.
035800 PROCEDURE       DIVISION
035900                    USING             Ｌ−予約データ.
036000 DECLARATIVES.
036100 ERR SECTION.
036200     USE AFTER EXCEPTION エラークラス
036300     INVOKE EXCEPTION-OBJECT          "時間枠オブジェクト検索エラー".
036400 END DECLARATIVES.  
036500*
036600*  時間枠オブジェクトの取得
036700*
036800     EVALUATE 時間枠
036900     WHEN  "AM"
037000       SET  時間枠オブジェクト    TO  ＡＭ時間枠オブジェクト
037100     WHEN  "PM"
037200       SET  時間枠オブジェクト    TO  ＰＭ時間枠オブジェクト
037300     WHEN  OTHER
037400       RAISE  エラークラス  :: "NEW"
037500       EXIT METHOD
037600     END-EVALUATE
037700*
037800*  予約データを設定する。
037900*
038000     INVOKE  時間枠オブジェクト       "時間枠の予約データ設定"
038100                                      USING            Ｌ−予約データ
038200 END METHOD 時間枠の予約状況設定.
038300 END OBJECT.
038400 END CLASS  予約状態−会議室クラス.
