000100*========================================================================
000200*　予約状態−日付クラス
000300*                                   −サンプルプログラム「会議室予約」−
000400*
000500*　　日付単位の予約状態について固有なデータ及びメソッドを持つ。
000600*
000700*　〇継承クラス
000800*　　１）FJBASE
000900*
001000*　〇継承クラス
001100*　　１）Dict
001200*　　２）List
001300*　　３）予約状態−会議室クラス
001400*　　４）エラークラス
001500*
001600*　〇ファクトリデータ
001700*　　１）予約日辞書オブジェクト　−　OBJECT REFERENCE Dict
001800*
001900*　○オブジェクトデータ
002000*　　１）予約日　　　　　　　　　−　PIC 9(8)
002100*　　２）会議室リストオブジェクト−　OBJECT REFERENCE List
002200*
002300*　〇ファクトリメソッド
002400*　　１）予約日の参照
002500*　　２）予約日の全削除
002600*
002700*　〇オブジェクトメソッド
002800*　　１）予約日の初期化
002900*　　２）予約日の削除
003000*　　３）会議室の予約状況参照
003100*　　４）会議室の予約状況設定
003200*　　５）会議室リストオブジェクトの参照
003300*
003400*            Copyright 1999-2015 FUJITSU LIMITED
003500*========================================================================
003600 IDENTIFICATION  DIVISION.
003700 CLASS-ID.   予約状態−日付クラス  INHERITS FJBASE.
003800 ENVIRONMENT     DIVISION.
003900 CONFIGURATION   SECTION.
004000 SPECIAL-NAMES.
004100     SYMBOLIC CONSTANT
004200     COPY R_CONST.
004300     .
004400 REPOSITORY.
004500     CLASS FJBASE
004600     CLASS Dict
004700     CLASS List
004800     CLASS 予約状態−会議室クラス
004900     CLASS エラークラス
005000     .
005100 FACTORY.
005200 DATA            DIVISION.
005300 WORKING-STORAGE SECTION.
005400  01 予約日辞書オブジェクト           OBJECT REFERENCE Dict.
005500 PROCEDURE       DIVISION.
005600*========================================================================
005700*　予約日の参照メソッド
005800*
005900*　　予約日辞書オブジェクトがNULLの時、予約日辞書オブジェクトを生成する。
006000*　　予約日をキーに、予約日辞書オブジェクトにその予約日と一致する予約日オ
006100*　ブジェクトがあるか否かを検索する。該当する予約日オブジェクトがない場合
006200*　新たに生成する。
006300*
006400*　〇入力：予約日 (PIC 9(8))
006500*　〇出力：予約日オブジェクト（OBJECT REFERENCE 予約状態−日付クラス)
006600*========================================================================
006700 METHOD-ID.  予約日の参照.
006800 DATA            DIVISION.
006900 WORKING-STORAGE SECTION.
007000  01 共通オブジェクト                 OBJECT REFERENCE.
007100  01 会議室リストオブジェクト         OBJECT REFERENCE List.
007200 LINKAGE         SECTION.
007300  01 Ｌ−予約日                       PIC 9(8).
007400  01 Ｘ−予約日                       REDEFINES Ｌ−予約日 PIC X(8).
007500  01 Ｌ−予約日オブジェクト           OBJECT REFERENCE 予約状態−日付クラス.
007600 PROCEDURE       DIVISION
007700                    USING             Ｌ−予約日
007800                    RETURNING         Ｌ−予約日オブジェクト.
007900*
008000*　予約日辞書オブジェクトがNULLなら新規に予約日オブジェクトを生成する。
008100*　予約日辞書オブジェクトがNULLでなければ、予約日をキーにして、予約日に対
008200*　する予約オブジェクトを取得する。
008300*
008400     IF  予約日辞書オブジェクト = NULL THEN
008500       SET 予約日辞書オブジェクト TO  Dict :: "NEW"
008600     ELSE
008700       INVOKE  予約日辞書オブジェクト "Element-Get"
008800                                      USING            Ｘ−予約日
008900                                      RETURNING        共通オブジェクト
009000       IF 共通オブジェクト NOT = NULL THEN
009100         SET   Ｌ−予約日オブジェクト TO 共通オブジェクト AS 予約状態−日付クラス
009200         EXIT  METHOD
009300       END-IF
009400     END-IF
009500*
009600*　予約日が未登録の場合、予約日オブジェクトを生成し予約日辞書オブジェクト
009700*　追加する。
009800*
009900     SET Ｌ−予約日オブジェクト   TO  SELF :: "NEW"
010000     INVOKE  予約日辞書オブジェクト   "Element-PutAt"
010100                                      USING            Ｘ−予約日
010200                                            BY CONTENT Ｌ−予約日オブジェクト
010300*
010400*　予約日オブジェクトが持つ、会議室リストを作成し設定する。
010500*
010600     INVOKE  予約状態−会議室クラス   "会議室オブジェクトの生成"
010700                                      RETURNING        会議室リストオブジェクト
010800	   INVOKE  Ｌ−予約日オブジェクト   "予約日の初期化"
010900                                      USING BY CONTENT Ｌ−予約日
011000                                            BY CONTENT 会議室リストオブジェクト
011100 END METHOD 予約日の参照.
011200*========================================================================
011300*　予約日の全削除メソッド
011400*
011500*　　予約日辞書オブジェクトに登録された予約日オブジェクトを全て削除する。
011600*
011700*  ○入力：なし
011800*　〇出力：なし
011900*========================================================================
012000 METHOD-ID.  予約日の全削除.
012100 DATA            DIVISION.
012200 WORKING-STORAGE SECTION.
012300  01  共通オブジェクト                OBJECT REFERENCE.
012400 LINKAGE         SECTION.
012500 PROCEDURE       DIVISION.
012600     INVOKE  予約日辞書オブジェクト   "FirstElement-Get"
012700                                      RETURNING        共通オブジェクト
012800     PERFORM TEST BEFORE UNTIL 共通オブジェクト = NULL
012900       INVOKE  共通オブジェクト       "予約日の削除"
013000       INVOKE  予約日辞書オブジェクト "NextElement-Get"
013100                                      RETURNING        共通オブジェクト
013200     END-PERFORM
013300     INVOKE 予約日辞書オブジェクト    "Remove-all"
013400     SET   予約日辞書オブジェクト TO  NULL
013500 END METHOD 予約日の全削除.
013600 END FACTORY.
013700*
013800 OBJECT.
013900 DATA DIVISION.
014000 BASED-STORAGE   SECTION.
014100  COPY ROOMINFO.
014200 WORKING-STORAGE SECTION.
014300  01  予約日                          PIC 9(8).
014400  01  会議室リストオブジェクト        OBJECT REFERENCE List.
014500 PROCEDURE       DIVISION.
014600*========================================================================
014700*　予約日の初期化メソッド
014800*
014900*　　予約日の初期情報を設定する。パラメタで渡された予約日と会議室リストオ
015000*　ブジェクトを設定する。
015100*
015200*　〇入力：予約日  (PIC 9(8))
015300*　　　　　会議室リストオブジェクト (OBJECT REFERENCE List)
015400*　〇出力：なし
015500*========================================================================
015600 METHOD-ID.  予約日の初期化.
015700 DATA            DIVISION.
015800 LINKAGE         SECTION.
015900  01 Ｌ−予約日                       PIC 9(8).
016000  01 Ｌ−会議室リストオブジェクト     OBJECT REFERENCE List.
016100 PROCEDURE       DIVISION
016200                    USING             Ｌ−予約日
016300                                      Ｌ−会議室リストオブジェクト.
016400     MOVE Ｌ−予約日              TO  予約日
016500     SET 会議室リストオブジェクト TO  Ｌ−会議室リストオブジェクト
016600 END METHOD 予約日の初期化.
016700*========================================================================
016800*　予約日の削除メソッド
016900*
017000*　　予約日オブジェクトが持つ、会議室リストオブジェクトを削除する。
017100*
017200*  ○入力：なし
017300*　〇出力：なし
017400*========================================================================
017500 METHOD-ID.  予約日の削除.
017600 DATA            DIVISION.
017700 WORKING-STORAGE SECTION.
017800 LINKAGE         SECTION.
017900 PROCEDURE       DIVISION.
018000     INVOKE  予約状態−会議室クラス   "会議室の全削除"
018100                                      USING BY CONTENT 会議室リストオブジェクト
018200     INVOKE  会議室リストオブジェクト "Remove-all"
018300     SET 会議室リストオブジェクト TO  NULL
018400 END METHOD 予約日の削除.
018500**========================================================================
018600*　会議室の予約状況参照メソッド
018700*
018800*　　指定された会議室の予約状況を取得する。
018900*
019000*　〇入力：予約データ (TYPE 予約リスト)
019100*　　　　　会議室データ（TYPE 会議室仕様)
019200*　〇出力：予約状況（PIC 9)
019300*========================================================================
019400 METHOD-ID.  会議室の予約状況参照.
019500 DATA            DIVISION.
019600 BASED-STORAGE   SECTION.
019700     COPY RSVINFO.
019800 WORKING-STORAGE SECTION.
019900  01  会議室オブジェクト              OBJECT REFERENCE  予約状態−会議室クラス.
020000 LINKAGE         SECTION.
020100  01  Ｌ−予約データ                  TYPE 予約リスト.
020200  01  Ｌ−会議室データ                TYPE 会議室仕様.
020300  01  Ｌ−予約状況                    PIC  9.
020400 PROCEDURE       DIVISION
020500                   USING              Ｌ−予約データ
020600                                      Ｌ−会議室データ
020700                   RETURNING          Ｌ−予約状況.
020800 DECLARATIVES.
020900 ERR SECTION.
021000     USE AFTER EXCEPTION エラークラス
021100     INVOKE EXCEPTION-OBJECT          "会議室オブジェクト検索エラー".
021200 END DECLARATIVES.  
021300     INVOKE  予約状態−会議室クラス   "会議室の検索"
021400                                      USING BY CONTENT 会議室名 OF Ｌ−予約データ
021500                                            BY CONTENT 会議室リストオブジェクト
021600                                      RETURNING        会議室オブジェクト
021700*
021800*　該当する会議オブジェクトが見つからない場合、エラークラスでメッセージを
021900*　出力する。該当する会議室オブジェクトが見つかった場合、予約情報を取得する。
022000* 
022100     IF  会議室オブジェクト = NULL THEN
022200       RAISE  エラークラス  :: "NEW"
022300     ELSE
022400       INVOKE  会議室オブジェクト     "時間枠の予約状況参照"
022500                                      USING BY REFERENCE Ｌ−予約データ
022600                                            BY REFERENCE Ｌ−会議室データ
022700                                      RETURNING          Ｌ−予約状況
022800     END-IF
022900 END METHOD 会議室の予約状況参照.
023000*========================================================================
023100*　会議室の予約状況設定メソッド
023200*
023300*　　指定された会議室の予約情報を設定する。
023400*
023500*　〇入力：予約データ (TYPE 予約リスト)
023600*　〇出力：なし
023700*========================================================================
023800 METHOD-ID.  会議室の予約状況設定.
023900 DATA            DIVISION.              
024000 BASED-STORAGE   SECTION.
024100     COPY RSVINFO.
024200 WORKING-STORAGE SECTION.
024300  01  会議室オブジェクト              OBJECT REFERENCE  予約状態−会議室クラス.
024400 LINKAGE         SECTION.
024500  01  Ｌ−予約データ                  TYPE 予約リスト.
024600 PROCEDURE       DIVISION
024700                   USING              Ｌ−予約データ.
024800 DECLARATIVES.
024900 ERR SECTION.
025000     USE AFTER EXCEPTION エラークラス
025100     INVOKE EXCEPTION-OBJECT          "会議室オブジェクト検索エラー".
025200 END DECLARATIVES.  
025300     INVOKE  予約状態−会議室クラス   "会議室の検索"
025400                                      USING BY CONTENT 会議室名 OF Ｌ−予約データ
025500                                            BY CONTENT 会議室リストオブジェクト
025600                                      RETURNING        会議室オブジェクト
025700*
025800*　該当する会議オブジェクトが見つからない場合、エラークラスでメッセージを
025900*　出力する。該当する会議室オブジェクトが見つかった場合、予約情報を設定する。
026000* 
026100     IF  会議室オブジェクト = NULL THEN
026200       RAISE   エラークラス  :: "NEW"
026300     ELSE
026400       INVOKE  会議室オブジェクト     "時間枠の予約状況設定"
026500                                      USING            Ｌ−予約データ
026600     END-IF
026700 END METHOD 会議室の予約状況設定.
026800*========================================================================
026900*　会議室リストオブジェクトの参照メソッド
027000*
027100*　　会議室リストオブジェクトを取得する。
027200*
027300*　〇入力：なし
027400*　〇出力：会議室リストオブジェクト (OBJECT REFERENCE List)
027500*========================================================================
027600 METHOD-ID.  会議室リストオブジェクトの参照.
027700 DATA            DIVISION.
027800 LINKAGE         SECTION.
027900  01 Ｌ−会議室リストオブジェクト     OBJECT REFERENCE List.
028000 PROCEDURE       DIVISION
028100                    RETURNING         Ｌ−会議室リストオブジェクト.
028200     SET Ｌ−会議室リストオブジェクト TO  会議室リストオブジェクト
028300 END METHOD 会議室リストオブジェクトの参照.
028400 END OBJECT.
028500 END CLASS  予約状態−日付クラス.
