000100*========================================================================
000200*　予約状態−時間枠クラス
000300*                                   −サンプルプログラム「会議室予約」−
000400*
000500*　　　時間枠単位の予約状態について固有なデータ及びメソッドを持つ。
000600*
000700*　〇継承クラス
000800*　　１）FJBASE
000900*
001000*　〇参照クラス
001100*　　１）予約情報クラス
001200*
001300*　〇オブジェクトデータ
001400*　　１）予約状況　　　　　　　　−　PIC 9
001500*　　２）時間枠名                −  PIC X(4).
001600*　　３）予約オブジェクト　　　　−　OBJECT REFERENCE 予約情報クラス
001700*
001800*　〇ファクトリメソッド
001900*　　１）時間枠オブジェクトの生成
002000*
002100*　〇オブジェクトメソッド
002200*　　１）時間枠の初期化
002300*　　２）時間枠の削除
002400*　　３）時間枠の予約状況参照
002500*　　４）時間枠の予約データ設定
002600*　　５）時間枠の予約データ参照
002700*
002800*            Copyright 1999-2015 FUJITSU LIMITED
002900*========================================================================
003000 IDENTIFICATION  DIVISION.
003100 CLASS-ID.   予約状態−時間枠クラス  INHERITS FJBASE.
003200 ENVIRONMENT     DIVISION.
003300 CONFIGURATION   SECTION.
003400 SPECIAL-NAMES.
003500     SYMBOLIC CONSTANT
003600     COPY R_CONST.
003700     .
003800 REPOSITORY.
003900     CLASS FJBASE
004000     CLASS 予約情報クラス
004100     .
004200 FACTORY.
004300 DATA            DIVISION.
004400 PROCEDURE       DIVISION.
004500*========================================================================
004600*　時間枠オブジェクトの生成メソッド
004700*
004800*　　時間枠オブジェクトを生成する。
004900*
005000*　〇入力：時間枠 (PIC X(4))
005100*　〇出力：時間枠オブジェクト（OBJECT REFERENCE SELF)
005200*========================================================================
005300 METHOD-ID.  時間枠オブジェクトの生成.
005400 DATA            DIVISION.
005500 WORKING-STORAGE SECTION.
005600 LINKAGE         SECTION.
005700  01 Ｌ−時間枠                       PIC X(4).
005800  01 Ｌ−時間枠オブジェクト           OBJECT REFERENCE SELF.
005900 PROCEDURE       DIVISION
006000                    USING             Ｌ−時間枠
006100                    RETURNING         Ｌ−時間枠オブジェクト.
006200*
006300*　時間枠オブジェクトを生成
006400*
006500     SET  Ｌ−時間枠オブジェクト  TO  SELF :: "NEW"
006600     INVOKE  Ｌ−時間枠オブジェクト   "時間枠の初期化"
006700                                      USING BY CONTENT Ｌ−時間枠
006800 END METHOD 時間枠オブジェクトの生成.
006900 END FACTORY.
007000*
007100 OBJECT.
007200 DATA DIVISION.
007300 BASED-STORAGE   SECTION.
007400  COPY RSVINFO.
007500 WORKING-STORAGE SECTION.
007600  01  時間枠名                        PIC X(4).
007700  01  予約状況                        PIC 9.
007800  01  予約オブジェクト                OBJECT REFERENCE 予約情報クラス.
007900 PROCEDURE       DIVISION.
008000*========================================================================
008100*　時間枠の初期化メソッド
008200*
008300*　　時間枠オブジェクトの初期情報を設定する。時間枠名にはパラメタで渡され
008400*　た時間枠を、予約状況には「空き」を、予約オブジェクトにはNULLを設定する。
008500*
008600*　〇入力：時間枠 (PIC X(4))
008700*　〇出力：なし
008800*========================================================================
008900 METHOD-ID.  時間枠の初期化.
009000 DATA            DIVISION.
009100 LINKAGE         SECTION.
009200  01 Ｌ−時間枠                       PIC X(4).
009300 PROCEDURE       DIVISION
009400                    USING             Ｌ−時間枠.
009500     MOVE  Ｌ−時間枠             TO  時間枠名
009600     MOVE  空き                   TO  予約状況
009700     SET   予約オブジェクト       TO  NULL
009800 END METHOD 時間枠の初期化.
009900*========================================================================
010000*　時間枠の削除メソッド
010100*
010200*　　時間枠オブジェクトが持つ予約オブジェクトを削除する。
010300*
010400*  ○入力：なし
010500*　〇出力：なし
010600*========================================================================
010700 METHOD-ID.  時間枠の削除.
010800 DATA            DIVISION.
010900 WORKING-STORAGE SECTION.
011000 LINKAGE         SECTION.
011100 PROCEDURE       DIVISION.
011200     IF 予約オブジェクト NOT = NULL THEN
011300       INVOKE  予約情報クラス         "予約データの削除"
011400                                      USING BY CONTENT 予約オブジェクト
011500       SET  予約オブジェクト      TO  NULL
011600     END-IF
011700 END METHOD 時間枠の削除.
011800*========================================================================
011900*　時間枠の予約状況参照メソッド
012000*
012100*　　時間枠オブジェクトの予約状況を取得する。
012200*
012300*　〇入力：なし
012400*　〇出力：予約状況 (PIC 9)
012500*========================================================================
012600 METHOD-ID.  時間枠の予約状況参照.
012700 DATA            DIVISION.
012800 LINKAGE         SECTION.
012900  01 Ｌ−予約状況                     PIC 9.
013000 PROCEDURE       DIVISION
013100                    RETURNING         Ｌ−予約状況.
013200     MOVE   予約状況              TO  Ｌ−予約状況
013300 END METHOD 時間枠の予約状況参照.
013400*========================================================================
013500*　時間枠の予約データ設定メソッド
013600*
013700*　　予約番号の情報を元に、予約オブジェクトを生成又は削除する。
013800*　　　
013900*　［予約番号］［状態］　　　　　　　　　［処理］
014000*    −１　　　　予約キャンセル時　　　　　予約データを削除する
014100*　　　０　　　　新規予約時　　　　　　　　予約データを生成／設定する
014200*　　　１以上　　予約データの復元時　　　　予約データを生成／設定する
014300*　　　
014400*
014500*　〇入力：予約データ (TYPE 予約リスト)
014600*　〇出力：なし
014700*========================================================================
014800 METHOD-ID.  時間枠の予約データ設定.
014900 DATA            DIVISION.
015000 LINKAGE         SECTION.
015100  01 Ｌ−予約データ                   TYPE 予約リスト.
015200 PROCEDURE       DIVISION
015300                    USING             Ｌ−予約データ.
015400
015500     IF 予約番号 >= 0 THEN
015600       INVOKE  予約情報クラス         "予約データの生成"
015700                                      USING            Ｌ−予約データ
015800                                      RETURNING        予約オブジェクト
015900       MOVE   予約                TO  予約状況
016000     ELSE
016100       INVOKE 予約情報クラス          "予約データの削除"
016200                                      USING BY CONTENT 予約オブジェクト
016300       SET    予約オブジェクト    TO  NULL
016400       MOVE   空き                TO  予約状況
016500     END-IF
016600 END METHOD 時間枠の予約データ設定.
016700*========================================================================
016800*　時間枠の予約データ参照メソッド
016900*
017000*　　時間枠オブジェクトが持つ予約オブジェクトから予約データを取得する。
017100*
017200*　〇入力：なし
017300*　〇出力：予約データ (TYPE 予約リスト)
017400*========================================================================
017500 METHOD-ID.  時間枠の予約データ参照.
017600 DATA            DIVISION.
017700 LINKAGE         SECTION.
017800  01 Ｌ−予約データ                   TYPE 予約リスト.
017900 PROCEDURE       DIVISION
018000                    RETURNING         Ｌ−予約データ.
018100     INVOKE  予約オブジェクト         "予約データの参照"
018200                                      RETURNING        Ｌ−予約データ
018300 END METHOD 時間枠の予約データ参照.
018400 END OBJECT.
018500 END CLASS  予約状態−時間枠クラス.
