000100*========================================================================
000200*  オブジェクト指向機能サンプルプログラム
000300*
000400*　　　オブジェクト指向の基本的な機能のみを使用した，従業員管理のプログ
000500*　　ラムです。
000600*
000700*　〇機能
000800*　　１．用意されている従業員データより，３名分の従業員オブジェクトを生成
000900*　　　します。
001000*　　２．従業員データのうちの住所情報に対しては，住所オブジェクトを生成し
001100*　　　，その後従業員オブジェクトとのリンクをはります。
001200*  　３．従業員番号の入力要求を行います。
001300*  　４．入力された従業員番号の従業員情報を表示します。
001400*
001500*　〇参照クラス
001600*　　１）従業員クラス　　　　（Employee）
001700*　　２）住所クラス　　　　　（Addr）
001800*
001900*          Copyright 1997-2015 FUJITSU LIMITED
002000*========================================================================
002100 PROGRAM-ID.   Main.
002200 ENVIRONMENT  DIVISION.
002300 CONFIGURATION  SECTION.
002400   REPOSITORY.
002500      CLASS  Addr
002600      CLASS  Employee.
002700*
002800 DATA                   DIVISION.
002900 WORKING-STORAGE        SECTION.
003000   01  従業員番号              PIC 9(04).
003100*
003200   01  オブジェクト.
003300     02  従業員オブジェクト    OBJECT REFERENCE  Employee  OCCURS 3.
003400     02  住所オブジェクト      OBJECT REFERENCE  Addr.
003500*
003600   01  入力番号                PIC 9(1).
003700   01  終了要求                PIC X(1).
003800*
003900   01  従業員データテーブル.
004000     02                        PIC N(08)  VALUE  NC"富士通一郎".
004100     02                        PIC N(08)  VALUE  NC"営業第一課".
004200     02                        PIC X(03)  VALUE    "410".
004300     02                        PIC X(04)  VALUE    "0007".
004400     02                        PIC N(15)  VALUE
004500                         NC"静岡県沼津市北沢田３２８−１１".
004600     02                        PIC N(08)  VALUE  NC"富士通花子".
004700     02                        PIC N(08)  VALUE  NC"総務課".
004800     02                        PIC X(03)  VALUE    "410".
004900     02                        PIC X(04)  VALUE    "0000".
005000     02                        PIC N(15)  VALUE
005100                         NC"静岡県沼津市南沢田３４８−７".
005200     02                        PIC N(08)  VALUE  NC"富士通太郎".
005300     02                        PIC N(08)  VALUE  NC"製造第１部第二課".
005400     02                        PIC X(03)  VALUE    "410".
005500     02                        PIC X(04)  VALUE    "0007".
005600     02                        PIC N(15)  VALUE
005700                         NC"静岡県沼津市北沢田３５５".
005800   01  REDEFINES  従業員データテーブル.
005900     02  従業員データ  OCCURS  3.
006000       03  氏名                  PIC N(08).
006100       03  所属                  PIC N(8).
006200       03  郵便番号.
006300         04  郵便番号１          PIC X(03).
006400         04  郵便番号２          PIC X(04).
006500       03  住所                  PIC N(15).
006600 PROCEDURE                DIVISION.
006700*========================================================================
006800*  オブジェクト生成処理
006900*========================================================================
007000     MOVE  0   TO   従業員番号.
007100     PERFORM  3  TIMES
007200         ADD   1   TO   従業員番号
007300*------------------------------------------------------------------------
007400*  　従業員オブジェクト及び住所オブジェクトを生成する。
007500*------------------------------------------------------------------------
007600         INVOKE  Employee "new"
007700                           returning  従業員オブジェクト(従業員番号)
007800         INVOKE  Addr     "new"
007900                           returning  住所オブジェクト
008000*------------------------------------------------------------------------
008100*  　従業員オブジェクトに，従業員データ及び生成した住所オブジェクトのオブ
008200*  ジェクト参照を設定する。　　　　　　　　　　　　　　　　
008300*------------------------------------------------------------------------
008400         INVOKE  従業員オブジェクト(従業員番号)  "Data-Set"
008500                                          USING   従業員番号
008600                                                  氏名(従業員番号)
008700                                                  所属(従業員番号)
008800                                                  住所オブジェクト
008900*------------------------------------------------------------------------
009000*  　住所オブジェクトに，住所データを設定する。
009100*------------------------------------------------------------------------
009200         INVOKE  住所オブジェクト  "Data-Set"
009300                                          USING   郵便番号(従業員番号)
009400                                                  住所(従業員番号)
009500     END-PERFORM.
009600*========================================================================
009700*  従業員番号入力要求及び従業員データ表示処理
009800*========================================================================
009900     PERFORM  TEST AFTER  UNTIL  終了要求  =  "Y"  or
010000                                 終了要求  =  "y"
010100*------------------------------------------------------------------------
010200*  　従業員番号の入力を要求する。
010300*------------------------------------------------------------------------
010400         DISPLAY  "従業員番号を入力して下さい（1 or 2 or 3）"
010500         ACCEPT    入力番号
010600*------------------------------------------------------------------------
010700*  　入力値が正しいならば，情報を表示するメソッドを呼ぶ。
010800*------------------------------------------------------------------------
010900         IF  入力番号  >=  1   AND   入力番号  <=  3   THEN
011000             INVOKE   従業員オブジェクト(入力番号)  "Data-Display"
011100         ELSE
011200             DISPLAY  "誤った従業員番号が入力されました。"
011300         END-IF
011400*------------------------------------------------------------------------
011500*  　継続ｏｒ終了の選択要求を行う。
011600*------------------------------------------------------------------------
011700         DISPLAY  "        "
011800         DISPLAY  "終了しますか？（Y/N）"
011900         ACCEPT    終了要求
012000         DISPLAY  "        "
012100     END-PERFORM.
012200*
012300     STOP RUN.
012400*
012500 END PROGRAM Main.
