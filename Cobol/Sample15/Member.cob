000100*========================================================================
000200*　従業員クラス（Employee）
000300*　　　　　　　　　　　　　−オブジェクト指向機能サンプルプログラム−
000400*
000500*　　　従業員についてのデータ及びメソッドを持つ。
000600*
000700*　〇継承クラス
000800*　　１）Fjbase
000900*
001000*　〇参照クラス
001100*　　１）住所クラス　　（Addr）
001200*
001300*　〇オブジェクトデータ
001400*　　１）従業員番号　−　PIC  9(4)
001500*　　２）氏名　　　　−　PIC  N(8)
001600*　　３）所属　　　　−　PIC  N(8)
001700*　　４）住所参照　　−　OBJECT REFERENCE Addr
001800*
001900*　〇オブジェクトメソッド
002000*　　１）データ設定　　（Data-Set）
002100*　　２）表示　　　　　（Data-Disp）
002200*
002300*          Copyright 1997-2015 FUJITSU LIMITED
002400*========================================================================
002500 CLASS-ID.   Employee INHERITS  FJBASE.
002600 ENVIRONMENT    DIVISION.
002700 CONFIGURATION    SECTION.
002800 REPOSITORY.
002900      CLASS FJBASE
003000      CLASS Addr.
003100*
003200   OBJECT.
003300   DATA DIVISION.
003400   WORKING-STORAGE SECTION.
003500   01  従業員番号          PIC   9(004).
003600   01  氏名                PIC   N(008).
003700   01  所属                PIC   N(008).
003800   01  住所参照            OBJECT  REFERENCE  Addr.
003900   PROCEDURE DIVISION.
004000*========================================================================
004100*　データ設定
004200*
004300*　　渡された従業員データを，オブジェクトデータに設定する。
004400*
004500*　〇入力：　従業員番号    PIC   9(004)
004600*　　　　　　氏名　　　    PIC   N(008)
004700*　　　　　　所属　　　    PIC   N(008)
004800*　　　　　　住所参照　　  OBJECT  REFERENCE  Addr
004900*========================================================================
005000   METHOD-ID.  Data-Set.
005100   DATA DIVISION.
005200   LINKAGE SECTION.
005300    01  Ｌ−従業員番号     PIC   9(004).
005400    01  Ｌ−氏名           PIC   N(008).
005500    01  Ｌ−所属           PIC   N(008).
005600    01  Ｌ−住所参照       OBJECT  REFERENCE  Addr.
005700   PROCEDURE   DIVISION    USING   Ｌ−従業員番号
005800                                   Ｌ−氏名
005900                                   Ｌ−所属
006000                                   Ｌ−住所参照.
006100*
006200       MOVE  Ｌ−従業員番号   TO  従業員番号.
006300       MOVE  Ｌ−氏名         TO  氏名.
006400       MOVE  Ｌ−所属         TO  所属.
006500       SET   住所参照         TO  Ｌ−住所参照.
006600*
006700   END METHOD Data-Set.
006800*
006900*========================================================================
007000*　データ表示
007100*
007200*　　従業員データを表示する。
007300*========================================================================
007400   METHOD-ID.  Data-Display.
007500   DATA DIVISION.
007600   WORKING-STORAGE SECTION.
007700    01  従業員データ.
007800      02  Ｗ−従業員番号         PIC 9(04).
007900      02  FILLER                 PIC X(1)  VALUE SPACE.
008000      02  Ｗ−氏名               PIC N(08).
008100      02  FILLER                 PIC X(1)  VALUE SPACE.
008200      02  Ｗ−所属               PIC N(08).
008300      02  FILLER                 PIC X(1)  VALUE SPACE.
008400      02  Ｗ−郵便番号.
008500        03  Ｗ−郵便番号１       PIC X(03).
008600        03  FILLER               PIC X(1)  VALUE "-".
008700        03  Ｗ−郵便番号２       PIC X(04).
008800      02  FILLER                 PIC X(1)  VALUE SPACE.
008900      02  Ｗ−住所               PIC N(15).
009000    01  獲得住所データ.
009100      02  郵便番号               PIC X(07).
009200      02  住所                   PIC N(15).
009300   CONSTANT    SECTION.
009400    01  見出し.
009500      02  FILLER                 PIC X(05)  VALUE  "NO.--".
009600      02  FILLER                 PIC X(17)  VALUE  "氏名-------------".
009700      02  FILLER                 PIC X(17)  VALUE  "所属-------------".
009800      02  FILLER                 PIC X(09)  VALUE  "〒-------".
009900      02  FILLER                 PIC X(30)  VALUE
010000                             "住所--------------------------".
010100   LINKAGE     SECTION.
010200   PROCEDURE   DIVISION.
010300*------------------------------------------------------------------------
010400*  　オブジェクトデータの情報を，表示用作業域に格納する。
010500*------------------------------------------------------------------------
010600       MOVE  従業員番号    TO   Ｗ−従業員番号.
010700       MOVE  氏名          TO   Ｗ−氏名.
010800       MOVE  所属          TO   Ｗ−所属.
010900*------------------------------------------------------------------------
011000*  　住所オブジェクトよりデータを獲得し，表示用作業域に格納する。
011100*------------------------------------------------------------------------
011200       INVOKE  住所参照  "Data-Get"  RETURNING  獲得住所データ.
011300       MOVE  郵便番号(1:3)  TO   Ｗ−郵便番号１.
011400       MOVE  郵便番号(4:4)  TO   Ｗ−郵便番号２.
011500       MOVE  住所           TO   Ｗ−住所.
011600*------------------------------------------------------------------------
011700*  　従業員データを表示する。
011800*------------------------------------------------------------------------
011900       DISPLAY  "       ".
012000       DISPLAY  見出し.
012100       DISPLAY  従業員データ.
012200*
012300   END METHOD Data-Display.
012400*
012500   END OBJECT.
012600  END CLASS Employee.
