000100*========================================================================
000200*　従業員クラス（AllMember-class）　−サンプルプログラム「従業員管理」−
000300*
000400*　　　全ての従業員について共通なデータ及びメソッドを持つ。
000500*
000600*　〇継承クラス
000700*　　１）Fjbase
000800*
000900*　〇オブジェクトデータ
001000*　　１）従業員番号　−　PIC  9(4)
001100*　　２）氏名　　　　−　PIC  N(8)
001200*　　３）入社年月日　−　PIC  X(8)
001300*　　４）退社年月日　−　PIC  X(8)
001400*　　５）職級　　　　−　PIC  9(1)
001500*　　６）基本給　　　−　PIC  9(8)
001600*　　７）総支給　　　−　PIC  9(8)
001700*　　８）住所参照　　−　object reference Address-class
001800*
001900*　〇オブジェクトメソッド
002000*　　１）オブジェクトデータに対する，GET及びSETのプロパティメソッド
002100*　　２）給与計算
002200*　　３）賞与計算
002300*　　４）在籍チェック
002400*                              Copyright 1997-2015 FUJITSU LIMITED
002500*========================================================================
002600 CLASS-ID.   AllMember-class INHERITS  FJBASE.
002700 ENVIRONMENT      DIVISION.
002800 CONFIGURATION    SECTION.
002900 REPOSITORY.
003000     CLASS FJBASE
003100     CLASS Address-class.
003200*
003300   OBJECT.
003400   DATA DIVISION.
003500   WORKING-STORAGE SECTION.
003600   01  従業員番号          PIC   9(004).
003700   01  氏名                PIC   N(008).
003800   01  入社年月日          PIC   X(008).
003900   01  退社年月日          PIC   X(008).
004000   01  職級                PIC   9(001).
004100   01  基本給              PIC   9(008).
004200   01  総支給              PIC   9(008) PROPERTY.
004300   01  住所参照            OBJECT  REFERENCE  Address-class PROPERTY.
004400   PROCEDURE DIVISION.
004500*=======================================================================
004600*　初期化メソッド（Initial-method）
004700*
004800*　〇出力：　総支給（PIC 9(8)）
004900*=======================================================================
005000  METHOD-ID.  Initial-method.
005100  DATA DIVISION.
005200  LINKAGE SECTION.
005300   01  Ｌ−従業員番号          PIC   9(004).
005400   01  Ｌ−氏名                PIC   N(008).
005500   01  Ｌ−入社年月日          PIC   X(008).
005600   01  Ｌ−退社年月日          PIC   X(008).
005700   01  Ｌ−職級                PIC   9(001).
005800   01  Ｌ−基本給              PIC   9(008).
005900   01  Ｌ−住所参照            OBJECT  REFERENCE  Address-class.
006000  PROCEDURE DIVISION  USING
006100     Ｌ−従業員番号
006200     Ｌ−氏名
006300     Ｌ−入社年月日
006400     Ｌ−退社年月日
006500     Ｌ−職級
006600     Ｌ−基本給
006700     Ｌ−住所参照.
006800*
006900     MOVE  Ｌ−従業員番号  TO  従業員番号
007000     MOVE  Ｌ−氏名        TO  氏名
007100     MOVE  Ｌ−入社年月日  TO  入社年月日
007200     MOVE  Ｌ−退社年月日  TO  退社年月日
007300     MOVE  Ｌ−職級        TO  職級
007400     MOVE  Ｌ−基本給      TO  基本給
007500     SET   住所参照        TO  Ｌ−住所参照
007600*
007700  END METHOD  Initial-method.
007800*
007900*=======================================================================
008000*　メンバー情報獲得メソッド（Get-member-info）
008100*
008200*　〇出力：　総支給（PIC 9(8)）
008300*=======================================================================
008400  METHOD-ID.  Get-member-info.
008500  DATA DIVISION.
008600  LINKAGE SECTION.
008700   01  Ｌ−氏名                PIC   N(008).
008800   01  Ｌ−入社年月日          PIC   X(008).
008900   01  Ｌ−退社年月日          PIC   X(008).
009000   01  Ｌ−職級                PIC   9(001).
009100   01  Ｌ−基本給              PIC   9(008).
009200   01  Ｌ−住所参照            OBJECT  REFERENCE  Address-class.
009300  PROCEDURE DIVISION  USING
009400     Ｌ−氏名
009500     Ｌ−入社年月日
009600     Ｌ−退社年月日
009700     Ｌ−職級
009800     Ｌ−基本給
009900     Ｌ−住所参照.
010000*
010100     MOVE  氏名           TO  Ｌ−氏名
010200     MOVE  入社年月日     TO  Ｌ−入社年月日
010300     MOVE  退社年月日     TO  Ｌ−退社年月日
010400     MOVE  職級           TO  Ｌ−職級
010500     MOVE  基本給         TO  Ｌ−基本給
010600     SET   Ｌ−住所参照   TO  住所参照
010700*
010800  END METHOD  Get-member-info.
010900*
011000*=======================================================================
011100*　メンバー情報獲得メソッド（Get-print-info）
011200*
011300*　〇出力：　総支給（PIC 9(8)）
011400*=======================================================================
011500  METHOD-ID.  Get-print-info.
011600  DATA DIVISION.
011700  LINKAGE SECTION.
011800   01  Ｌ−従業員番号          PIC   9(004).
011900   01  Ｌ−氏名                PIC   N(008).
012000   01  Ｌ−職級                PIC   9(001).
012100   01  Ｌ−住所参照            OBJECT  REFERENCE  Address-class.
012200  PROCEDURE DIVISION  USING
012300     Ｌ−従業員番号
012400     Ｌ−氏名
012500     Ｌ−職級
012600     Ｌ−住所参照.
012700*
012800     MOVE  従業員番号     TO  Ｌ−従業員番号
012900     MOVE  氏名           TO  Ｌ−氏名
013000     MOVE  職級           TO  Ｌ−職級
013100     SET   Ｌ−住所参照   TO  住所参照
013200*
013300  END METHOD  Get-print-info.
013400*
013500*=======================================================================
013600*　給与計算メソッド（Salary-method）
013700*
013800*　〇出力：　総支給（PIC 9(8)）
013900*=======================================================================
014000  METHOD-ID.  Salary-method.
014100  DATA DIVISION.
014200  LINKAGE SECTION.
014300  01  Ｌ−総支給         PIC   9(008).
014400  PROCEDURE  DIVISION    RETURNING   Ｌ−総支給.
014500     MOVE  基本給  TO  Ｌ−総支給
014600  END METHOD  Salary-method.
014700*
014800*========================================================================
014900*　賞与計算メソッド（Bonus-method）
015000*
015100*　〇出力：　総支給（PIC 9(8)）
015200*========================================================================
015300  METHOD-ID.  Bonus-method.
015400  DATA DIVISION.
015500  LINKAGE SECTION.
015600   01  Ｌ−総支給        PIC   9(008).
015700  PROCEDURE  DIVISION    RETURNING   Ｌ−総支給.
015800     COMPUTE  Ｌ−総支給  =  基本給  *  2.5
015900  END METHOD  Bonus-method.
016000*
016100*========================================================================
016200*　在籍チェックメソッド（IOCheck-method）
016300*
016400*　〇出力：　在籍区分（PIC X(1)）
016500*========================================================================
016600  METHOD-ID.  IOCheck-method.
016700  DATA DIVISION.
016800  LINKAGE SECTION.
016900   01  在籍区分          PIC   X(001).
017000  PROCEDURE  DIVISION    RETURNING   在籍区分.
017100*
017200      IF   退社年月日     =     SPACE
017300      THEN
017400           MOVE     "0"         TO     在籍区分
017500      ELSE
017600           MOVE     "1"         TO     在籍区分
017700      END-IF.
017800*
017900  END METHOD IOCheck-method.
018000*
018100  END OBJECT.
018200 END CLASS AllMember-class.
