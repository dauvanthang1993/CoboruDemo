000010*>
000020 IDENTIFICATION DIVISION.
000030 PROGRAM-ID.   Example3.
000040 ENVIRONMENT    DIVISION.
000050 CONFIGURATION  SECTION.
000051 INPUT-OUTPUT SECTION.
000052 FILE-CONTROL.
000053 		SELECT PRODUCT-FILE ASSIGN TO "PRODUCT_MASTER.txt"
000054 		ORGANIZATION IS LINE SEQUENTIAL.
000055 		
000056 		SELECT DATA-FILE ASSIGN TO "DATA_FILE.txt"
000057 		ORGANIZATION IS LINE SEQUENTIAL.
000058 		
000059 		SELECT LIST-FILE ASSIGN TO "LIST_PRODUCT.txt"
000060 		ORGANIZATION IS LINE SEQUENTIAL.
000061 		
000062 		SELECT NEW-PRODUCT-FILE ASSIGN TO "NEW_PRODUCT.txt"
000063 		ORGANIZATION IS LINE SEQUENTIAL.
000064 DATA DIVISION.
000065 FILE SECTION.
000066*******OLD在庫マスタ*********
000067 FD PRODUCT-FILE.
000068 01 PRODUCT.
000069 		02 ID-P				PIC X(5).
000070 		02 NAME-P			PIC X(15).
000071 		02 AMOUNT-P	 		PIC 9(4).
000072 		02 AMOUNT-SAFE		PIC 9(4).
000073********変動データ************* 		
000074 FD DATA-FILE.
000075 01 CHANGE-DATA.
000076 		02 ID-DATA			PIC X(5).
000077 		02 AMOUNT-IN		PIC 9(4).
000078 		02 AMOUNT-OUT		PIC 9(4).	
000079******在庫リスト*********** 		
000080 FD LIST-FILE.
000081 01 LIST-PRODUCT.
000082 		02 	LIST-WRITE 		PIC X(120).
000083*******新在庫マスト********* 									
000084 FD NEW-PRODUCT-FILE.
000085 01 NEW-PRODUCT.
000086 		02 WRITE-PRODUCT	PIC X(80). 									
000087 WORKING-STORAGE SECTION.
000088 01 CHECK.
000089 		02 CHECK-EOF-P		PIC X(2).
000090 		02 CHECK-EOF-D		PIC X(2).
000091 		02 CHECK-DATA-USE	PIC X.
000092 01 AMOUNT-NEW			PIC 9(4)	VALUE ZERO.
000095 
000096 01 COUNT-RECORD-PAGE		PIC 99		VALUE ZERO.
000097
000098******在庫リスト編集印字******* 
000099 01 DISPLAY-PRODUCT-LIST.
000100 		02 					PIC X(9)	VALUE SPACE.
000101 		02 ID-P-L			PIC X(5).
000102 		02					PIC X(5)	VALUE SPACE.
000103 		02 NAME-P-L			PIC X(15).
000104 		02 					PIC X(5) 	VALUE SPACE.
000105 		02 AMOUNT-P-L		PIC z,zz9.
000106 		02					PIC X(7) 	VALUE SPACE.
000107 		02 AMOUNT-IN-L		PIC z,zz9	BLANK WHEN ZERO.
000108 		02					PIC X(6)	VALUE SPACE.
000109 		02 AMOUNT-OUT-L		PIC z,zz9	BLANK WHEN ZERO.
000110 		02					PIC X(7)	VALUE SPACE.
000111 		02 AMOUNT-NEW-L		PIC z,zz9	VALUE SPACE.
000112 		02					PIC X(7)	VALUE SPACE.
000113 		02 AMOUNT-SAFE-L	PIC z,zz9	BLANK WHEN ZERO.
000114 		02					PIC X(8)	VALUE SPACE.
000115 		02 ERROR-P-L		PIC X(3)	VALUE SPACE.
000116*****在庫リストのタイトルの編集印字***** 		
000117 01 DISPLAY-LIST-TITLE.
000118 		02					PIC X(9)	VALUE SPACE.
000119 		02					PIC X(2)	VALUE "ID".
000120 		02					PIC X(8)	VALUE SPACE.			
000121 		02					PIC X(4)	VALUE "NAME".
000122 		02					PIC X(14)	VALUE SPACE.
000123 		02					PIC X(10)	VALUE "OLD_AMOUNT".
000124 		02					PIC X(2)	VALUE SPACE.	
000125 		02					PIC X(9)	VALUE "IN_AMOUNT".
000126 		02					PIC X(2)	VALUE SPACE.
000127 		02					PIC X(10)	VALUE "OUT_AMOUNT".
000128 		02					PIC X(2)	VALUE SPACE.
000129 		02					PIC X(10)	VALUE "NEW_AMOUNT".
000130 		02					PIC X(2)	VALUE SPACE.
000131 		02					PIC X(11)	VALUE "SAFE_AMOUNT".
000132 		02					PIC X(2)	VALUE SPACE.
000133 		02					PIC X(5)	VALUE "ERROR".
000134 		
000135 01 DISPLAY-LIST-FILE.
000136 		02					PIC X(40)	VALUE SPACE.
000137 		02					PIC X(35)	VALUE "* * * INVENTORY LIST * * *".			
000138*******新在庫の編集印字******* 		
000139 01 DISPLAY-DATA-LIST.
000140 		02 					PIC X(8)	VALUE SPACE.
000141 		02					PIC X		VALUE "(".
000142 		02 DATA-LIST		PIC X(13).
000143 		02					PIC X		VALUE ")".					
000144  
000145 PROCEDURE DIVISION.
000146     OPEN INPUT PRODUCT-FILE DATA-FILE
000147     OPEN OUTPUT NEW-PRODUCT-FILE LIST-FILE
000148     MOVE 99 TO COUNT-RECORD-PAGE
000149     PERFORM PRODUCT-INPUT
000150     PERFORM DATA-INPUT
000151***********************     
000152     PERFORM UNTIL CHECK-EOF-P = HIGH-VALUE
000153******変動データのエンドのチェック*******
000154********変動データのエンド=?HIGH-VALUE残るOLD在庫データの入力************     
000155     		IF CHECK-EOF-D = HIGH-VALUE THEN
000156     		   MOVE "F" TO CHECK-DATA-USE
000157     		   PERFORM WRITE-LIST-PRODUCT
000158     		   PERFORM WRITE-NEW-PRODUCT
000159     		   PERFORM PRODUCT-INPUT
000160     		ELSE
000161*******OLD商品コード<変動データコード*********     		
000162     		   IF ID-P < ID-DATA THEN
000163     		   		MOVE "F" TO CHECK-DATA-USE
000164     		  		PERFORM WRITE-LIST-PRODUCT
000165     		  		PERFORM WRITE-NEW-PRODUCT
000166     		  		PERFORM PRODUCT-INPUT
000167     		   END-IF
000168*******OLD商品コード　=　変動データコード******     		   
000170     		   IF ID-P = ID-DATA THEN
000171     		   	   MOVE "T" TO CHECK-DATA-USE
000172     		       PERFORM WRITE-LIST-PRODUCT
000173     		       PERFORM WRITE-NEW-PRODUCT
000174     		       PERFORM PRODUCT-INPUT
000175     		       PERFORM DATA-INPUT
000176     		   END-IF
000177*******OLD商品コード　>　変動データコード*****     		   
000178     		   IF ID-P > ID-DATA THEN
000179     		   		PERFORM WRITE-LIST-DATA		
000180     		   		PERFORM DATA-INPUT
000181     		   END-IF
000182     		   
000183     		END-IF
000184     		   
000185     END-PERFORM
000186*******OLD在庫マスタの入力ができた。しかし、変動データの入力はまだできない時、残る変動データを入力する**********     
000187     PERFORM UNTIL CHECK-EOF-D = HIGH-VALUE
000188     		PERFORM WRITE-LIST-DATA
000189     		PERFORM DATA-INPUT
000190     END-PERFORM
000191		     
000192     CLOSE PRODUCT-FILE DATA-FILE NEW-PRODUCT-FILE LIST-FILE
000193     STOP RUN.
000194     
000195******OLD商品コードの入力********     
000196 PRODUCT-INPUT.
000197 		READ PRODUCT-FILE
000198		        AT END 
000199		         	MOVE HIGH-VALUE TO CHECK-EOF-P
000200		END-READ.
000201 END-PRODUCT-INPUT.
000202 
000203*****変動データの入力******
000204 DATA-INPUT.
000205 		READ DATA-FILE
000206		        AT END 
000207		         	MOVE HIGH-VALUE TO CHECK-EOF-D
000208		END-READ.
000209 END-DATA-INPUT.  
000210*****詳細な在庫リストの編集印字******* 		
000211 WRITE-LIST-PRODUCT.
000212 		PERFORM WRITE-LIST-TITLE
000213	 		IF CHECK-DATA-USE = "T" THEN
000214	 		    COMPUTE AMOUNT-NEW = AMOUNT-P + AMOUNT-IN
000215	 		IF AMOUNT-NEW <= AMOUNT-OUT THEN
000216	 		    MOVE 0 TO AMOUNT-NEW
000217	 		ELSE
000218	 		    COMPUTE AMOUNT-NEW = AMOUNT-NEW - AMOUNT-OUT
000219	 		END-IF
000220	 		MOVE AMOUNT-IN TO AMOUNT-IN-L
000221 			MOVE AMOUNT-OUT TO AMOUNT-OUT-L
000222 		ELSE
000223 			MOVE AMOUNT-P TO AMOUNT-NEW
000224 		    MOVE 0 TO AMOUNT-IN-L
000225 		    MOVE 0 TO AMOUNT-OUT-L
000226 		END-IF
000227 		
000228 		MOVE ID-P TO ID-P-L
000229 		MOVE NAME-P TO NAME-P-L
000230 		MOVE AMOUNT-P TO AMOUNT-P-L
000231 
000232 		
000236 		
000238 		MOVE AMOUNT-SAFE TO AMOUNT-SAFE-L
000239 		MOVE AMOUNT-NEW TO AMOUNT-NEW-L
000240 		
000241 		IF AMOUNT-NEW < AMOUNT-SAFE THEN
000242 		    MOVE "-*-"	TO ERROR-P-L
000243 		ELSE
000244 		    MOVE SPACE TO ERROR-P-L
000245 		END-IF
000246 
000247 		WRITE LIST-PRODUCT FROM DISPLAY-PRODUCT-LIST AFTER 2 LINE
000248 		COMPUTE COUNT-RECORD-PAGE = COUNT-RECORD-PAGE + 1.
000249 END-WRITE-LIST-PRODUCT.   
000250* 
000251****商品リストに変動データだけの編集印字*****
000252 WRITE-LIST-DATA.
000253 		PERFORM WRITE-LIST-TITLE
000254 		MOVE CHANGE-DATA TO DATA-LIST
000255 		WRITE LIST-PRODUCT FROM DISPLAY-DATA-LIST AFTER 2 LINE
000256 		COMPUTE COUNT-RECORD-PAGE = COUNT-RECORD-PAGE + 1.
000257 END-WRITE-LIST-DATA.
000258*
000259****詳細な新商品データの編集印字******
000260 WRITE-NEW-PRODUCT.
000261 		MOVE AMOUNT-NEW TO AMOUNT-P
000262 		WRITE NEW-PRODUCT FROM PRODUCT.
000263 END-WRITE-NEW-PRODUCT.
000264* 
000265*****改ページ*****
000266 WRITE-LIST-TITLE.
000267 		IF COUNT-RECORD-PAGE >= 20 THEN
000268 			MOVE 0 TO COUNT-RECORD-PAGE
000269 			MOVE SPACE TO LIST-PRODUCT
000270 			WRITE LIST-PRODUCT AFTER PAGE
000271 			WRITE LIST-PRODUCT FROM DISPLAY-LIST-FILE AFTER 5 LINE
000272 			WRITE LIST-PRODUCT FROM DISPLAY-LIST-TITLE AFTER 2 LINE
000273 		END-IF.
000274 END-WRITE-LIST-TITLE.	
000279 END PROGRAM Example3.